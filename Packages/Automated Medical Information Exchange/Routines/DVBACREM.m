DVBACREM ;ALB/RTW - REMARKS SENT TO CONTRACTORS;05/27/2015
 ;;2.7;AMIE;**192**;Apr 10, 1995;Build 15
 ; At WPGET This routine gets remarks sent to contractors,(from global 396.4),for a 2507 Exam Reqiest
 ; At WPSET This routine sets remarks sent to contractors,(from global 396.4),for a 2507 Exam Request 
 Q  ;no direct entry
 ;
WPGET(DVBADATA,DVBEIEN) ;
 ;RPC: DVBA CAPRI GETCONTREMARKS
 N DVBCR
 K ^TMP("DVBA",$J)
 S X=0
 S DVBCR=0 F  S DVBCR=$O(^DVB(396.4,DVBEIEN,6,DVBCR)) Q:'DVBCR  D
 . S DVBARMKS=$G(^DVB(396.4,DVBEIEN,6,DVBCR,0))
 . S ^TMP("DVBA",$J,DVBCR)=DVBARMKS
 S DVBCNT=0,DVBCNA=0 F  S DVBCNA=$O(^TMP("DVBA",$J,DVBCNA)) Q:DVBCNA=""  D
 . S DVBAREM=$G(^TMP("DVBA",$J,DVBCNA))
 . S DVBADATA(DVBCNT)=DVBAREM
 . S DVBCNT=DVBCNT+1
 K DVBCA,DVBCNT,DVBAREM,DVBARMKS,X
 S RESULT=$NA(^TMP("DVBA",$J))
 Q
 ;
WPSET(RESULT,DVBRIEN,DVBEIEN,DVBDATA) ;RPC: DVBA CAPRI SETCONTREMARKS
 ;DVBEIEN exam ien(s) example ** DVBEIEN="12736^12737^12738"
 ;DVBPEIN is the individual IENs pieced out.
 K ^TMP("DVBA",$J)
 S DVBPIEN=0 F DVBI=1:1 S DVBPIEN=$P(DVBEIEN,U,DVBI) Q:'DVBPIEN  D
 . D WPDATA
 K DVBI,DVBRIEN,DVBPIEN,DVBCNT,DVBCNA
 Q
WPDATA ;
 S DVBLINE="",DVBCNT=1 F  S DVBLINE=$O(DVBDATA(DVBLINE)) Q:DVBLINE=""  D
 .S ^TMP("DVBA",$J,"WP",DVBCNT)=$G(DVBDATA(DVBLINE))
 .S DVBCNT=DVBCNT+1
 S D0=DVBPIEN
 D WP^DIE(396.4,DVBPIEN,103,"K","^TMP(""DVBA"",$J,""WP"")")
 S RESULT=$NA(^TMP("DVB",$J))
 K D0,DVBLINE,DVBCNT
 Q
