DVBA189P ;ALB/GAK - PATCH DVBA*2.7*189 POST-INSTALL ROUTINE;08/08/2014
 ;;2.7;AMIE;**189**;Apr 10, 1995;Build 22
 ; This routine adds an entry to the REMOTE APPLICATION file (#8994.5) for VLER DAS-CAPRI
 ; ALB/RTW -This routine adds new and hides old 2507 Cancelation Reasons in global (#396.5) for VLER DAS-CAPRI 
 Q
 ;
ENTER ;
 D INRSN
 D SPCON
 D CLMTYP
 D CANRSN
 Q
 ;
 ;
INRSN ;SETUP THE INSUFFICIENT REASON FILE #396.94
 ;HIDE REQUESTED INSUFFICIENT REASONS
 N DVBAI,DVBLINE,FIND,OUT,IRIEN,RSN,CODE,REPORT
 F DVBAI=1:1 S DVBLINE=$P($T(IRHIDE+DVBAI),";;",2) Q:DVBLINE="QUIT"  D
 . K FIND,OUT
 . S FIND=DVBLINE
 . D FIND^DIC(396.94,"",.01,"X",.FIND,"","","","","OUT")
 . S IRIEN=$G(OUT("DILIST",2,1))
 . I IRIEN="" D BMES^XPDUTL("INSUFFICIENT REASON NOT ON FILE TO HIDE: "_DVBLINE) Q
 . I '$D(^DVB(396.94,IRIEN)) D BMES^XPDUTL("INSUFFICIENT REASON NOT ON FILE TO HIDE: "_DVBLINE) Q
 . ;
 . Q:$P(^DVB(396.94,IRIEN,0),"^",4)'=""
 . ;
 . K FDA,ERR,KEYIEN
 . S FDA(396.94,IRIEN_",",1)="Y"
 . S KEYIEN(1)=IRIEN
 . D UPDATE^DIE("","FDA","KEYIEN","ERR")
 . I $G(ERR)'="" D BMES^XPDUTL("INSUFFICIENT REASON: "_DVBLINE_" COULD NOT BE DEACTIVATED BY POST-INSTALL ROUTINE. PLEASE SET INACTIVE FIELD TO YES.") Q
 . D BMES^XPDUTL("INSUFFICIENT REASON: "_DVBLINE_" HAS BEEN DEACTIVATED.") Q
 ;
 ;NEW REQUESTED INSUFFICIENT REASONS
 N DVBAI,DVBLINE,FIND,OUT
 F DVBAI=1:1 S DVBLINE=$P($T(IRNEW+DVBAI),";;",2) Q:DVBLINE="QUIT"  D
 . K FIND,OUT
 . S FIND=$TR($P(DVBLINE,"^",1),"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ")
 . D FIND^DIC(396.94,"",.01,"X",.FIND,"","","","","OUT")
 . I $G(OUT("DILIST",2,1))'="" D BMES^XPDUTL("INSUFFICIENT REASON: "_FIND_" HAS ALREADY BEEN ADDED.") D  Q
 . . S DVBABIEN=OUT("DILIST","2",1)
 . . S DVBAFDA(396.94,DVBABIEN_",",1)="N"
 . . D FILE^DIE("","DVBAFDA","DVBAERR")
 . K RSN,CODE,REPORT
 . S RSN=$TR($P(DVBLINE,"^",1),"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ")
 . S CODE=$P(DVBLINE,"^",2)
 . S REPORT=$P(DVBLINE,"^",3)
 . K FDA,ERR
 . S FDA(396.94,"+1,",.01)=RSN
 . S FDA(396.94,"+1,",.02)=CODE
 . S FDA(396.94,"+1,",.03)=REPORT
 . S FDA(396.94,"+1,",1)="N"
 . D UPDATE^DIE("","FDA","","ERR")
 . I $G(ERR)'="" D BMES^XPDUTL("INSUFFICIENT REASON: "_RSN_" COULD NOT BE ADDED BY POST-INSTALL ROUTINE.") Q
 . D BMES^XPDUTL("INSUFFICIENT REASON: "_RSN_" HAS BEEN ADDED.") Q
 ;
 Q
 ;
IRHIDE ;INACTIVATE THE FOLLOWING REASONS
 ;;DIAGNOSIS INCONSISTENT WITH EVIDENCE
 ;;EXAM PREVIOUSLY RETURNED AS INSUFFICIENT, RETURN AGAIN INSUFFICIENT
 ;;EYE EXAM IS INSUFFICIENT
 ;;FAILED TO PERFORM REQUESTED SPECIALIST EXAM
 ;;FAILED TO PROVIDE ADDITIONAL EXAM RECOMMENDED BY EXAMINING PHYSICIAN
 ;;FAILED TO PROVIDE RANGE OF MOTION
 ;;FAILED TO PROVIDE REQUESTED MEDICAL OPINION
 ;;FAILED TO PROVIDE TEST ROUTINELY DONE FOR SPECIFIC CONDITION
 ;;FAILED TO REVIEW TEST RESULTS BEFORE MAKING DIAGNOSIS
 ;;OTHER
 ;;QUIT
 Q
 ;
IRNEW ;ADD THE FOLLOWING NEW REASONS
 ;;Failed to provide requested/required information^FRI^Failed to provide requested/required information
 ;;Failed to provide medical opinion^FMO^Failed to provide medical opinion
 ;;Failed to comply with BVA Remand Instructions^FBVA^Failed to comply with BVA Remand Instructions
 ;;Failed to provide diagnosis^FPD^Failed to provide diagnosis
 ;;Failed to provide rationale^FPR^Failed to provide rationale
 ;;Conflicting information in report^CIR^Conflicting information in report
 ;;Failed to address all conditions requested^FAC^Failed to address all conditions requested
 ;;In-person examination was not performed^IPN^In-person examination was not performed
 ;;Case required exam by a specialist^CRS^Case required exam by a specialist
 ;;QUIT
 Q
 ;
 ;
SPCON ;SETUP THE 2507 SPECIAL CONSIDERATIONS FILE #396.25
 ;NEW REQUESTED SPECIAL CONSIDERATIONS
 N DVBAI,DVBLINE,DVBLINE,FIND,OUT
 F DVBAI=1:1 S DVBLINE=$P($T(SCNEW+DVBAI),";;",2) Q:DVBLINE="QUIT"  D
 . K FIND,OUT
 . S FIND=$TR(DVBLINE,"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ")
 . D FIND^DIC(396.25,"",.01,"X",.FIND,"","","","","OUT")
 . I $G(OUT("DILIST",2,1))'="" D BMES^XPDUTL("SPECIAL CONSIDERATION: "_FIND_" HAS ALREADY BEEN ADDED.") Q
 . K FDA,ERR
 . S DVBLINE=$TR(DVBLINE,"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ")
 . S FDA(396.25,"+1,",.01)=DVBLINE
 . D UPDATE^DIE("","FDA","","ERR")
 . I $G(ERR)'="" D BMES^XPDUTL("SPECIAL CONSIDERATION: "_DVBLINE_" COULD NOT BE ADDED BY POST-INSTALL ROUTINE.") Q
 . D BMES^XPDUTL("SPECIAL CONSIDERATION: "_DVBLINE_" HAS BEEN ADDED.") Q
 ;
 Q
 ;
SCNEW ;ADD THE FOLLOWING NEW SPECIAL CONSIDERATIONS
 ;;AGE OF CLAIM
 ;;FDC
 ;;HOMELESS
 ;;NOT APPLICABLE
 ;;POW
 ;;TERMINAL
 ;;QUIT
 Q
 ;
 ;
CLMTYP ;SETUP THE 2507 CLAIM TYPE FILE #396.27
 ;NEW REQUESTED CLAIM TYPES
 N DVBAI,DVBLINE,DVBLINE,FIND,OUT
 F DVBAI=1:1 S DVBLINE=$P($T(CTNEW+DVBAI),";;",2) Q:DVBLINE="QUIT"  D
 . K FIND,OUT
 . S FIND=$TR(DVBLINE,"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ")
 . D FIND^DIC(396.27,"",.01,"X",.FIND,"","","","","OUT")
 . I $G(OUT("DILIST",2,1))'="" D BMES^XPDUTL("CLAIM TYPE: "_FIND_" HAS ALREADY BEEN ADDED.") Q
 . K FDA,ERR
 . S DVBLINE=$TR(DVBLINE,"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ")
 . S FDA(396.27,"+1,",.01)=DVBLINE
 . D UPDATE^DIE("","FDA","","ERR")
 . I $G(ERR)'="" D BMES^XPDUTL("CLAIM TYPE: "_DVBLINE_" COULD NOT BE ADDED BY POST-INSTALL ROUTINE.") Q
 . D BMES^XPDUTL("CLAIM TYPE: "_DVBLINE_" HAS BEEN ADDED.") Q
 ;
 Q
 ;
CTNEW ;ADD THE FOLLOWING NEW CLAIM TYPES
 ;;BDD
 ;;BDD - DOD SHA
 ;;IDES
 ;;INCREASE ONLY
 ;;ORIGINAL
 ;;QUICK START
 ;;SUPPLEMENTAL
 ;;QUIT
 Q
 ;
CANRSN ;SETUP THE CANCELLATION REASON FILE #396.5
 ;HIDE REQUESTED CANCELLATION REASONS
 N DVBAI,DVBLINE,FIND,OUT,IRIEN,RSN,CODE,REPORT
 F DVBAI=1:1 S DVBLINE=$P($T(CANHIDE+DVBAI),";;",2) Q:DVBLINE="QUIT"  D
 . K FIND,OUT
 . S FIND=DVBLINE
 . D FIND^DIC(396.5,"",.01,"X",.FIND,"","","","","OUT")
 . S IRIEN=$G(OUT("DILIST",2,1))
 . I IRIEN="" D BMES^XPDUTL("CANCELLATION REASON NOT ON FILE TO HIDE: "_DVBLINE) Q
 . I '$D(^DVB(396.5,IRIEN)) D BMES^XPDUTL("CANCELLATION REASON NOT ON FILE TO HIDE: "_DVBLINE) Q
 . ;
 . Q:$P(^DVB(396.5,IRIEN,0),"^",4)'=""
 . ;
 . K FDA,ERR,KEYIEN
 . S FDA(396.5,IRIEN_",",.02)=0
 . S KEYIEN(1)=IRIEN
 . D UPDATE^DIE("","FDA","KEYIEN","ERR")
 . I $G(ERR)'="" D BMES^XPDUTL("CANCELLATION REASON: "_DVBLINE_" COULD NOT BE DEACTIVATED BY POST-INSTALL ROUTINE. PLEASE SET INACTIVE FIELD TO YES.") Q
 . D BMES^XPDUTL("CANCELLATION REASON: "_DVBLINE_" HAS BEEN DEACTIVATED.") Q
 ;
CANNEW ;NEW REQUESTED CANCELLATION REASONS
 N DVBAI,DVBLINE,FIND,OUT
 F DVBAI=1:1 S DVBLINE=$P($T(CANRNEW+DVBAI),";;",2) Q:DVBLINE="QUIT"  D
 . K FIND,OUT
 . S FIND=$P(DVBLINE,"^",1)
 . D FIND^DIC(396.5,"",.01,"X",.FIND,"","","","","OUT")
 . I $G(OUT("DILIST",2,1))'="" D BMES^XPDUTL("CANCELLATION REASON: "_FIND_" HAS ALREADY BEEN ADDED.") D  Q
 . . S DVBABIEN=OUT("DILIST","2",1)
 . . S DVBAFDA(396.5,DVBABIEN_",",.02)="1"
 . . D FILE^DIE("","DVBAFDA","DVBAERR")
 . K CRSN,CODE,REPORT
 . S CRSN=$P(DVBLINE,"^",1)
 . K FDA,ERR
 . S FDA(396.5,"+1,",.01)=CRSN
 . S FDA(396.5,"+1,",.02)="1"
 . D UPDATE^DIE("","FDA","","ERR")
 . I $G(ERR)'="" D BMES^XPDUTL("CANCELLATION REASON: "_CRSN_" COULD NOT BE ADDED BY POST-INSTALL ROUTINE.") Q
 . D BMES^XPDUTL("CANCELLATION REASON: "_CRSN_" HAS BEEN ADDED.") Q
 ;
 Q
 ;
CANHIDE ;INACTIVATE THE FOLLOWING CANCELLATION REASONS
 ;;ADEQUATE MEDICAL EVIDENCE
 ;;DUPLICATE EXAM
 ;;EMPLOYEE
 ;;EXAM NAME DELETED FROM FILE
 ;;FAILED TO REPORT
 ;;INCORRECT JURISDICTION
 ;;INCORRECT TEST ORDERED
 ;;NO EXAMS ORDERED
 ;;NOT ENOUGH INFORMATION
 ;;UNDELIVERED NOTIFICATION
 ;;VET REFUSED EXAM THIS LOCATION
 ;;VETERAN CANCELLED EXAM FOR DBQ
 ;;VETERAN WITHDREW CLAIM
 ;;QUIT
 Q
 ;
CANRNEW ;ADD THE FOLLOWING NEW CANCELLATION REASONS
 ;;DUPLICATE REQUEST
 ;;EXAM REQUESTED AT DIFFERENT LOCATION
 ;;EXAM REQUESTED IN ERROR
 ;;INCOMPLETE REQUEST
 ;;UNABLE TO CONTACT VETERAN
 ;;VETERAN DIED
 ;;VETERAN FAILED TO REPORT
 ;;VETERAN FAILED TO RSVP
 ;;VETERAN HOSPITALIZED
 ;;VETERAN IS EMPLOYEE
 ;;VETERAN REFUSED EXAM
 ;;QUIT
 Q
