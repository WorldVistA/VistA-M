ORDEBUG2 ;SLC/JMH - DEBUG REPORT ;07/13/17  12:53
 ;;3.0;ORDER ENTRY/RESULTS REPORTING;**350,443**;Dec 17, 1997;Build 5
 ;
 ;
 ;
DL ;
 N ORLST,ORI,ORCNT,ORREP
 W !!,"#",?5,"USER",?35,"DATE/TIME"
 W !,"============================================================================"
 S ORI=0,ORCNT=0 F  S ORI=$O(^XTMP("CPRS DEBUG LOG",ORI)) Q:ORI=""  D
 .S ORCNT=ORCNT+1
 .S ORLST(ORCNT)=ORI
 .W !,ORCNT,") ",?5,$P($G(^VA(200,+ORI,0)),U),?35,$P(ORI,U,2)
 S ORREP=$$TDIR("Debug Report #")
 I ORREP=0 Q
 I '$D(ORLST(ORREP)) W !,"Not a valid selection" Q
 D REP(ORLST(ORREP))
 D DL
 Q
REP(ORINST) ;
 ;ZW ^XTMP("CPRS DEBUG LOG",ORINST)
 W !!,"Viewing report generated by "_$P($G(^VA(200,+ORINST,0)),U)_" on "_$P(ORINST,U,2)
 W !!,"DESCRIPTION",!,"==========="
 N ORJ S ORJ="" F  S ORJ=$O(^XTMP("CPRS DEBUG LOG",ORINST,"DESCRIPTION",ORJ)) Q:'$L(ORJ)  D
 .W !,^XTMP("CPRS DEBUG LOG",ORINST,"DESCRIPTION",ORJ)
 N ORLST,ORI,ORCNT
 W !!,"#",?5,"RPC"
 W !,"============================================================================"
 S ORI=0,ORCNT=0 F  S ORI=$O(^XTMP("CPRS DEBUG LOG",ORINST,"RPCS",ORI)) Q:'$L(ORI)  D
 .S ORCNT=ORCNT+1
 .S ORLST(ORCNT)=ORI
 .W !,ORCNT,") ",?5,$G(^XTMP("CPRS DEBUG LOG",ORINST,"RPCS",ORI,0))
 N ORRPC
 F  S ORRPC=$$TDIR("RPC #") Q:ORRPC=0  D
 .I '$D(ORLST(ORRPC)) W !,"Not a valid selection" Q
 .W !!,"============","BEGINNING OF RPC INFO","============"
 .N ORK S ORK="" F  S ORK=$O(^XTMP("CPRS DEBUG LOG",ORINST,"RPCS",ORLST(ORRPC),ORK)) Q:'$L(ORK)  D
 ..W !,">>",^XTMP("CPRS DEBUG LOG",ORINST,"RPCS",ORLST(ORRPC),ORK)
 .W !!,"============","END OF RPC INFO","============"
 Q
TDIR(ORTXT) ;
 N X,Y,DIR
 S DIR(0)="NO"
 S DIR("A")=ORTXT,DIR("B")="^"
 S DIR("?")=""
 W !,"Choose a number to view"
 D ^DIR S:$D(DUOUT)!$D(DTOUT) Y="0"
 Q Y
