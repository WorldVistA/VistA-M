ORUTL5 ; SLC/JLC - OE/RR Utilities ;07/27/17  14:21
 ;;3.0;ORDER ENTRY/RESULTS REPORTING;**461,397**;Dec 17, 1997;Build 22
 ;
 ;
 ;
MSG(ORREC,ORMSGT) ;
 N XMSUB,XMY,XMTEXT,XMDUZ,ORTEXT,SITE,I,A
 S ORTEXT(1)="Attempt to save order with INPATIENT MEDICATIONS Display Group",SITE=$$SITE^VASITE()
 S ORTEXT(2)=" "
 S A="ORMSGT("""")" F I=3:1 S A=$Q(@A) Q:A=""  S ORTEXT(I)=$P(A,"(",2,99)_" = "_@A
 S XMDUZ=DUZ
 S XMSUB="NEW PROBLEM ORDER SUBMITTED FROM "_$P(SITE,"^",2)
 S XMY("G.HPS CLIN2@FORUM.DOMAIN.EXT")="",XMY("G.CLINICAL REMINDERS SUPPORT@FORUM.DOMAIN.EXT")=""
 S XMTEXT="ORTEXT("
 D ^XMD
 Q
COMPLETE(ORID) ;
 N ORDN,ORD,ORDIALOG,ORINFO
 S ORINFO("OR0")=$G(^OR(100,+ORID,0)),ORINFO("OR3")=$G(^(3)),ORINFO("OR4")=$G(^(4)),ORINFO("OR6")=$G(^(6)),ORINFO("OR7")=$G(^(7))
 S ORDN=$P(ORINFO("OR0"),"^",5) Q:ORDN'["101.41"  S ORDN=+ORDN
 D GETDLG^ORCD(ORDN),GETORDER^ORCD(+ORID,"ORD")
 S ORINFO("DIALOG")=ORDN
 M ORINFO("RESPONSES")=ORD
 D MSG^XQOR("OR COMPLETE ORDER",.ORINFO)
 Q
