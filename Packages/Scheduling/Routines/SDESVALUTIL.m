SDESVALUTIL ;ALB/BWF - VISTA SCHEDULING VALIDATION UTILITIES ;SEP 22, 2022
 ;;5.3;Scheduling;**826**;Aug 13, 1993;Build 18
 ;;Per VHA Directive 6402, this routine should not be modified
 ;
 Q
 ; validate appointment ID/IEN
VALAPPTIEN(ERRORS,APPTIEN) ;
 I '$L(APPTIEN) D ERRLOG^SDESJSON(.ERRORS,14) Q
 I 'APPTIEN D ERRLOG^SDESJSON(.ERRORS,15) Q
 I APPTIEN,'$D(^SDEC(409.84,APPTIEN)) D ERRLOG^SDESJSON(.ERRORS,15)
 Q
 ; validate ISO date
VALDATE(ERRORS,DATE,CLINIC) ;
 N FMSDATE
 I DATE="" D ERRLOG^SDESJSON(.ERRORS,45) Q ""
 S FMSDATE=$$ISOTFM^SDAMUTDT(DATE,CLINIC)
 I FMSDATE<1 D ERRLOG^SDESJSON(.ERRORS,46) Q ""
 Q FMSDATE
 ; validate SD SUPERVISOR key
VALKEY(ERRORS,DUZ) ;
 I '$$KCHK^XUSRB("SD SUPERVISOR",DUZ) D ERRLOG^SDESJSON(.ERRORS,316)
 Q
