SDESPATSEARCH  ;ALB/BLB - VISTA SCHEDULING RPCS; Oct 17, 2022@20:49
 ;;5.3;Scheduling;**833**;Aug 13, 1993;Build 9
 ;;Per VHA Directive 6402, this routine should not be modified
 ;
 ; Reference to PATIENT in ICR #7030
 ; Reference to PATIENT in ICR #7029
 ; Reference to PATIENT in ICR #1476
 ; Reference to PATIENT in ICR #10035
 ;
 Q
 ;
SEARCH(JSONRETURN,SEARCHSTRING,NUMOFRECORDS) ; SDES PATIENT SEARCH
 N RETURN,ERRORS,PATIENTLIST,INDEX
 ;
 D VALIDATENUMREC(.NUMOFRECORDS,.ERRORS)
 I $D(ERRORS) M RETURN=ERRORS D BUILDJSON(.JSONRETURN,.RETURN) Q
 ;
 I $G(SEARCHSTRING)=" " D  Q
 .D POPULATE(.PATIENTLIST,$G(^DISV(DUZ,"^DPT(")),1)
 .M RETURN=PATIENTLIST D BUILDJSON^SDESBUILDJSON(.JSONRETURN,.RETURN)
 ;
 S INDEX=$$INDEX(.SEARCHSTRING)
 I $L($G(INDEX)) D
 .D BUILDPATIENTLIST(.PATIENTLIST,$G(SEARCHSTRING),$G(NUMOFRECORDS),$G(INDEX))
 ;
 I '$D(PATIENTLIST) S PATIENTLIST("Patient",1)=""
 M RETURN=PATIENTLIST D BUILDJSON^SDESBUILDJSON(.JSONRETURN,.RETURN)
 Q
 ;
INDEX(SEARCHSTRING) ; returns the index to be searched in ^DPT
 I $G(SEARCHSTRING)="" Q "B" ;return all
 I $G(SEARCHSTRING)?1A4N Q "BS5" ;initial+4SSN
 I $E($G(SEARCHSTRING),1,3)?3A Q "B" ;partial name
 I $$ISOTFM^SDAMUTDT($G(SEARCHSTRING))'=-1 D  Q "ADOB" ;DOB
 .S SEARCHSTRING=$$ISOTFM^SDAMUTDT($G(SEARCHSTRING))
 ;
 I $G(SEARCHSTRING)?9N!($G(SEARCHSTRING)?3N1"-"2N1"-"4N)!($G(SEARCHSTRING)?9N.1"P")!($G(SEARCHSTRING)?3N1"-"2N1"-"4N.1"P") D  Q "SSN" ;SSN
 .S SEARCHSTRING=$TR($G(SEARCHSTRING),"-","")
 ;
 Q ""
 ;
BUILDPATIENTLIST(PATIENTLIST,SEARCHSTRING,NUMOFRECORDS,INDEX) ;
 N NUM,PATIENTNAME,DFN,SEARCHCRITERIA
 ;
 S SEARCHCRITERIA=$$GETSUB^SDECU(SEARCHSTRING) ;decrements all input types by 1
 I $G(SEARCHSTRING)="" S SEARCHCRITERIA=0
 S NUM=0
 F  S SEARCHCRITERIA=$O(^DPT(INDEX,SEARCHCRITERIA)) Q:SEARCHCRITERIA=""!(NUM=NUMOFRECORDS)!(SEARCHCRITERIA'[SEARCHSTRING)  D
 .S DFN=0
 .F  S DFN=$O(^DPT(INDEX,SEARCHCRITERIA,DFN)) Q:'DFN!(NUM=NUMOFRECORDS)  D
 ..S NUM=NUM+1
 ..D POPULATE(.PATIENTLIST,$G(DFN),NUM)
 Q
 ;
POPULATE(PATIENTLIST,DFN,NUM) ;
 N PATIENTDATA
 ;
 D GETS^DIQ(2,DFN,".01;.02;.024;.03;.1;.2405;.301;.302;.361;391;1100.01","IE","PATIENTDATA")
 S PATIENTLIST("Patient",NUM,"DFN")=DFN
 S PATIENTLIST("Patient",NUM,"Name")=$G(PATIENTDATA(2,DFN_",",.01,"E"))
 S PATIENTLIST("Patient",NUM,"DateOfBirth")=$G(PATIENTDATA(2,DFN_",",.03,"E"))
 S PATIENTLIST("Patient",NUM,"PreferredName")=$G(PATIENTDATA(2,DFN_",",.2405,"E"))
 S PATIENTLIST("Patient",NUM,"PatientType")=$G(PATIENTDATA(2,DFN_",",391,"E"))
 S PATIENTLIST("Patient",NUM,"Ward")=$G(PATIENTDATA(2,DFN_",",.1,"E")) ;.1
 S PATIENTLIST("Patient",NUM,"FugitiveFelon")=$G(PATIENTDATA(2,DFN_",",1100.01,"E"))
 S PATIENTLIST("Patient",NUM,"Gender")=$G(PATIENTDATA(2,DFN_",",.02,"E")) ;.02
 S PATIENTLIST("Patient",NUM,"GenderIdentity")=$G(PATIENTDATA(2,DFN_",",.024,"E"))
 S PATIENTLIST("Patient",NUM,"MentalHealthProvider")=$P($$START^SCMCMHTC(DFN),U,2)
 S PATIENTLIST("Patient",NUM,"PrimaryCarePractitioner")=$P($$OUTPTPR^SDUTL3(DFN),U,2)
 S PATIENTLIST("Patient",NUM,"ICNNumber")=$$GETICN(DFN)
 ;
 I $D(^DGS(41.41,"B",DFN)) D GETLASTDEMOUPDAT(.PATIENTLIST,DFN,NUM)
 I '$D(^DGS(41.41,"B",DFN)) S PATIENTLIST("Patient",NUM,"DemographicsUpdated")=""
 ;
 I $D(^DPT(DFN,"E","B")) D GETELIGIBILITY(.PATIENTLIST,DFN,NUM)
 I '$D(^DPT(DFN,"E","B")) S PATIENTLIST("Patient",NUM,"Eligibility")=""
 ;
 I $D(^DPT(DFN,.373)) D GETCONDITIONS(.PATIENTLIST,DFN,NUM)
 I '$D(^DPT(DFN,.373)) S PATIENTLIST("Patient",NUM,"ServiceConnectedCondition")=""
 ;
 I $G(PATIENTDATA(2,DFN_",",.301,"I"))="Y" S PATIENTLIST("Patient",NUM,"ServiceConnected")="YES"_" "_"("_$G(PATIENTDATA(2,DFN_",",.302,"I"))_"%)"
 I $G(PATIENTDATA(2,DFN_",",.301,"I"))="N" S PATIENTLIST("Patient",NUM,"ServiceConnected")="NO"
 Q
 ;
GETLASTDEMOUPDAT(PATIENTLIST,DFN,NUM) ; last patient demographic update
 N PREREGIEN
 ;
 S PREREGIEN=0,PREREGIEN=$O(^DGS(41.41,"B",DFN,"A"),-1)
 S PATIENTLIST("Patient",NUM,"DemographicsUpdated")=$P($$FMTISO^SDAMUTDT($$GET1^DIQ(41.41,PREREGIEN_",",1,"I")),"T")
 Q
 ;
GETELIGIBILITY(PATIENTLIST,DFN,NUM) ; top level primary code + secondary eligibilities
 N ELIGIBILITYIEN,COUNT
 ;
 S COUNT=1
 S PATIENTLIST("Patient",NUM,"Eligibility",COUNT,"PrimaryEligibilityCode")=$$GET1^DIQ(2,DFN_",",.361,"E")
 S PATIENTLIST("Patient",NUM,"Eligibility",COUNT,"PrimaryEligibilityCodeType")=$$GET1^DIQ(8,$$GET1^DIQ(2,DFN_",",.361,"I"),4,"E")
 ;
 S ELIGIBILITYIEN=0
 F  S ELIGIBILITYIEN=$O(^DPT(DFN,"E","B",ELIGIBILITYIEN)) Q:ELIGIBILITYIEN="B"!(ELIGIBILITYIEN="")  D
 .I $$GET1^DIQ(2.0361,ELIGIBILITYIEN_","_DFN_",",.01,"I")=$$GET1^DIQ(2,DFN_",",.361,"I") Q
 .S COUNT=COUNT+1
 .S PATIENTLIST("Patient",NUM,"Eligibility",COUNT,"SecondaryEligibilityCode")=$$GET1^DIQ(2.0361,ELIGIBILITYIEN_","_DFN_",",.01,"E")
 .S PATIENTLIST("Patient",NUM,"Eligibility",COUNT,"SecondaryEligibilityCodeType")=$$GET1^DIQ(8,ELIGIBILITYIEN,4,"E")
 Q
 ;
GETCONDITIONS(PATIENTLIST,DFN,NUM) ; service connected conditions
 N CONDITIONIEN,COUNT
 ;
 S CONDITIONIEN=0,COUNT=0
 F  S CONDITIONIEN=$O(^DPT(DFN,.373,CONDITIONIEN)) Q:'CONDITIONIEN  D
 .S COUNT=COUNT+1
 .S PATIENTLIST("Patient",NUM,"ServiceConnectedCondition",COUNT,"Condition")=$$GET1^DIQ(2.05,CONDITIONIEN_","_DFN_",",.01,"E")
 Q
 ;
GETICN(DFN) ; patient ICN
 N ICN
 ;
 S ICN=$$GETICN^MPIF001(DFN)
 I ICN["-1",$$GET1^DIQ(8989.3,1,.01,"E")["TEST" Q $$GET1^DIQ(2,DFN,991.1)
 Q ICN
 ;
VALIDATENUMREC(NUMOFRECORDS,ERRORS) ; number of records to return
 I $G(NUMOFRECORDS)="" S NUMOFRECORDS=10 Q
 I $G(NUMOFRECORDS)>50!($G(NUMOFRECORDS)<1) D ERRLOG^SDESJSON(.ERRORS,382)
 Q
 ;
BUILDJSON(JSONRETURN,RETURN) ;
 N JSONERROR
 D ENCODE^XLFJSON("RETURN","JSONRETURN","JSONERR")
 Q
 ;
