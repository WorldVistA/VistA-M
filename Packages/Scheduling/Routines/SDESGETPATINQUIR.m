SDESGETPATINQUIR ;ALB/BLB - SDES GET PATIENT INQUIRY ;Jan 17, 2023
 ;;5.3;Scheduling;**842**;Aug 13, 1993;Build 17
 ;;Per VHA Directive 6402, this routine should not be modified
 Q
 ;
GETINQUIRY(JSONRETURN,DFN) ;
 N ERRORS,RETURN,DETAILS,TMPRETURN
 I '$$VALIDATEDFN(.ERRORS,$G(DFN)) M RETURN=ERRORS D BUILDJSON(.JSONRETURN,.RETURN) Q
 ;
 K ^TMP("ORDATA",$J)
 D PTINQ^ORWPT(.TMPRETURN,DFN)
 M DETAILS("PatientInquiryDetails")=^TMP("ORDATA",$J)
 ;
 I '$D(DETAILS) S DETAILS("PatientInquiryDetails",1)=""
 M RETURN=DETAILS D BUILDJSON(.JSONRETURN,.RETURN)
 Q
 ;
VALIDATEDFN(ERRORS,DFN) ;
 I DFN="" D ERRLOG^SDESJSON(.ERRORS,1) Q 0
 I DFN'="",'$D(^DPT(DFN,0)) D ERRLOG^SDESJSON(.ERRORS,2) Q 0
 Q 1
 ;
BUILDJSON(JSONRETURN,RETURN) ;
 N JSONERR
 D ENCODE^XLFJSON("RETURN","JSONRETURN","JSONERR")
 Q
 ;
