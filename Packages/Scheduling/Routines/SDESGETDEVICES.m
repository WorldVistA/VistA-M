SDESGETDEVICES ;ALB/BWF - VISTA SCHEDULING RPCS - DEVICE RETRIEVAL ; September 07, 2022
 ;;5.3;Scheduling;**825**;Aug 13, 1993;Build 2
 ;;Per VHA Directive 6402, this routine should not be modified
 ;
 Q
DEVICES(SDRES,LOOKUP) ;
 N ERRORS,DEVICES,DEVLIST,DEV,DEVDAT
 S LOOKUP=$G(LOOKUP)
 D VALLOOKUP(.ERRORS,LOOKUP)
 I $D(ERRORS) S ERRORS("Device",1)="" D BUILDJSON^SDESBUILDJSON(.SDRES,.ERRORS) Q
 D DEVICE^XUDHGUI(.DEVICES,LOOKUP,1)
 I '$D(DEVICES) S DEVLIST("Device",1)="" D BUILDJSON^SDESBUILDJSON(.SDRES,.DEVLIST) Q
 S DEV=0 F  S DEV=$O(DEVICES(DEV)) Q:'DEV  D
 .S DEVDAT=$G(DEVICES(DEV))
 .S DEVLIST("Device",DEV,"ID")=$P(DEVDAT,U)
 .S DEVLIST("Device",DEV,"Name")=$P(DEVDAT,U,2)
 .S DEVLIST("Device",DEV,"DisplayName")=$P(DEVDAT,U,3)
 .S DEVLIST("Device",DEV,"Location")=$P(DEVDAT,U,4)
 .S DEVLIST("Device",DEV,"RightMargin")=$P(DEVDAT,U,5)
 .S DEVLIST("Device",DEV,"PageLength")=$P(DEVDAT,U,6)
 D BUILDJSON^SDESBUILDJSON(.SDRES,.DEVLIST)
 Q
VALLOOKUP(ERRORS,LOOKUP) ;
 I LOOKUP="" D ERRLOG^SDESJSON(.ERRORS,277) Q
 I $L(LOOKUP)>15 D ERRLOG^SDESJSON(.ERRORS,278)
 Q
