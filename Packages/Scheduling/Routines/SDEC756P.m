SDEC756P ;ALB/CLT - PATCH 756 POST INSTALL ROUTINE ;Sep 24, 2020@10:41
 ;;5.3;SCHEDULING;**756**;AUG 13, 1993;Build 43
 ;
FIND ;FIND THE IEN FOR "VS GUI NATIONAL"
 N SDECDA,SDECDA1
 W !!?3,"Updating SDEC SETTINGS file (#409.98)",!!
 S SDECDA=0,SDECDA=$O(^SDEC(409.98,"B","VS GUI NATIONAL",SDECDA)) G:$G(SDECDA)="" NOFIND
 D VERSION   ;update GUI version number and date
 D VVC       ;update the VVC stop codes
 D VVCURL    ;update the VVC URL
 Q
VERSION ;SET THE NEW VERSION UPDATE IN SDEC SETTING FILE #409.98 TO 1.7.2.1
 S DA=SDECDA,DIE=409.98,DR="2///1.7.2.1;3///"_DT D ^DIE  ;update VS GUI NATIONAL
 K DIE,DR,DA
 S SDECDA1=0,SDECDA1=$O(^SDEC(409.98,"B","VS GUI LOCAL",SDECDA1)) Q:$G(SDECDA1)=""    ;get DA for the VS GUI LOCAL
 S DA=SDECDA1,DIE=409.98,DR="2///1.7.2.1;3///"_DT D ^DIE  ;update VS GUI LOCAL
 K DIE,DR,DA
 Q
VVC ;SET THE VVC NATIONAL STOP CODES TO 179 AND 225 IN THE SDEC SETTINGS FILE #409.98
 N X,Y,DIC,DA ;
 ;
 F X=179,225 I '$D(^SDEC(409.98,1,3,"B",X)) D
 . K DIC,DA S DIC="^SDEC(409.98,1,3,",DIC(0)="L",DA(1)=1 D FILE^DICN
 Q
 ;
VVCURL ;
 N Y,VVCURL,DIE,DR,DA ;
 S VVCURL="https://www.mobile.domain.ext/app/virtual-care-manager"
 K DIE,DR,DA
 S DA=1,DIE="^SDEC(409.98,",DR="6////^S X=VVCURL" D ^DIE
 K DIC,DIE,DR,DA
 Q
 ;
NOFIND ;"VS GUI NATIONAL" NOT FOUND
 W !!?3,"VS GUI NATIONAL not found in the SDEC SETTINGS file (#409.98)"
 Q
