SDESEDITAPPTREQ ;ALB/BLB,MGD - VISTA SCHEDULING RPCS; Sept 16, 2022@15:00
 ;;5.3;Scheduling;**823,826**;Aug 13, 1993;Build 18
 ;;Per VHA Directive 6402, this routine should not be modified
 ;
 ; SDES EDIT APPT REQ
 ;
 Q
 ;
EDITREQUEST(JSONRETURN,REQUEST) ;
 N REQUESTIEN,ERRORS,RETURN,ISAPPREQIENVALID,ISDFNVALID,ISDATETIMEVALID,ISEASVALID,INSTITUTIONIEN
 N ISCLINSTOPVALID,ISMODALINVALID,ISREQUESTBYVALID,ISPROVIDERVALID,ISPIDVALID,ISPRIGROUPVALID
 N ISREQTYPEVALID,ISPRIORITYVALID,ISSERVCONNVALID,ISAPPTTYPEVALID,ISPATSTATVALID
 N ISDATEPREFVALID,ISMTRCDATAVALID,ISCPRSDATAVALID
 ;
 D VALIDATE(.REQUEST,.ERRORS)
 ;
 I $D(ERRORS) M RETURN=ERRORS D BUILDJSON(.JSONRETURN,.RETURN) Q
 ;
 S RETURN("Request","IEN")=$$BUILDER(.REQUEST)
 ;
 D BUILDJSON(.JSONRETURN,.RETURN)
 Q
 ;
VALIDATE(REQUEST,ERRORS) ;
 ;
 S ISAPPREQIENVALID=$$VALIDATEAPPTREQ(.ERRORS,$G(REQUEST("REQUEST IEN")))
 ;
 S ISDFNVALID=$$VALIDATEDFN(.ERRORS,$G(REQUEST("DFN")))
 ;
 S ISDATETIMEVALID=$$VALIDATEDATETIME(.ERRORS,.REQUEST)
 ;
 S ISREQTYPEVALID=$$VALIDATEREQTYPE(.ERRORS,$G(REQUEST("REQUEST SUB TYPE")))
 ;
 S INSTITUTIONIEN=$$STATIONTOINST(.ERRORS,$G(REQUEST("STATION NUMBER")),$G(REQUEST("INSTITUTION NAME")))
 ;
 S ISCLINSTOPVALID=$$VALIDATECLINSTOP(.ERRORS,.REQUEST,$G(REQUEST("CLINIC IEN")),$G(REQUEST("STOP CODE")),$G(REQUEST("SECONDARY STOP CODE")))
 ;
 S ISREQUESTBYVALID=$$VALIDATEREQBY(.ERRORS,$G(REQUEST("REQUESTED BY")))
 ;
 I $G(REQUEST("REQUESTED BY"))="PROVIDER" S ISPROVIDERVALID=$$VALIDATEPROVIDER(.ERRORS,$G(REQUEST("PROVIDER IEN")))
 ;
 S ISPIDVALID=$$VALIDATEPID(.ERRORS,.REQUEST)
 ;
 S ISEASVALID=$$VALIDATEEAS(.ERRORS,$G(REQUEST("EAS")))
 ;
 I $L($G(REQUEST("MODALITY"))) S ISMODALINVALID=$$VALIDATEMODALITY^SDESINPUTVALUTL(.ERRORS,$G(REQUEST("MODALITY")))
 ;
 I $L($G(REQUEST("PRIORITY"))) S ISPRIORITYVALID=$$VALIDATEPRIORITY(.ERRORS,$G(REQUEST("PRIORITY")))
 ;
 I $L($G(REQUEST("PRIORITY GROUP"))) S ISPRIGROUPVALID=$$VALIDATEPRIGROUP(.ERRORS,$G(REQUEST("PRIORITY GROUP")))
 ;
 I $L($G(REQUEST("SERVICE CONNECTED"))) S ISSERVCONNVALID=$$VALIDATESERVCONN(.ERRORS,$G(REQUEST("SERVICE CONNECTED")),$G(REQUEST("SERVICE CONNECTED PERCENTAGE")))
 ;
 ; Evaluate Appt Type IEN and Appt Type Name together
 N SDIEN,SDNAME
 S SDIEN=$G(REQUEST("APPOINTMENT TYPE IEN")),SDNAME=$G(REQUEST("APPOINTMENT TYPE NAME"))
 S ISAPPTTYPEVALID=$$VALIDATEAPPTTYPE^SDESEDITAPPTREQ2(.ERRORS,.SDIEN,SDNAME)
 S REQUEST("APPOINTMENT TYPE IEN")=SDIEN
 ;
 I $L($G(REQUEST("PATIENT STATUS"))) S ISPATSTATVALID=$$VALIDATEPATSTAT(.ERRORS,$G(REQUEST("PATIENT STATUS")))
 ;
 I $D(REQUEST("PATIENT PREFERRED START DATE")),$D(REQUEST("PATIENT PREFERRED END DATE")) D VALIDATEDATEPREF^SDESEDITAPPTREQ2(.ERRORS,.REQUEST)
 ;
 I $D(REQUEST("MRTC")) D VALIDATEMRTCDATA(.ERRORS,.REQUEST,$G(REQUEST("MRTC","PARENT REQUEST")),$G(REQUEST("MRTC","NEEDED")),$G(REQUEST("MRTC","DAYS BETWEEN APPTS")),$G(REQUEST("MRTC","HOW MANY NEEDED")))
 ;
 Q
 ;
BUILDER(REQUEST) ;
 N FDA,FDAERR,RETURNIEN
 S REQUESTIEN=$G(REQUEST("REQUEST IEN"))_","
 S FDA(409.85,REQUESTIEN,.01)=$G(REQUEST("DFN"))
 S FDA(409.85,REQUESTIEN,.02)=$G(REQUEST("PATIENT STATUS"))
 S FDA(409.85,REQUESTIEN,1)=$G(REQUEST("CREATE DATE"))
 S FDA(409.85,REQUESTIEN,2)=$G(INSTITUTIONIEN)
 S FDA(409.85,REQUESTIEN,4)=$G(REQUEST("REQUEST SUB TYPE"))
 S FDA(409.85,REQUESTIEN,5)=$G(REQUEST("VAOS GUID"))
 S FDA(409.85,REQUESTIEN,6)=$G(REQUEST("MODALITY"))
 S FDA(409.85,REQUESTIEN,8)=$G(REQUEST("CLINIC IEN"))
 S FDA(409.85,REQUESTIEN,8.5)=$G(REQUEST("STOP CODE"))
 S FDA(409.85,REQUESTIEN,8.6)=$G(REQUEST("SECONDARY STOP CODE"))
 S FDA(409.85,REQUESTIEN,8.7)=$G(REQUEST("APPOINTMENT TYPE IEN"))
 S FDA(409.85,REQUESTIEN,9)=$G(DUZ)
 S FDA(409.85,REQUESTIEN,9.5)=$$NOW^XLFDT
 S FDA(409.85,REQUESTIEN,10)=$G(REQUEST("PRIORITY"))
 S FDA(409.85,REQUESTIEN,10.5)=$G(REQUEST("PRIORITY GROUP"))
 S FDA(409.85,REQUESTIEN,11)=$G(REQUEST("REQUESTED BY"))
 S FDA(409.85,REQUESTIEN,12)=$G(REQUEST("PROVIDER IEN"))
 S FDA(409.85,REQUESTIEN,14)=$G(REQUEST("SERVICE CONNECTED PERCENTAGE"))
 S FDA(409.85,REQUESTIEN,15)=$G(REQUEST("SERVICE CONNECTED"))
 S FDA(409.85,REQUESTIEN,22)=$G(REQUEST("PATIENT INDICATED DATE"))
 S FDA(409.85,REQUESTIEN,41)=$G(REQUEST("MRTC","NEEDED"))
 S FDA(409.85,REQUESTIEN,42)=$G(REQUEST("MRTC","DAYS BETWEEN APPTS"))
 S FDA(409.85,REQUESTIEN,43)=$G(REQUEST("MRTC","HOW MANY NEEDED"))
 S FDA(409.85,REQUESTIEN,43.8)=$G(REQUEST("MRTC","PARENT REQUEST"))
 S FDA(409.85,REQUESTIEN,46)=$G(REQUEST("CPRS ORDER NUMBER"))
 S FDA(409.85,REQUESTIEN,47)=$G(REQUEST("CPRS TIME SENSITIVE"))
 S FDA(409.85,REQUESTIEN,100)=$G(REQUEST("EAS"))
 ;
 D FILE^DIE(,"FDA","FDAERR") K FDA
 ;
 I $D(REQUEST("CPRS PREREQUISITES")) D EDITCPRSPREREQS(.REQUEST,$G(REQUEST("REQUEST IEN")))
 ;
 I $D(REQUEST("REQUEST COMMENT"))!($D(REQUEST("PATIENT COMMENT")))!($D(REQUEST("PATIENT PREFERRED START DATE"))) D BUILDCOMMENTS(.REQUEST,$G(REQUEST("REQUEST IEN")))
 ;
 I $D(REQUEST("MRTC","PATIENT INDICATED DATE")) D EDITMRTCPID(.REQUEST,$G(REQUEST("REQUEST IEN")))
 ;
 I $D(REQUEST("MRTC","CHILD REQUEST"))!($D(REQUEST("MRTC","MRTC APPOINTMENT"))) D EDITMRTCLINKS(.REQUEST,$G(REQUEST("REQUEST IEN")))
 ;
 D AUDIT($G(REQUEST("REQUEST IEN")),$G(REQUEST("CLINIC IEN")),$G(REQUEST("STOP CODE")))
 ;
 Q $G(REQUEST("REQUEST IEN"))
 ;
BUILDAPPTDATA(REQUESTIEN,APPTDATETIME,CLINICIEN,SERVCONNPERC,SERVCONN,APPTTYPE,EAS) ;
 N FDA,FDAERR
 S REQUESTIEN=$G(REQUESTIEN)_","
 S FDA(409.85,REQUESTIEN,8.7)=$G(APPTTYPE)
 S FDA(409.85,REQUESTIEN,13)=$G(APPTDATETIME)
 S FDA(409.85,REQUESTIEN,13.1)=$P($$NOW^XLFDT,".",1)
 S FDA(409.85,REQUESTIEN,13.2)=$G(CLINICIEN)
 S FDA(409.85,REQUESTIEN,13.3)=$$GET1^DIQ(44,$G(CLINICIEN),3,"I") ; appt institution ;
 S FDA(409.85,REQUESTIEN,13.4)=$$GET1^DIQ(44,$G(CLINICIEN),8,"I") ; appt stop code
 S FDA(409.85,REQUESTIEN,13.6)=$$GET1^DIQ(40.8,$$GET1^DIQ(44,$G(CLINICIEN),3.5,"I"),1,"I") ; appt station number
 S FDA(409.85,REQUESTIEN,13.7)=$G(DUZ)
 S FDA(409.85,REQUESTIEN,13.8)="R" ; 'R' FOR Scheduled/Kept;
 S FDA(409.85,REQUESTIEN,14)=$G(SERVCONNPERC)
 S FDA(409.85,REQUESTIEN,15)=$G(SERVCONN)
 S FDA(409.85,REQUESTIEN,100)=$G(EAS)
 S FDA(409.85,REQUESTIEN,23)="C"
 ;
 D FILE^DIE(,"FDA","FDAERR") K FDA
 ;
 Q
 ;
EDITMRTCLINKS(REQUEST,REQUESTIEN) ;
 N NUM,FDA,FDAERR,SUBIEN
 S SUBIEN=0,NUM=0
 F  S SUBIEN=$O(^SDEC(409.85,REQUESTIEN,2,SUBIEN)) Q:'SUBIEN  D
 .S NUM=NUM+1
 .S FDA(409.852,SUBIEN_","_REQUESTIEN_",",.01)=$G(REQUEST("MRTC","CHILD REQUEST",NUM))
 .S FDA(409.852,SUBIEN_","_REQUESTIEN_",",.02)=$G(REQUEST("MRTC","MRTC APPOINTMENT",NUM))
 .D FILE^DIE(,"FDA","FDAERR") K FDA
 Q
 ;
EDITMRTCPID(REQUEST,REQUESTIEN) ;
 N NUM,FDA,FDAERR,SUBIEN
 S SUBIEN=0,NUM=0
 F  S SUBIEN=$O(^SDEC(409.85,REQUESTIEN,5,SUBIEN)) Q:'SUBIEN  D
 .S NUM=NUM+1
 .S FDA(409.851,SUBIEN_","_REQUESTIEN_",",.01)=$G(REQUEST("MRTC","PATIENT INDICATED DATE",NUM))
 .D FILE^DIE(,"FDA","FDAERR") K FDA
 Q
 ;
BUILDMRTCLINKS(REQUEST,REQUESTIEN) ; called from SDESCREATEAPPT after appt is made from mrtc child
 N FDA,FDAERR
 S FDA(409.852,"+1,"_REQUESTIEN_",",.01)=$G(REQUEST("MRTC","CHILD REQUEST"))
 S FDA(409.852,"+1,"_REQUESTIEN_",",.02)=$G(REQUEST("MRTC","MRTC APPOINTMENT"))
 D UPDATE^DIE(,"FDA",,"FDAERR") K FDA
 Q
 ;
BUILDMRTCPID(REQUEST,REQUESTIEN) ; called from SDESCREATEAPPT after appt is made from mrtc child
 N FDA,FDAERR
 S FDA(409.851,"+1,"_REQUESTIEN_",",.01)=$G(REQUEST("MRTC","PATIENT INDICATED DATE"))
 D UPDATE^DIE(,"FDA",,"FDAERR") K FDA
 Q
 ;
BUILDCOMMENTS(REQUEST,REQUESTIEN) ;
 N REQCOMMS,NUM,NUM2,DONE,PREFDATES,PATCOMMS,RANGE,DATERANGE1,DATERANGE2,DATERANGE3,EDITPATCOM
 S NUM=0
 I $D(REQUEST("REQUEST COMMENT")) D
 .D WP^SDECUTL(.REQCOMMS,$G(REQUEST("REQUEST COMMENT")))
 .D WP^DIE(409.85,REQUESTIEN_",",26,"","REQCOMMS")
 ;
 I $D(REQUEST("PATIENT COMMENT")) D
 .I $G(REQUEST("PATIENT COMMENT"))'["Patient preferred date range" S EDITPATCOM(1)=$G(REQUEST("PATIENT COMMENT")) D WP^DIE(409.85,REQUESTIEN_",",60,"","EDITPATCOM") Q
 .S EDITPATCOM(1)=$P($G(REQUEST("PATIENT COMMENT")),"Patient preferred date range",1)
 .S DATERANGE1=$P($G(REQUEST("PATIENT COMMENT")),"Patient preferred date range",2) I $L($G(DATERANGE1)) S EDITPATCOM(2)="Patient preferred date range"_$P($G(REQUEST("PATIENT COMMENT")),"Patient preferred date range",2)
 .S DATERANGE2=$P($G(REQUEST("PATIENT COMMENT")),"Patient preferred date range",3) I $L($G(DATERANGE2)) S EDITPATCOM(3)="Patient preferred date range"_$P($G(REQUEST("PATIENT COMMENT")),"Patient preferred date range",3)
 .S DATERANGE3=$P($G(REQUEST("PATIENT COMMENT")),"Patient preferred date range",4) I $L($G(DATERANGE3)) S EDITPATCOM(4)="Patient preferred date range"_$P($G(REQUEST("PATIENT COMMENT")),"Patient preferred date range",4)
 .D WP^DIE(409.85,REQUESTIEN_",",60,"","EDITPATCOM")
 Q
 ;
EDITCPRSPREREQS(REQUEST,REQUESTIEN) ;
 N NUM,FDA,FDAERR,SUBIEN
 I '$D(^SDEC(409.85,REQUESTIEN,8)) D BUILDCPRSPREREQS^SDESCREATEAPPREQ(.REQUEST,REQUESTIEN) Q
 S SUBIEN=0,NUM=0
 F  S SUBIEN=$O(^SDEC(409.85,REQUESTIEN,8,SUBIEN)) Q:'SUBIEN  D
 .S NUM=NUM+1
 .S FDA(409.8548,SUBIEN_","_REQUESTIEN_",",.01)=$G(REQUEST("CPRS PREREQUISITES",NUM))
 .D FILE^DIE(,"FDA","FDAERR") K FDA
 Q
 ;
AUDIT(REQUESTIEN,CLINICIEN,STOPCODE) ;
 N FDA,FDAERR
 S FDA(409.8545,"+1,"_REQUESTIEN_",",.01)=$$NOW^XLFDT
 S FDA(409.8545,"+1,"_REQUESTIEN_",",1)=$G(DUZ)
 S FDA(409.8545,"+1,"_REQUESTIEN_",",2)=$G(CLINICIEN)
 S FDA(409.8545,"+1,"_REQUESTIEN_",",3)=$G(STOPCODE)
 D UPDATE^DIE("","FDA",,"FDAERR") K FDA
 Q
VALIDATEAPPTREQ(ERRORS,REQUESTIEN) ;
 I REQUESTIEN="@" D ERRLOG^SDESJSON(.ERRORS,229) Q 0
 I REQUESTIEN="" D ERRLOG^SDESJSON(.ERRORS,3) Q 0
 I REQUESTIEN'="",('$D(^SDEC(409.85,REQUESTIEN)))!(REQUESTIEN=0) D ERRLOG^SDESJSON(.ERRORS,4) Q 0
 Q 1
 ;
VALIDATEDFN(ERRORS,DFN) ;
 I DFN="@" D ERRLOG^SDESJSON(.ERRORS,229) Q 0
 I DFN="" D ERRLOG^SDESJSON(.ERRORS,1) Q 0
 I DFN'="",'$D(^DPT(DFN,0)) D ERRLOG^SDESJSON(.ERRORS,2) Q 0
 Q 1
 ;
VALIDATEDATETIME(ERRORS,REQUEST) ;
 I $G(REQUEST("CREATE DATE"))="@" D ERRLOG^SDESJSON(.ERRORS,229) Q 0
 I $G(REQUEST("CREATE DATE"))="" D ERRLOG^SDESJSON(.ERRORS,48) Q 0
 I $G(REQUEST("CREATE DATE"))'="" S REQUEST("CREATE DATE")=$$ISOTFM^SDAMUTDT($G(REQUEST("CREATE DATE")))
 I $G(REQUEST("CREATE DATE"))=-1 D ERRLOG^SDESJSON(.ERRORS,49) Q 0
 Q 1
 ;
VALIDATEREQTYPE(ERRORS,REQTYPE) ;
 I REQTYPE="@" D ERRLOG^SDESJSON(.ERRORS,229) Q 0
 I REQTYPE="" D ERRLOG^SDESJSON(.ERRORS,60) Q 0
 I REQTYPE'="",REQTYPE'="APPT",REQTYPE'="MOBILE",REQTYPE'="RTC",REQTYPE'="VETERAN" D ERRLOG^SDESJSON(.ERRORS,61) Q 0
 Q 1
 ;
STATIONTOINST(ERRORS,STATIONNUM,INSTNAME) ; station number has precedence over institution name
 N INSTITUTIONIEN
 I STATIONNUM="",INSTNAME="" D ERRLOG^SDESJSON(.ERRORS,204) Q 0
 I STATIONNUM="",INSTNAME'="" D
 .S INSTITUTIONIEN=$$FIND1^DIC(4,"","X",INSTNAME,"B") I 'INSTITUTIONIEN D ERRLOG^SDESJSON(.ERRORS,205)
 I STATIONNUM'="" S INSTITUTIONIEN=$$FIND1^DIC(4,"","X",STATIONNUM,"D") I 'INSTITUTIONIEN D ERRLOG^SDESJSON(.ERRORS,197) Q 0
 Q INSTITUTIONIEN
 ;
VALIDATECLINSTOP(ERRORS,REQUEST,CLINICIEN,STOPCODE,SECSTOPCODE) ;
 N STOPCODEIEN
 I CLINICIEN'="",'$D(^SC(CLINICIEN,0)) D ERRLOG^SDESJSON(.ERRORS,19) Q 0
 I STOPCODE'="" S STOPCODE=$$FIND1^DIC(40.7,"","X",STOPCODE,"C") I '$G(STOPCODE) D ERRLOG^SDESJSON(.ERRORS,99) Q 0
 I SECSTOPCODE'="" S SECSTOPCODE=$$FIND1^DIC(40.7,"","X",SECSTOPCODE,"C") I '$G(SECSTOPCODE) D ERRLOG^SDESJSON(.ERRORS,214) Q 0
 I SECSTOPCODE'="",STOPCODE="" D ERRLOG^SDESJSON(.ERRORS,234) Q 0
 I CLINICIEN="",STOPCODE="" D ERRLOG^SDESJSON(.ERRORS,63) Q 0
 I STOPCODE'="",CLINICIEN'="" D ERRLOG^SDESJSON(.ERRORS,202) Q 0
 I SECSTOPCODE'="",CLINICIEN'="" D ERRLOG^SDESJSON(.ERRORS,202) Q 0
 Q 1
 ;
VALIDATEREQBY(ERRORS,REQUESTEDBY) ;
 I REQUESTEDBY="@" D ERRLOG^SDESJSON(.ERRORS,229) Q 0
 I REQUESTEDBY="" D ERRLOG^SDESJSON(.ERRORS,62) Q 0
 I REQUESTEDBY'="",REQUESTEDBY'="PATIENT",REQUESTEDBY'="PROVIDER" D ERRLOG^SDESJSON(.ERRORS,198) Q 0
 Q 1
 ;
VALIDATEPROVIDER(ERRORS,PROVIDERIEN) ;
 I PROVIDERIEN="@" D ERRLOG^SDESJSON(.ERRORS,229) Q 0
 I PROVIDERIEN="" D ERRLOG^SDESJSON(.ERRORS,53) Q 0
 I PROVIDERIEN'="",'$D(^VA(200,PROVIDERIEN,0)) D ERRLOG^SDESJSON(.ERRORS,54) Q 0
 Q 1
 ;
VALIDATEPID(ERRORS,REQUEST) ;
 I $G(REQUEST("PATIENT INDICATED DATE"))="@" D ERRLOG^SDESJSON(.ERRORS,229) Q 0
 I $G(REQUEST("PATIENT INDICATED DATE"))="" D ERRLOG^SDESJSON(.ERRORS,159) Q 0
 I $G(REQUEST("PATIENT INDICATED DATE"))'="" S REQUEST("PATIENT INDICATED DATE")=$$ISOTFM^SDAMUTDT($G(REQUEST("PATIENT INDICATED DATE")))
 I $G(REQUEST("PATIENT INDICATED DATE"))=-1 D ERRLOG^SDESJSON(.ERRORS,160) Q 0
 Q 1
 ;
VALIDATEEAS(ERRORS,EAS) ;
 I $L(EAS) S EAS=$$EASVALIDATE^SDESUTIL($G(EAS))
 I $P($G(EAS),U)=-1 D ERRLOG^SDESJSON(.ERRORS,142) Q 0
 Q 1
 ;
VALIDATEPRIORITY(ERRORS,PRIORITY) ;
 I PRIORITY'="ASAP",PRIORITY'="FUTURE" D ERRLOG^SDESJSON(.ERRORS,211) Q 0
 Q 1
 ;
VALIDATEPRIGROUP(ERRORS,GROUP) ;
 N NUM,FOUND,GCHECK
 S NUM=0,FOUND=0
 F NUM=1:1:8  D
 .S GCHECK="GROUP "_NUM
 .I GROUP=GCHECK S REQUEST("PRIORITY GROUP")=NUM S FOUND=1
 I FOUND=0 D ERRLOG^SDESJSON(.ERRORS,199) Q 0
 Q 1
 ;
VALIDATESERVCONN(ERRORS,SERVCONN,SERVCONNPERC) ;
 I $D(SERVCONN),SERVCONN'="YES",SERVCONN'="NO" D ERRLOG^SDESJSON(.ERRORS,200) Q 0
 I $G(SERVCONNPERC),'+SERVCONNPERC!(SERVCONNPERC'>0)!(SERVCONNPERC'<101) D ERRLOG^SDESJSON(.ERRORS,201) Q 0
 I $G(SERVCONN)="NO",+$G(SERVCONNPERC) D ERRLOG^SDESJSON(.ERRORS,232) Q 0
 S REQUEST("SERVICE CONNECTED")=$S(SERVCONN="YES":"1",SERVCONN="NO":"0",1:"")
 Q 1
 ;
VALIDATEPATSTAT(ERRORS,PATIENTSTATUS) ;
 I PATIENTSTATUS'="NEW",PATIENTSTATUS'="ESTABLISHED" D ERRLOG^SDESJSON(.ERRORS,203) Q 0
 S REQUEST("PATIENT STATUS")=$S(PATIENTSTATUS="NEW":"N",PATIENTSTATUS="ESTABLISHED":"E",1:"")
 Q 1
 ;
VALIDATEMRTCDATA(ERRORS,REQUEST,PARENTREQUEST,NEEDED,DAYSBETWEEN,HOWMANY) ;
 N NUM,DONE,CNT
 I NEEDED'="YES" D  Q ""
 .I $G(PARENTREQUEST)!($G(DAYSBETWEEN))!($G(HOWMANY)) D ERRLOG^SDESJSON(.ERRORS,233)
 I NEEDED'="",NEEDED'="YES",NEEDED'="NO" D ERRLOG^SDESJSON(.ERRORS,208) Q
 ;
 I PARENTREQUEST'="",('$D(^SDEC(409.85,PARENTREQUEST)))!(PARENTREQUEST=0) D ERRLOG^SDESJSON(.ERRORS,207)
 ;
 I DAYSBETWEEN'="" D
 .I '+DAYSBETWEEN!(DAYSBETWEEN'<366) D ERRLOG^SDESJSON(.ERRORS,209)
 ;
 I HOWMANY'="" D
 .I '+HOWMANY!(HOWMANY'<101) D ERRLOG^SDESJSON(.ERRORS,210)
 ;
 I $D(REQUEST("MRTC","PATIENT INDICATED DATE")) D
 .S NUM=0,DONE=0
 .F  S NUM=$O(REQUEST("MRTC","PATIENT INDICATED DATE",NUM)) Q:'NUM!(DONE=1)  D
 ..S REQUEST("MRTC","PATIENT INDICATED DATE",NUM)=$$ISOTFM^SDAMUTDT($G(REQUEST("MRTC","PATIENT INDICATED DATE",NUM)))
 ..I $G(REQUEST("MRTC","PATIENT INDICATED DATE",NUM))=-1 D ERRLOG^SDESJSON(.ERRORS,215) S DONE=1 Q
 ;
 I $D(REQUEST("MRTC","CHILD REQUEST"))!($D(REQUEST("MRTC","MRTC APPOINTMENT"))) D
 .S CNT=0,DONE=0
 .F  S CNT=$O(REQUEST("MRTC","CHILD REQUEST",CNT)) Q:'CNT!(DONE=1)  D
 ..I '$D(^SDEC(409.85,$G(REQUEST("MRTC","CHILD REQUEST",CNT)),0)) D ERRLOG^SDESJSON(.ERRORS,216) S DONE=1 Q
 ..I '$D(^SDEC(409.84,$G(REQUEST("MRTC","MRTC APPOINTMENT",CNT)),0)) D ERRLOG^SDESJSON(.ERRORS,217) S DONE=1 Q
 Q
 ;
BUILDJSON(JSONRETURN,RETURN) ;.
 N JSONERROR
 D ENCODE^XLFJSON("RETURN","JSONRETURN","JSONERR")
 Q
 ;
