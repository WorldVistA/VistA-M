SDESGETDIVISION ;ALB/BWF - VISTA SCHEDULING RPCS ;JUNE 10, 2022
 ;;5.3;Scheduling;**819**;Aug 13, 1993;Build 5
 ;;Per VHA Directive 6402, this routine should not be modified
 ;
 Q
GETDIVISIONLIST(RES,INP,EAS) ;
 N DATA,DIVNM,DIVID,FACILITYNUM,FACARRY,ERRORS,DCNT,ITEM
 D VALINPUT(.ERRORS,INP)
 D VALIDATEEAS(.ERRORS,$G(EAS))
 I $D(ERRORS) D  Q
 .S ERRORS("Division",1)=""
 .D BUILDJSON^SDESBUILDJSON(.RES,.ERRORS) Q
 D FIND^DIC(40.8,,,"M",INP,,,,,"DATA")
 S (DCNT,ITEM)=0 F  S ITEM=$O(DATA("DILIST",1,ITEM)) Q:'ITEM  D
 .S DCNT=DCNT+1
 .S DIVNM=$G(DATA("DILIST",1,ITEM))
 .S DIVID=$G(DATA("DILIST",2,ITEM))
 .S FACILITYNUM=$G(DATA("DILIST","ID",ITEM,1))
 .S FACARRY("Division",DCNT,"Name")=DIVNM
 .S FACARRY("Division",DCNT,"ID")=DIVID
 .S FACARRY("Division",DCNT,"FacilityNumber")=FACILITYNUM
 I '$D(FACARRY) S FACARRY("Division",1)=""
 D BUILDJSON^SDESBUILDJSON(.RES,.FACARRY)
 Q
VALINPUT(ERRORS,INP)    ;
 I $L($G(INP))<2 D ERRLOG^SDESJSON(.ERRORS,64)
 Q
VALIDATEEAS(ERRORS,SDEAS) ;
 I $L(SDEAS) S SDEAS=$$EASVALIDATE^SDESUTIL($G(SDEAS))
 I $P($G(SDEAS),U)=-1 D ERRLOG^SDESJSON(.ERRORS,142)
 Q
