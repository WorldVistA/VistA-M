OOPS233P ;MNT/RFS - OOPS*2*33 post install routine ; Feb 9, 2022@08:54
 ;;2.0;ASISTS;**33**;Jun 03, 2002;Build 5
 ;
 Q
EN ;entry point
 D DEQUEUE
 Q
 ;
DEQUEUE ;Dequeue scheduled options
 N OOPSLST,OOPSLP,OOPSLP1,OOPSOPT,OOPSX,OOPSTSK,OOPSLST1
 D BMES^XPDUTL("Starting dequeue of OOPS tasks")
 F OOPSLP=1:1 S OOPSTSK=$P($TEXT(TSKLST+OOPSLP),";;",2) Q:OOPSTSK="$$END"  D
 .D RTN^%ZTLOAD(OOPSTSK,"OOPSLST")
 .S OOPSX=0 F  S OOPSX=$O(OOPSLST(OOPSX)) Q:'OOPSX  S ZTSK=OOPSX D DQ^%ZTLOAD
 F OOPSLP1=1:1 S OOPSOPT=$P($TEXT(OPTLST+OOPSLP1),";;",2) Q:OOPSOPT="$$END"  D
 .D OPTION^%ZTLOAD(OOPSOPT,"OOPSLST1")
 .S OOPSX=0 F  S OOPSX=$O(OOPSLST1(OOPSX)) Q:'OOPSX  S ZTSK=OOPSX D DQ^%ZTLOAD
 .D RESCH^XUTMOPT(OOPSOPT,"@",,"@") ;Un-schedule option
 D BMES^XPDUTL("Dequeuing of OOPS tasks complete")
 Q
 ;
TSKLST ;Task List
 ;;EN^OOPSNDB
 ;;EN^OOPSDOL
 ;;$$END
OPTLST ;Option List
 ;;OOPS DOL SCHEDULED XMIT DATA
 ;;OOPS SCHEDULED XMIT 2162 DATA
 ;;$$END
