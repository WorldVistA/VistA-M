PXVUTL ;BIR/ADM - SKIN TEST UTILITY ROUTINE ;08/20/2015
 ;;1.0;PCE PATIENT CARE ENCOUNTER;**210**;Aug 12, 1996;Build 21
 ;
HR ; called by AH new style x-ref in V SKIN TEST file
 ; set number of hours between placement and reading of test
 N PXVX,X1,X2,X3
 S X1=$P($G(^AUPNVSK(DA,0)),"^",6) ; DATE READ
 S X2=$P($G(^AUPNVSK(DA,12)),"^") ; EVENT DATE AND TIME
 S X3=2 ; return difference in seconds
 S PXVX=""
 I $G(X1),$L(X1)>7,$G(X2),$L(X2)>7,$G(X2)'>$G(X1) S PXVX=$$FMDIFF^XLFDT(X1,X2,X3)\3600
 S $P(^AUPNVSK(DA,12),"^",14)=PXVX
 Q
CODSYS ; set logic for AC x-ref on SKIN TEST field to populate CODE SYSTEM multiple
 N PXVCODE,PXVSK,PXVTN
 S PXVCODE=$$CODX(X,DA)
 Q
CODX(PXVSK,PXVTN) ; populate CODE SYSTEM multiple
 N DA,DD,DO,DIC,DR,PXVC,PXVCOD,PXVIEN,PXVM,PXVSYS,X S PXVCODE=0
 S PXVM=0 F  S PXVM=$O(^AUTTSK(PXVSK,3,PXVM)) Q:'PXVM  D
 .S PXVSYS=$P(^AUTTSK(PXVSK,3,PXVM,0),"^") Q:PXVSYS=""  S PXVCODE=1
 .K DA,DD,DO,DIC S DA(1)=PXVTN,DIC="^AUPNVSK(PXVTN,3,",DIC(0)="L",X=PXVSYS D FILE^DICN K DA,DD,DIC,DO,DR S PXVIEN=+Y I PXVIEN'>0 S PXVCODE=0 Q
 .S PXVC=0 F  S PXVC=$O(^AUTTSK(PXVSK,3,PXVM,1,PXVC)) Q:'PXVC  D
 ..S PXVCOD=$P(^AUTTSK(PXVSK,3,PXVM,1,PXVC,0),"^")
 ..S PXVY(9000010.1231,"+1,"_PXVIEN_","_PXVTN_",",.01)=PXVCOD D UPDATE^DIE("","PXVY") K PXVY
 Q PXVCODE
KCODSYS ; kill logic for AC x-ref
 N PXVCODE,PXVTN
 S PXVCODE=$$KCODX(DA)
 Q
KCODX(PXVTN) ;
 N DA,DD,DO,DIC,DR,PXVJ S PXVCODE=0
 S PXVJ=0 F  S PXVJ=$O(^AUPNVSK(PXVTN,3,PXVJ)) Q:'PXVJ  D  S PXVCODE=1
 .S PXVY(9000010.123,PXVJ_","_PXVTN_",",.01)="@" D FILE^DIE("","PXVY")
 Q PXVCODE
TIME() ; determine if future time
 N PXV,PXVX,PXVY S PXVX=X,PXV=0
 D NOW I PXVX>PXVY S PXV=1
 Q PXV
NOW ; get now for comparison
 N %,%H,%I,X
 D NOW^%DTC S PXVY=$E(%,1,12)
 Q 
