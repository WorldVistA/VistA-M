PXRRPCR1 ;HIN/MjK - Clinic Specfic Workload Reports ;6/7/96
 ;;1.0;PCE PATIENT CARE ENCOUNTER;;Aug 12, 1996
VIS ;_._._._._._._._._._._._.Clinic Workload_._._._._._._._._._._._.
 D HDR^PXRRPCR S PXRRQ=1 I $D(PXRSTPNM) W !?1,"Clinic Stop: ",PXRSTPNM,!
 W ?1,"Encounter Date Range: ",$$FMTE^XLFDT($P(PXRRBDT,"."))," through ",$$FMTE^XLFDT($P(PXRREDT,".")),!
 D COL^PXRRPCR,PRINT,FTR^PXRRPCR
 Q
PRINT ;_._.Set local varaibles and print report_._.
 S PX=0 F  S PX=$O(PXRRCLIN(PX)) Q:PX=""  S PXRRCLIN=$P(PXRRCLIN(PX),U) D:PXRRQ
 . I '$D(^TMP($J,"CL",1,PXRRCLIN))&('+$G(^TMP($J,PXRRCLIN,"TOT"))) W ?0,$E(PXRRCLIN,1,25) F I=32,41,53,62,69,74,79,95,106,117 W @"?I",0 I I=117 W !
 . S PXRRTVCO=^TMP($J,"TVCO")
 . S PXRRAV=$J((PXRRTVCO/PXRRCN),2,1)
 . Q:'$D(^TMP($J,"CL",1,PXRRCLIN))&('+$G(^TMP($J,PXRRCLIN,"TOT")))
 . S PXRRNEW=^TMP($J,PXRRCLIN,"NEW")
 . S PXRREST=^TMP($J,PXRRCLIN,"ESTABLISHED")
 . S PXRRCON=^TMP($J,PXRRCLIN,"CONSULT")
 . S PXRRENT=^TMP($J,PXRRCLIN,"ENT")
 . S PXRRTOT=^TMP($J,PXRRCLIN,"TOT")
 . S PXRRNS=^TMP($J,PXRRCLIN,"NS")
 . S PXRRCA=^TMP($J,PXRRCLIN,"CA")
 . S PXRRUN=^TMP($J,PXRRCLIN,"UN")
 . S PXRRNVCP=^TMP($J,PXRRCLIN,"NVCPT")
 . S PXRROTH=^TMP($J,PXRRCLIN,"OTHER")
 . S PXRROCP=^TMP($J,PXRRCLIN,"OTHER CPT")
 . S PXRRPCT=$S($G(^TMP($J,PXRRCLIN,"TOT"))>0:(PXRRENT/PXRRTOT)*100,1:0)
 . I $Y>(IOSL-5),IOST'?1"C-".E W @IOF,! D COL^PXRRPCR
 . I $Y>(IOSL-5),IOST?1"C-".E S DIR(0)="E" D ^DIR K DIR S PXRRQ=Y W:Y @IOF,! D:PXRRQ COL^PXRRPCR
 . W ?0,$E(PXRRCLIN,1,23),?32,PXRRNEW,?41,PXRREST,?53,PXRRCON,?62,PXRROTH,?69,PXRROCP,?74,PXRRNVCP,?79,PXRRENT,?95,PXRRUN,?106,PXRRNS,?117,PXRRCA,!
 Q
     
