PXAIERR ;ISL/JVS,PKR - Set error array. ;03/20/2018
 ;;1.0;PCE PATIENT CARE ENCOUNTER;**211**;Aug 12, 1996;Build 244
 ;
SETPROB ;--Put errrors into PXAPROB.
 I PXADI("DIALOG")=8390001.001 D  Q
 . S PXASUB=PXASUB+1
 . S PXAPROB($J,PXASUB,"ERROR1",PXAERR(7),PXAERR(9),PXAK)=$G(PXAERR(12))
 . I $G(PXAERR(13))'="" S PXAPROB($J,PXASUB,"ERROR2",PXAERR(7),PXAERR(9),PXAK)=$G(PXAERR(13))
 ;
 I PXADI("DIALOG")=8390001.002 D  Q
 . S PXASUB=PXASUB+1
 . S PXAPROB($J,PXASUB,"WARNING2",PXAERR(7),PXAERR(9),PXAK)=$G(PXAERR(12))
 ;
 I PXADI("DIALOG")=8390001.003 D  Q
 . N TEMPW
 . S PXASUB=PXASUB+1
 . S TEMPW=$G(PXAERR("6W"))
 . I TEMPW'="No error" S PXAPROB($J,PXASUB,"WARNING3","ENCOUNTER",1,"SC")=TEMPW
 . S TEMPW=$G(PXAERR("7W"))
 . I TEMPW'="No error" S PXAPROB($J,PXASUB,"WARNING3","ENCOUNTER",1,"AO")=TEMPW
 . S TEMPW=$G(PXAERR("8W"))
 . I TEMPW'="No error" S PXAPROB($J,PXASUB,"WARNING3","ENCOUNTER",1,"IR")=TEMPW
 . S TEMPW=$G(PXAERR("9W"))
 . I TEMPW'="No error" S PXAPROB($J,PXASUB,"WARNING3","ENCOUNTER",1,"EC")=TEMPW
 . S TEMPW=$G(PXAERR("10W"))
 . I TEMPW'="No error" S PXAPROB($J,PXASUB,"WARNING3","ENCOUNTER",1,"MST")=TEMPW
 .;PX*1*111 - Add HNC
 . S TEMPW=$G(PXAERR("17W"))
 . I TEMPW'="No error" S PXAPROB($J,PXASUB,"WARNING3","ENCOUNTER",1,"HNC")=TEMPW
 . S TEMPW=$G(PXAERR("20W"))
 . I TEMPW'="No error" S PXAPROB($J,PXASUB,"WARNING3","ENCOUNTER",1,"CV")=TEMPW
 . S TEMPW=$G(PXAERR("23W"))
 . I TEMPW'="No error" S PXAPROB($J,PXASUB,"WARNING3","ENCOUNTER",1,"SHAD")=TEMPW
 ;
 I PXADI("DIALOG")=8390001.004 D
 . S PXASUB=PXASUB+1
 . S PXAPROB($J,PXASUB,"ERROR4","PX/DL",PXAK)=$G(PXAERR("PL1"))
 Q
 ;
 ;===============
SSCL(SCL) ;Populate the site support contact list. Return a maximum of three.
 N IND,JND,TEMP
 S (IND,JND)=0
 F  S IND=+$O(^PX(815,1,900,IND)) Q:(IND=0)!(JND>2)  D
 . S TEMP=$G(^PX(815,1,900,IND,0))
 . S JND=JND+1
 . S SCL(900+JND)=$P(TEMP,U,1)_"; "_$P(TEMP,U,2)_"; "_$P(TEMP,U,3)_"; "_$P(TEMP,U,4)
 I JND=0 S SCL(901)="No PCE support contacts are defined for this site!"
 Q
 ;
