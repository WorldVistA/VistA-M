PRCHDP5 ;WISC/DJM-PRINT AMENDMENT ;6/29/00  12:18
V ;;5.1;IFCAP;**21,131**;Oct 20, 2000;Build 13
 ;Per VHA Directive 2004-038, this routine should not be modified.
 ;
START(PRCHPO,PRCHAM)  ;PRCHPO IS 442 INTERNAL ENTRY NUMBER FOR RECORD BEING AMENDED.
 ;PRCHAM IS IEN OF AMENDMENT MULTIPLE WITHIN PRCHPO BEING AMENDED.
 ;FIND OUT WHAT TYPES OF AMENDMENTS HAVE BEEN DONE TO BE ABLE TO
 ;DETERMINE WHAT TO PRINT OUT FROM THE AMENDMENTS CHANGES MULTIPLE.
 N AMEND,GOTO
 S AMEND=0 F  S AMEND=$O(^PRC(442,PRCHPO,6,PRCHAM,3,"AC",AMEND)) Q:AMEND'>0  S GOTO="E"_+AMEND_"^PRCHDP5" D @GOTO
 ;D LCNT^PRCHDP9(.LCNT) S LCNTX=LCNT I LCNT>37 S LCNT=LCNT-52
 ;F  Q:LCNT'>41  S LCNT=LCNT-56,PRCHPGT=PRCHPGT+1
 ;S:LCNTX>37 PRCHPGT=PRCHPGT+1
 Q
 ;
E20 ;SHIP TO Edit PRINT
 N FIELD,CHANGE,CHANGES,OLD,NEW,LCNT,DATA,SITE
 D LCNT^PRCHDP9(.LCNT)
 S FIELD=$O(^PRC(442,PRCHPO,6,PRCHAM,3,"AC",AMEND,0)) Q:FIELD'>0
 S CHANGE=$O(^PRC(442,PRCHPO,6,PRCHAM,3,"AC",AMEND,FIELD,0)) Q:CHANGE'>0
 S CHANGES=^PRC(442,PRCHPO,6,PRCHAM,3,CHANGE,0),OLD=^PRC(442,PRCHPO,6,PRCHAM,3,CHANGE,1,1,0)
 S SHIP=$P($P(CHANGES,U,3),";")
 I SHIP=5.4 D
 .S SITE=$P($G(^PRC(442,PRCHPO,23)),U,7),SITE=$S($G(SITE)]"":SITE,1:$P($P(^PRC(442,PRCHPO,0),U),"-"))
 .S OLD=$P(^PRC(411,SITE,1,OLD,0),U)
 .S NEW=$P(^PRC(442,PRCHPO,1),U,3),NEW=$P(^PRC(411,SITE,1,NEW,0),U)
 .Q
 I SHIP=5.3 D
 .S NEW=$P(^PRC(442,PRCHPO,1),U,12),NEW=$P(^PRC(440.2,NEW,0),U),NEW=$P($P(^DPT(NEW,0),U),",",2)_" "_$P($P(^DPT(NEW,0),U),",")
 .S OLD=$P(^PRC(440.2,OLD,0),U),OLD=$P($P(^DPT(OLD,0),U),",",2)_" "_$P($P(^DPT(OLD,0),U),",")
 .Q
 D LINE^PRCHDP9(.LCNT,2) S DATA="Ship to location "_OLD_" has been changed to "_NEW_"."
 D DATA^PRCHDP9(.LCNT,DATA),LCNT1^PRCHDP9(LCNT)
 Q
E21 ;LINE ITEM Add PRINT
 N FIELD,CHANGE,CHANGES,OLD,ITEM,ITEM0,ITEM1,LCNT,DATA,I,UOP
 S FIELD=0 K ITEM D LCNT^PRCHDP9(.LCNT)
 F  S FIELD=$O(^PRC(442,PRCHPO,6,PRCHAM,3,"AC",AMEND,FIELD)) Q:FIELD'>0  D
 .S CHANGE=0 F  S CHANGE=$O(^PRC(442,PRCHPO,6,PRCHAM,3,"AC",AMEND,FIELD,CHANGE)) Q:CHANGE'>0  D
 ..S CHANGES=^PRC(442,PRCHPO,6,PRCHAM,3,CHANGE,0),OLD=^PRC(442,PRCHPO,6,PRCHAM,3,CHANGE,1,1,0)
 ..S ITEM=$P(CHANGES,U,4) Q:$D(ITEM(ITEM))  S ITEM(ITEM)=1
 ..S ITEM0=$G(^PRC(442,PRCHPO,2,ITEM,0))
 ..I ITEM0="" Q
 ..I $P(ITEM0,U,2)=0,$P(ITEM0,U,9)=0 Q
 ..S ITEM1=$G(^PRC(442,PRCHPO,2,ITEM,1,1,0))
 ..D LINE^PRCHDP9(.LCNT,2) S DATA=" *ADDED THROUGH AMENDMENT*" D DATA^PRCHDP9(.LCNT,DATA)
 ..S DATA="Item No. "_$P(ITEM0,U)_"     Item Master File No. "
 ..S DATA=DATA_$P(ITEM0,U,5)_"     BOC: "_+$P(ITEM0,U,4)
 ..S DATA=DATA_"   CONTRACT: "_$P($G(^PRC(442,PRCHPO,2,ITEM,2)),U,2)
 ..D DATA^PRCHDP9(.LCNT,DATA)
 ..D NEW^PRCHDP7
 ..S UOP=$S($P(ITEM0,U,3)>0:$P($G(^PRCD(420.5,$P(ITEM0,U,3),0)),U),1:"")
 ..S DATA="    Items per "_UOP_": "_$P(ITEM0,U,12)
 ..F I=1:1:26-$L(DATA) S DATA=DATA_" "
 ..S DATA=DATA_"NSN: "_$P(ITEM0,U,13) D DATA^PRCHDP9(.LCNT,DATA)
 ..I $P(ITEM0,U,6)]"" S DATA="    STK#: "_$P(ITEM0,U,6) D DATA^PRCHDP9(.LCNT,DATA)
 ..S UOP=$S($P(ITEM0,U,3)>0:$P($G(^PRCD(420.5,$P(ITEM0,U,3),0)),U),1:"")
 ..S AMDQTY=$P(ITEM0,U,2),AMDVAL=$P(ITEM0,U,9) D NXTAMD^PRCHDP6
 ..S DATA="    "_AMDQTY_" "_UOP_" at $"_$J(AMDVAL,12,4)_" = $"_$J(AMDQTY*AMDVAL,9,2)
 ..K AMDQTY,ANDVAL
 ..D DATA^PRCHDP9(.LCNT,DATA),LCNT1^PRCHDP9(LCNT)
 Q
 ;
E22 ;LINE ITEM Delete PRINT
 G E22^PRCHDP6 ;NOT ENOUGH ROOM IN THIS ROUTINE.
 ;
E23 ;LINE ITEM Edit PRINT
 G E23^PRCHDP6
 ;
E24 ;SOURCE CODE Edit PRINT
 G E24^PRCHDP8
 ;
E25 ;Edit MAIL INVOICE TO PRINT
 G E25^PRCHDP7
 ;
E26 ;Edit METHOD OF PAYMENT PRINT
 G E26^PRCHDP7
 ;
E27 ;ADMINISTRATIVE CERTIFICATION Add PRINT
 G E27^PRCHDP7
 ;
E28 ;ADMINISTRATIVE CERTIFICATION Delete PRINT
 G E28^PRCHDP7
 ;
E29 ;EST. SHIPPING Edit PRINT
 G E29^PRCHDP7
 ;
E30 ;F.C.P. Edit PRINT
 G E30^PRCHDP8
 ;
E31 ;Change VENDOR PRINT
 G E31^PRCHDP8
 ;
E32 ;REPLACE P.O. NUMBER PRINT
 G E32^PRCHDP8
 ;
E33 ;PROMPT PAYMENT Edit PRINT
 G E33^PRCHDP8
 ;
E34 ;AUTHORITY Edit PRINT
 G E34^PRCHDP9
 ;
E35 ;F.O.B. Point PRINT
 G E35^PRCHDP9
 ;
E36 ;ITEM DISCOUNT Add/Edit PRINT
 G E36^PRCHDP9
 ;
E37 ;ITEM DISCOUNT Delete PRINT
 G E37^PRCHDP9
 ;
E98 ;DELIVERY DATE PRINT
 Q
E99 ;NET AMOUNT PRINT
 Q
