TIUP328 ;SPFO/AJB - Prescription Drug Monitoring Program ;May 26, 2020@08:16:11
 ;;1.0;TEXT INTEGRATION UTILITIES;**328**;Jun 20, 1997;Build 35
 ;
 ; $$FIND1^DIC     ICR#2051          UPDATE^DIE     ICR#2053
 ;  $$GET1^DIQ     ICR#2056           $$NOW^XLFDT   ICR#10103
 ;    $$UP^XLFSTR  ICR#10104            MES^XPDUTL  ICR#10141
 Q
POST ;
 N CL,DC,DOC,EST,SCR,TIUFPRIV S TIUFPRIV=1
 S SCR="I $P(^(0),U,4)=""CL""" ; screen for the class
 S EST=$$LU(8926.1,"MEDICATION MGT NOTE")
 I '+EST D  Q
 . D MES^XPDUTL(""),MES^XPDUTL("Installation Error:  Enterprise Standard Title not found"),MES^XPDUTL("")
 S CL=$$LU(8925.1,"PROGRESS NOTES","X",SCR)
 I '+CL D  Q
 . D MES^XPDUTL(""),MES^XPDUTL("Installation Error:  PROGRESS NOTES Class not found"),MES^XPDUTL("")
 S DC=$$CRDD^TIUCRDD("PDMP TITLES","DC",11,CL) I '+DC D
 . D MES^XPDUTL($P(DC,U,2)),MES^XPDUTL("")
 S DOC=$$CRDD^TIUCRDD("STATE PRESCRIPTION DRUG MONITORING PROGRAM","DOC",11,DC,$G(EST)) I '+DOC D
 . D MES^XPDUTL($P(DOC,U,2)),MES^XPDUTL("")
 Q
LU(FILE,NAME,FLAGS,SCREEN,INDEXES) ;
 Q $$FIND1^DIC(FILE,"",$G(FLAGS),NAME,$G(INDEXES),$G(SCREEN),"ERR")
