TIU347P ;SPFO/AJB - Clinical Contact Center Titles ;Aug 04, 2021@09:40:28
 ;;1.0;TEXT INTEGRATION UTILITIES;**347**;Jun 20, 1997;Build 2
 ;
 ; BMES^XPDUTL     ICR # 10141
 ; FIND1^DIC       ICR # 2051
 Q
 ;
POST ;
 N DATA,TITLE,X
 ; install new parent document class
 S TITLE="CLINICAL CONTACT CENTER PROGRESS NOTES"
 S DATA=$$CRDD^TIUCRDD(TITLE,"DC",11,"PROGRESS NOTES")
 I '+DATA,$P(DATA,U,2)'["already exists" D BMES^XPDUTL($P(DATA,U,2)) Q
 D BMES^XPDUTL($S(+DATA:TITLE_" created successfully.",1:TITLE_" already exists in 8925.1."))
 ; install titles
 F X=1:1 S DATA=$P($T(TITLES+X),";;",2) Q:DATA=""  D
 . N RESULT S RESULT=$$CRDD^TIUCRDD($P(DATA,U),"DOC",11,TITLE,$P(DATA,U,2))
 . I '+RESULT D BMES^XPDUTL($P(RESULT,U,2)) Q
 . D BMES^XPDUTL($P(DATA,U)_" created successfully.")
 Q
 ;
LU(FILE,NAME,FLAGS,SCREEN,INDEXES) ;
 Q $$FIND1^DIC(FILE,"",$G(FLAGS),NAME,$G(INDEXES),$G(SCREEN))
 ;
TITLES ; local title^enterprise standard title
 ;;CCC: SCHEDULING ADMINISTRATION^ADMINISTRATIVE NOTE
 ;;CCC: PHARMACY I^PHARMACY NOTE
 ;;CCC: PHARMACY II^PHARMACY NOTE
 ;;CCC: CLINICAL TRIAGE^RN PROGRESS NOTE
 ;;CCC: VIRTUAL CLINIC VISITS^PHYSICIAN NOTE
 Q
