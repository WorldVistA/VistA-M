TIU358P ; NA/AJB - POST-INSTALL ;04/06/23  06:19
 ;;1.0;TEXT INTEGRATION UTILITIES;**358**;Jun 20, 1997;Build 16
 ;
 Q
POST ;
 N ZTDESC,ZTDTH,ZTIO,ZTRTN,ZTSK S ZTDESC="Post-Install TIU,358",ZTIO=""
 S ZTDTH=+$H_","_($P($H,",",2)+10),ZTRTN="TASK^TIU358P(DUZ)" D ^%ZTLOAD
 D BMES^XPDUTL("TIU*1.0*358 Post-Install task started.  Task #"_ZTSK)
 Q
TASK(USER) ;
 S ZTREQ="@" N DOC,ENTRY,GBL,IEN S GBL=$NA(^TIU(8925.7,"AC"))
 S ENTRY=0 F  S ENTRY=$O(@GBL@(ENTRY)) Q:'ENTRY  S DOC=0 F  S DOC=$O(@GBL@(ENTRY,DOC)) Q:'DOC  S IEN=0 F  S IEN=$O(@GBL@(ENTRY,DOC,IEN)) Q:'IEN  D
 . N NODE S NODE=$G(^TIU(8925.7,IEN,0)) I +$P(NODE,U,4) K @GBL@(ENTRY,DOC,IEN) D SEND^TIUALRT(DOC) ; signed, kill "AC" index, resend alerts
 N XMDUN,XMDUZ,XMSUB,XMTEXT,XMY,XMZ S XMDUZ=.5,XMSUB="TIU*1.0*358 Post-Install Complete",XMTEXT="XMTEXT(",XMY(USER)=""
 S XMTEXT(1)="TIU*1.0*358 Post-Install task complete."
 D ^XMD
 Q
