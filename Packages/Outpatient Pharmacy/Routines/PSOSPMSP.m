PSOSPMSP ;BIRM/MFR - State Prescription Monitoring Program - State Parameters ;09/04/12
 ;;7.0;OUTPATIENT PHARMACY;**408**;DEC 1997;Build 100
 ;
EN ; - Menu option entry point
 N PSOASVER,PSOINCNV,PSOREPFQ,PSOVMSLD,PSOUNXLD,PSOWINLD,PSOPFIX,PSOEXT,PSOFTPIP
 N PSOFTPUS,PSOFTPPN,PSOFTPDR,PSOTXMOD,PSOQUIT,DIC,DIR,DIRUT,DTOUT,DUOUT,DWPK,LST
 ;
 D FULL^VALM1 W !
 ;
 W ! K DIC S DIC("A")="Select STATE: ",DIC="^DIC(5,",DIC(0)="QOEAM"
 I $O(^PS(58.41,0)) S DIC("B")=$O(^PS(58.41,0))
 K STATEIEN D ^DIC I X=""!(X="^")!$D(DUOUT)!$D(DTOUT) G END
 K DIC("A") G:Y<0 EN S STATEIEN=+Y
 ;
 D DISPLAY(STATEIEN),LOAD(STATEIEN)
 ;
 S PSOQUIT=0
 ;
ASAPVER ; - ASAP Version
 K DIR,DIRUT,DTOUT
 S DIR(0)="58.41,1" S:$G(PSOASVER)'="" DIR("B")=PSOASVER
 D ^DIR I $D(DIRUT)!$D(DTOUT) G @$$GOTO(X,"ASAPVER")
 S PSOASVER=Y D CHANGED(STATEIEN,"ASAPVER",PSOASVER)
 ;
TRXRTS ; - Auto Transmit Returns (ASAP 1995 only)
 I PSOASVER'="1995" S PSOTXRTS="" G INCNVET
 K DIR,DIRUT,DTOUT
 S DIR(0)="58.41,12" S:$G(PSOTXRTS)'="" DIR("B")=$S(PSOTXRTS:"YES",1:"NO")
 D ^DIR I X'="",$D(DIRUT)!$D(DTOUT) G @$$GOTO(X,"TRXRTS")
 S PSOTXRTS=Y D CHANGED(STATEIEN,"TRXRTS",PSOTXRTS)
 ;
INCNVET ; Include Non-Veteran Patients
 K DIR,DIRUT,DTOUT
 S DIR(0)="58.41,2" S DIR("B")=$S(+$G(PSOINCNV):"YES",1:"NO")
 D ^DIR I $D(DIRUT)!$D(DTOUT) G @$$GOTO(X,"INCNVET")
 S PSOINCNV=Y D CHANGED(STATEIEN,"INCNVET",PSOINCNV)
 ;
REPFRQ ; Reporting Frequency
 K DIR,DIRUT,DTOUT
 S DIR(0)="58.41,3" S:$G(PSOREPFQ) DIR("B")=PSOREPFQ
 D ^DIR I $D(DIRUT)!$D(DTOUT) G @$$GOTO(X,"REPFRQ")
 I Y'?1.2N W !,"Invalid Frequency"
 S PSOREPFQ=Y D CHANGED(STATEIEN,"REPFRQ",PSOREPFQ)
 ;
VMSLDIR ; - Open VMS Local Directory
 K DIR,DIRUT,DTOUT
 S DIR(0)="58.41,4" S:$G(PSOVMSLD)'="" DIR("B")=PSOVMSLD
 D ^DIR I X'="",$D(DIRUT)!$D(DTOUT) G @$$GOTO(X,"VMSLDIR")
 S PSOVMSLD=Y D CHANGED(STATEIEN,"VMSLDIR",PSOVMSLD)
 ;
UNXLDIR ; - Unix/Linux Local Directory
 K DIR,DIRUT,DTOUT,NEWDIR
 I $G(PSOUNXLD)="" S PSOUNXLD=$$LINUXDIR^PSOSPMU1()
 S DIR(0)="58.41,15" S:$G(PSOUNXLD)'="" DIR("B")=PSOUNXLD
 D ^DIR I X'="",$D(DIRUT)!$D(DTOUT) G @$$GOTO(X,"UNXLDIR")
 I $G(Y)'="",$E(Y,$L(X))'="/" S Y=Y_"/"
 S PSOUNXLD=Y D CHANGED(STATEIEN,"UNXLDIR",PSOUNXLD)
 ; 
 I $$OS^%ZOSV()="UNIX",$$UP^XLFSTR($$VERSION^%ZOSV(1))["CACHE" D
 . I '$$DIREXIST^PSOSPMU1(PSOUNXLD) D
 . . W ! S DIR("A",1)="The directory above could not be found."
 . . S DIR("A",2)=""
 . . S DIR("A")="Would you like to create it now",DIR(0)="Y",DIR("B")="N"
 . . D ^DIR I $G(DTOUT)!$G(DUOUT)!'Y W ! Q
 . . D MAKEDIR^PSOSPMU1(PSOUNXLD) S NEWDIR=1 W !
 . I '$$DIREXIST^PSOSPMU1(PSOUNXLD) D
 . . W !!,"Warning: "_$S($G(NEWDIR):"The directory could not be created.",1:"The directory could not be found."),!,$C(7)
 ;
WINLDIR ; - Windows/NT Local Directory
 K DIR,DIRUT,DTOUT
 S DIR(0)="58.41,16" S:$G(PSOWINLD)'="" DIR("B")=PSOWINLD
 D ^DIR I X'="",$D(DIRUT)!$D(DTOUT) G @$$GOTO(X,"WINLDIR")
 S PSOWINLD=Y D CHANGED(STATEIEN,"WINLDIR",PSOWINLD)
 ;
FILEPFIX ; - File name Prefix
 K DIR,DIRUT,DTOUT
 S DIR(0)="58.41,5" K DIR("B") S:$G(PSOPFIX)'="" DIR("B")=PSOPFIX
 D ^DIR I X'="",$D(DIRUT)!$D(DTOUT) G @$$GOTO(X,"FILEPFIX")
 I Y[" " W ?40,"No spaces allowed.",$C(7) G FILEPFIX
 S PSOPFIX=Y D CHANGED(STATEIEN,"FILEPFIX",PSOPFIX)
 ;
FILEEXT ; File Extension
 K DIR,DIRUT,DTOUT
 S DIR(0)="58.41,6" S:$G(PSOEXT)'="" DIR("B")=PSOEXT
 D ^DIR I $D(DIRUT)!$D(DTOUT) G @$$GOTO(X,"FILEEXT")
 S PSOEXT=Y D CHANGED(STATEIEN,"FILEEXT",PSOEXT)
 ;
FTPIP ; - State Server FTP IP Address
 K DIR,DIRUT,DTOUT,PV
 S DIR(0)="58.41,7" S:$G(PSOFTPIP)'="" DIR("B")=PSOFTPIP
 D ^DIR I X'="",$D(DIRUT)!$D(DTOUT) G @$$GOTO(X,"FTPIP")
 S PSOFTPIP=Y D CHANGED(STATEIEN,"FTPIP",PSOFTPIP)
 ;
FTPUSER ; - State Server FTP Username
 K DIR,DIRUT,DTOUT
 S DIR(0)="58.41,8" S:$G(PSOFTPUS)'="" DIR("B")=PSOFTPUS
 D ^DIR I X'="",$D(DIRUT)!$D(DTOUT) G @$$GOTO(X,"FTPUSER")
 S PSOFTPUS=Y D CHANGED(STATEIEN,"FTPUSER",PSOFTPUS)
 ;
FTPPORT ; - State Server FTP Password
 K DIR,DIRUT,DTOUT
 S DIR(0)="58.41,9" S:$G(PSOFTPPN)'="" DIR("B")=PSOFTPPN
 D ^DIR I X'="",$D(DIRUT)!$D(DTOUT) G @$$GOTO(X,"FTPPORT")
 S PSOFTPPN=Y D CHANGED(STATEIEN,"FTPPORT",PSOFTPPN)
 ;
FTPDIR ; - State Server FTP Remote Directory
 K DIR,DIRUT,DTOUT
 S DIR(0)="58.41,10" S:$G(PSOFTPDR)'="" DIR("B")=PSOFTPDR
 D ^DIR I X'="",$D(DIRUT)!$D(DTOUT) G @$$GOTO(X,"FTPDIR")
 S PSOFTPDR=Y D CHANGED(STATEIEN,"FTPDIR",PSOFTPDR)
 ;
TRXMODE ; - Transmission Mode (A - Automatic / M - Manual)
 K DIR,DIRUT,DTOUT
 S DIR(0)="58.41,13",DIR("B")=PSOTXMOD
 D ^DIR I X'="",$D(DIRUT)!$D(DTOUT) G @$$GOTO(X,"TRXMODE")
 S PSOTXMOD=Y D CHANGED(STATEIEN,"TRXMODE",PSOTXMOD)
 I PSOTXMOD="M" G EXIT
 ;
PRVKEY ; - Secure FTP Private Key
 I PSOTXMOD="M" G TRXMODE
 W !,"SFTP PRIVATE KEY TEXT: ",$S($O(^TMP("PSOPRVKEY",$J,0)):"<hidden>",1:"<empty>")
 K DIR S DIR(0)="Y",DIR("A")="Edit"
 S DIR("B")=$S($O(^TMP("PSOPRVKEY",$J,0)):"NO",1:"YES")
 D ^DIR I $D(DIRUT)!$D(DTOUT) G @$$GOTO(X,"PRVKEY")
 I $G(Y) D
 . K DIC S DWPK=1,DIC="^TMP(""PSOPRVKEY"","_$J_"," D EN^DIWE
 . I X="^" G EXIT
 . D CHANGED(STATEIEN,"PRVKEY")
 ;
PUBKEY ; - Secure FTP Public Key
 I PSOTXMOD="M" G TRXMODE
 W !,"SFTP PUBLIC KEY TEXT: ",$S($O(^TMP("PSOPUBKEY",$J,0)):"<hidden>",1:"<empty>")
 K DIR S DIR(0)="Y",DIR("A")="Edit"
 S DIR("B")=$S($O(^TMP("PSOPUBKEY",$J,0)):"NO",1:"YES")
 D ^DIR I $D(DIRUT)!$D(DTOUT) G @$$GOTO(X,"PUBKEY")
 I $G(Y) D
 . K DIC S DWPK=1,DIC="^TMP(""PSOPUBKEY"","_$J_"," D EN^DIWE
 . I X="^" G EXIT
 . D CHANGED(STATEIEN,"PUBKEY")
 ;
EXIT ; Exit
 ;
END S VALMBCK="R" Q
 ;
DISPLAY(STATE) ; - Displays the current Parameters
 N ZNODE,FNODE,F1NODE,X
 S ZNODE=$G(^PS(58.41,STATE,0)),FNODE=$G(^PS(58.41,STATE,"FILE")),F1NODE=$G(^PS(58.41,STATE,"FILE1"))
 W ! I ZNODE="" Q
 W !?5,"ASAP VERSION                : ",$P(ZNODE,"^",2)
 I $P(ZNODE,"^",2)="1995" D
 . W !?5,"TRANSMIT RETURN TO STOCK    : ",$S($P(ZNODE,"^",5):"YES",1:"NO")
 W !?5,"INCLUDE NON-VETERAN PATIENTS: ",$S($P(ZNODE,"^",3):"YES",1:"NO")
 W !?5,"REPORTING FREQUENCY IN DAYS : ",$P(ZNODE,"^",4)
 W !?5,"OPEN VMS LOCAL DIRECTORY    : ",$P(FNODE,"^",1)
 W !?5,"UNIX/LINUX LOCAL DIRECTORY  : ",$P(F1NODE,"^",1)
 W !?5,"WINDOWS/NT LOCAL DIRECTORY  : ",$P(F1NODE,"^",2)
 W !?5,"FILE NAME PREFIX            : ",$P(FNODE,"^",2)
 W !?5,"FILE EXTENSION              : ",$P(FNODE,"^",3)
 W !?5,"STATE SFTP SERVER IP ADDRESS: ",$P(FNODE,"^",4)
 W !?5,"STATE SFTP SERVER USERNAME  : ",$P(FNODE,"^",5)
 W !?5,"STATE SFTP SERVER PORT #    : ",$P(FNODE,"^",6)
 W !?5,"STATE SFTP SERVER DIRECTORY : ",$P(FNODE,"^",7)
 W !?5,"SFTP TRANSMISSION MODE      : ",$$GET1^DIQ(58.41,STATE,13)
 I $P(ZNODE,"^",6)="A" D
 . W !?5,"SFTP PRIVATE KEY TEXT       : ",$S($D(^PS(58.41,STATE,"PRVKEY",1)):"<hidden>",1:"<empty>")
 . W !?5,"SFTP PUBLIC KEY TEXT        : ",$S($D(^PS(58.41,STATE,"PUBKEY",1)):"<hidden>",1:"<empty>")
 W !
 Q 
 ;
GOTO(INPUT,HOME) ; - Directed up-arrow
 N GOTO,I,TAG,TRGT,OK2DEL
 I INPUT="@",HOME="ASAPVER",$D(^PS(58.41,STATEIEN)) D  Q GOTO
 . W ! S DIR(0)="Y",DIR("B")="NO",DIR("A")="DELETE SPMP STATE PARAMETERS FOR "_$$GET1^DIQ(5,STATEIEN,.01)
 . D ^DIR I $D(DIRUT)!$D(DTOUT)!(Y=0) S GOTO=HOME W ! Q
 . N DIK,DA S DIK="^PS(58.41,",DA=STATEIEN D ^DIK S GOTO="EN"
 ;
 S OK2DEL="TRXRTS^FTPDIR^FILEPFIX^VMSLDIR^UNXLDIR^WINLDIR^FTPPORT"
 I INPUT="@",OK2DEL'[HOME D  Q HOME
 . W " Cannot delete field",$C(7)
 I INPUT="@" D  Q HOME
 . S:HOME="FTPDIR" PSOFTPDR="" S:HOME="FILEPFIX" PSOPFIX=""
 . S:HOME="VMSLDIR" PSOVMSLD="" S:HOME="UNXLDIR" PSOUNXLD="" S:HOME="WINLDIR" PSOWINLD=""
 . S:HOME="FTPPORT" PSOFTPPN=""
 . D SAVE(STATEIEN,HOME,"")
 ;
 I $P(INPUT,"^",2)="" S PSOQUIT=1 Q "EXIT"
 ;
 S TRGT=$$UP^XLFSTR($P(INPUT,"^",2))
 S TAG(1)="ASAP VERSION^ASAPVER"
 S:PSOASVER="1995" TAG(2)="TRANSMIT RETURN TO STOCK^TRXRTS"
 S TAG(3)="INCLUDE NON-VETERAN PATIENTS^INCNVET"
 S TAG(4)="REPORTING FREQUENCY IN DAYS^REPFRQ"
 S TAG(5)="OPEN VMS LOCAL DIRECTORY^VMSLDIR"
 S TAG(6)="UNIX/LINUX LOCAL DIRECTORY^UNXLDIR"
 S TAG(7)="WINDOWS/NT LOCAL DIRECTORY^WINLDIR"
 S TAG(8)="FILE NAME PREFIX^FILEPFIX"
 S TAG(9)="FILE EXTENSION^FILEEXT"
 S TAG(10)="STATE SFTP SERVER IP ADDRESS^FTPIP"
 S TAG(11)="STATE SFTP SERVER USERNAME^FTPUSER"
 S TAG(12)="STATE SFTP SERVER PORT #^FTPPORT"
 S TAG(13)="STATE SFTP SERVER DIRECTORY^FTPDIR"
 S TAG(14)="SFTP TRANSMISSION MODE^TRXMODE"
 S:PSOTXMOD="A" TAG(15)="SFTP PRIVATE KEY TEXT^PRVKEY"
 S:PSOTXMOD="A" TAG(16)="SFTP PUBLIC KEY TEXT^PUBKEY"
 ;
 S GOTO=HOME,I=""
 F  S I=$O(TAG(I)) Q:'I  S TAG=TAG(I) I $E($P(TAG,"^"),1,$L(TRGT))=TRGT S GOTO=$P(TAG,"^",2) Q
 ;
 Q GOTO
 ;
LOAD(STATE) ; Loading Factory/Division/User preferences
 ;Input : STATE    - Pointer to STATE file (#5)
 ;Output: PSOASVER - ASAP Version
 ;        PSOTXRTS  - Transmit Return To Stock fills
 ;        PSOINCNV - Exclude Non-Veteran Patients
 ;        PSOREPFQ - Reporting Frequency
 ;        PSOVMSLD - Open VMS Local Directory
 ;        PSOUNXLD - Unix/Linux Local Directory
 ;        PSOWINLD - Windows/NT Local Directory
 ;        PSOPFIX  - File Name Prefix
 ;        PSOEXT   - File Extension (.TXT or .DAT)
 ;        PSOFTPIP - State Server FTP IP Address
 ;        PSOFTPUS - State Server FTP Username
 ;        PSOFTPPN - State Server FTP Password
 ;        PSOFTPDR - State Server FTP Directory
 ;        PSOTXMOD - Transmission Mode ('A' or 'M')
 ;        ^TMP("PSOPRVKEY",$J) - SFTP Private Key Text
 ;        ^TMP("PSOPUBKEY",$J) - SFTP Public Key Text
 ;
 K PSOASVER,PSOINCNV,PSOREPFQ,PSOVMSLD,PSOUNXLD,PSOWINLD,PSOPFIX,PSOEXT,PSOFTPIP,PSOFTPUS
 K PSOFTPPN,PSOFTPDR
 ;
 N X,ZNODE,FNODE,F1NODE,KEY,LINE
 ;
 ; - Defaults
 S PSOASVER="4.2"
 S PSOTXRTS="0"
 S PSOINCNV=0
 S PSOREPFQ="1"
 S PSOVMSLD=""
 S PSOUNXLD=""
 S PSOWINLD=""
 S PSOPFIX=""
 S PSOEXT=".DAT"
 S PSOFTPIP=""
 S PSOFTPUS=""
 S PSOFTPPN=""
 S PSOFTPDR=""
 S PSOTXMOD="A"
 K ^TMP("PSOPRVKEY",$J)
 K ^TMP("PSOPUBKEY",$J)
 ;
 ; - State's parameters
 I $D(^PS(58.41,STATE,0)) D
 . S ZNODE=$G(^PS(58.41,STATE,0)) I $P(ZNODE,"^",1)'=STATE Q
 . S FNODE=$G(^PS(58.41,STATE,"FILE")),F1NODE=$G(^PS(58.41,STATE,"FILE1"))
 . S:$P(ZNODE,"^",2)'="" PSOASVER=$P(ZNODE,"^",2)
 . S:$P(ZNODE,"^",3)'="" PSOINCNV=$P(ZNODE,"^",3)
 . S:$P(ZNODE,"^",4)'="" PSOREPFQ=$P(ZNODE,"^",4)
 . S:$P(ZNODE,"^",5)'="" PSOTXRTS=$P(ZNODE,"^",5)
 . S:$P(ZNODE,"^",6)'="" PSOTXMOD=$P(ZNODE,"^",6)
 . S:$P(FNODE,"^",1)'="" PSOVMSLD=$P(FNODE,"^",1)
 . S:$P(F1NODE,"^",1)'="" PSOUNXLD=$P(F1NODE,"^",1)
 . S:$P(F1NODE,"^",2)'="" PSOWINLD=$P(F1NODE,"^",2)
 . S:$P(FNODE,"^",2)'="" PSOPFIX=$P(FNODE,"^",2)
 . S:$P(FNODE,"^",3)'="" PSOEXT=$P(FNODE,"^",3)
 . S:$P(FNODE,"^",4)'="" PSOFTPIP=$P(FNODE,"^",4)
 . S:$P(FNODE,"^",5)'="" PSOFTPUS=$P(FNODE,"^",5)
 . S:$P(FNODE,"^",6)'="" PSOFTPPN=$P(FNODE,"^",6)
 . S:$P(FNODE,"^",7)'="" PSOFTPDR=$P(FNODE,"^",7)
 . F KEY="PRVKEY","PUBKEY" D
 . . S ^TMP("PSO"_KEY,$J,0)=$G(^PS(58.41,STATE,KEY,0))
 . . F LINE=1:1 Q:'$D(^PS(58.41,STATE,KEY,LINE))  D
 . . . S ^TMP("PSO"_KEY,$J,LINE,0)=$$DECRYP^XUSRB1(^PS(58.41,STATE,KEY,LINE,0))
 Q
 ;
CHANGED(STATE,FIELD,VALUE) ; - If field was change Automatically Invokes SAVE
 ;Input : STATE - Pointer to STATE file (#5)
 ;        FIELD - Field ID
 ;        VALUE - Field Value
 ;
 N CHANGED,ZNODE,FNODE,F1NODE,LINE
 ;
 S CHANGED=0
 ; - Saved Division's parameters
 S ZNODE=$G(^PS(58.41,STATE,0)),FNODE=$G(^PS(58.41,STATE,"FILE")),F1NODE=$G(^PS(58.41,STATE,"FILE1"))
 ;
 I FIELD="ASAPVER",VALUE'=$P(ZNODE,"^",2) S CHANGED=1
 I FIELD="TRXRTS",VALUE'=$P(ZNODE,"^",5) S CHANGED=1
 I FIELD="INCNVET",VALUE'=$P(ZNODE,"^",3) S CHANGED=1
 I FIELD="VMSLDIR",VALUE'=$P(FNODE,"^",1) S CHANGED=1
 I FIELD="UNXLDIR",VALUE'=$P(F1NODE,"^",1) S CHANGED=1
 I FIELD="WINLDIR",VALUE'=$P(F1NODE,"^",2) S CHANGED=1
 I FIELD="REPFRQ",VALUE'=$P(ZNODE,"^",4) S CHANGED=1
 I FIELD="FILEPFIX",VALUE'=$P(FNODE,"^",2) S CHANGED=1
 I FIELD="FILEEXT",VALUE'=$P(FNODE,"^",3) S CHANGED=1
 I FIELD="FTPIP",VALUE'=$P(FNODE,"^",4) S CHANGED=1
 I FIELD="FTPUSER",VALUE'=$P(FNODE,"^",5) S CHANGED=1
 I FIELD="FTPPORT",VALUE'=$P(FNODE,"^",6) S CHANGED=1
 I FIELD="FTPDIR",VALUE'=$P(FNODE,"^",7) S CHANGED=1
 I FIELD="TRXMODE",VALUE'=$P(ZNODE,"^",6) S CHANGED=1
 I FIELD="PRVKEY"!(FIELD="PUBKEY") D
 . I $P($G(^PS(58.41,STATE,FIELD,0)),"^",1,5)'=$P($G(^TMP("PSO"_FIELD,$J,0)),"^",1,5) S CHANGED=1 Q
 . F LINE=1:1 Q:'$D(^PS(58.41,STATE,FIELD,LINE))  D  I $G(CHANGED) Q
 . . I $$DECRYP^XUSRB1(^PS(58.41,STATE,FIELD,LINE,0))'=$G(^TMP("PSO"_FIELD,$J,LINE,0)) S CHANGED=1
 ;
 I CHANGED D SAVE(STATE,FIELD,$G(VALUE))
 Q
 ;
SAVE(STATE,FIELD,VALUE) ; - Saves preferences by Sate
 ;Input : STATE - Pointer to STATE file (#5)
 ;        FIELD - Field ID
 ;        VALUE - Field Value
 ;
 N DIE,DR,DA,KEY,LINE
 ;
 I '$D(^PS(58.41,STATE)) D
 . N %,DIC,DR,DA,X,DINUM,DLAYGO,DD,DO
 . S DIC="^PS(58.41,",(DINUM,X)=STATE,DIC(0)=""
 . K DD,DO D FILE^DICN K DD,DO
 ;
 I FIELD="ASAPVER" S DR="1///"_PSOASVER
 I FIELD="TRXRTS" S DR="12////"_PSOTXRTS
 I FIELD="INCNVET" S DR="2///"_PSOINCNV
 I FIELD="VMSLDIR" S DR="4////"_$S(PSOVMSLD="":"@",1:PSOVMSLD)
 I FIELD="UNXLDIR" S DR="15////"_$S(PSOUNXLD="":"@",1:PSOUNXLD)
 I FIELD="WINLDIR" S DR="16////"_$S(PSOWINLD="":"@",1:PSOWINLD)
 I FIELD="REPFRQ" S DR="3///"_PSOREPFQ
 I FIELD="FILEPFIX" S DR="5////"_$S(PSOPFIX="":"@",1:PSOPFIX)
 I FIELD="FILEEXT" S DR="6///"_PSOEXT
 I FIELD="FTPIP" S DR="7///"_PSOFTPIP
 I FIELD="FTPUSER" S DR="8///"_PSOFTPUS
 I FIELD="FTPPORT" S DR="9////"_$S(PSOFTPPN="":"@",1:PSOFTPPN)
 I FIELD="FTPDIR" S DR="10////"_$S(PSOFTPDR="":"@",1:PSOFTPDR)
 I FIELD="TRXMODE" S DR="13////"_PSOTXMOD
 I FIELD="PRVKEY"!(FIELD="PUBKEY") D
 . K ^PS(58.41,STATE,FIELD)
 . I '$D(^TMP("PSO"_FIELD,$J,0)) Q
 . S ^PS(58.41,STATE,FIELD,0)=^TMP("PSO"_FIELD,$J,0)
 . F LINE=1:1 Q:'$D(^TMP("PSO"_FIELD,$J,LINE))  D
 . . S ^PS(58.41,STATE,FIELD,LINE,0)=$$ENCRYP^XUSRB1(^TMP("PSO"_FIELD,$J,LINE,0))
 E  D
 . S DIE="^PS(58.41,",DA=STATE D ^DIE
 Q
