PSOERX1 ;ALB/BWF - eRx Utilities/RPC's ; 8/3/2016 5:14pm
 ;;7.0;OUTPATIENT PHARMACY;**467,520**;DEC 1997;Build 52
 ;
EN(PSOIEN) ; -- main entry point for PSO ERX HOLDING QUEUE
 D EN^VALM("PSO ERX HQ DISPLAY")
 Q
 ;
HDR ; -- header code
 S VALMHDR(1)="eRx Patient: "_$$GET1^DIQ(52.49,PSOIEN,.04,"E")
 S VALMHDR(2)="eRx Reference #: "_$$GET1^DIQ(52.49,PSOIEN,.01,"E")
 I VALMBCK="R" K @VALMAR S VALMBCK="" D INIT
 Q
 ;
INIT ;
 Q:'$G(PSOIEN)
 N PATIEN,PHARMIEN,PRVIEN,PHDAT,PATDAT,SUPDAT,PRVDAT,LINE,PRVLNM,PRVMI,PRVFN,EPAT,EPATDOB,VPATIEN
 N VPATNM,VPATDOB,EPRVIEN,EPRVNM,EPRVNPI,VAPRVIEN,VAPRVNM,VAPRVNPI,SUPIEN,ERXDRUG,ERXQTY,ERXFLS
 N ERXDS,ERXDT,VADRG,LINETXT,ERXCOMM,ERXRFLS,PATIENS,VAHREA,VAQTY,VAREF,VASIG,PATDAT,CURSTATE,CURSTATI
 N LHFOUND,LHMATCH,LHSTATI,VAPDEAEX,ERXCOMM,COMFRST,COMARY,SIGDATA,SIGLOOP,SFIRST,SGLOOP,SIGARY,VADAYS
 N SLOOP,VASIG,VASARY,FSSIG,VAHSTA,EDIRECT,VAHPER,PAMANVAL,DRMANVAL,PRMANVAL,VPATINST,VAPIARY,VLOOP,FSVPIN
 N DRGARY,DLP
 S LINE=0
 S PATIEN=$$GET1^DIQ(52.49,PSOIEN,.04,"I"),PATIENS=PATIEN_","
 D GETS^DIQ(52.46,PATIENS,"**","IE","PATDAT")
 S EPAT=$G(PATDAT(52.46,PATIENS,.01,"E"))
 S EPATDOB=$G(PATDAT(52.46,PATIENS,.08,"I")),EPATDOB=$$FMTE^XLFDT(EPATDOB,"2D")
 ; Future enhancement - if 52.46 is linked to the patient in the patient file, use it. for now use 52.49
 ;S VPATIEN=$G(PATDAT(52.46,PATIENS,1.5,"I"))
 S VPATIEN=$$GET1^DIQ(52.49,PSOIEN,.05,"I")
 S VPATNM=$S(VPATIEN:$$GET1^DIQ(2,VPATIEN,.01,"E"),1:"NOT LINKED")
 S VPATDOB=$S(VPATIEN:$$GET1^DIQ(2,VPATIEN,.03,"I"),1:"N/A")
 I VPATDOB S VPATDOB=$$FMTE^XLFDT(VPATDOB,"2D")
 S PHARMIEN=$$GET1^DIQ(52.49,PSOIEN,2.5,"I")
 D GETS^DIQ(52.47,PHARMIEN,"**","E","PHDAT")
 S EPRVIEN=$$GET1^DIQ(52.49,PSOIEN,2.1,"I")
 S EPRVNM=$$GET1^DIQ(52.48,EPRVIEN,.01,"E")
 S EPRVNPI=$$GET1^DIQ(52.48,EPRVIEN,1.5,"E")
 S VAPRVIEN=$$GET1^DIQ(52.49,PSOIEN,2.3,"I")
 S EDIRECT=$$GET1^DIQ(52.49,PSOIEN,7,"E")
 S VAPRVNM=$S(VAPRVIEN:$$GET1^DIQ(200,VAPRVIEN,.01,"E"),1:"NOT LINKED")
 S VAPRVNPI=$S(VAPRVIEN:$$GET1^DIQ(200,VAPRVIEN,41.99,"E"),1:"N/A")
 D GETS^DIQ(52.48,EPRVIEN,"**","E","PRVDAT")
 S SUPIEN=$$GET1^DIQ(52.49,PSOIEN,2.6,"I")
 D GETS^DIQ(52.48,SUPIEN,"**","E","SUPDAT")
 S PAMANVAL=$$GET1^DIQ(52.49,PSOIEN,1.7,"I")
 S PRMANVAL=$$GET1^DIQ(52.49,PSOIEN,1.3,"I")
 S DRMANVAL=$$GET1^DIQ(52.49,PSOIEN,1.5,"I")
 S LINETXT=""
 S LINE=LINE+1
 ;/BLB/ - PSO*7.0*520 BEGIN CHANGE
 D ADDITEM^PSOERX1A(.LINETXT,"eRx Patient: ",$E(EPAT,1,39),1,52)
 D ADDITEM^PSOERX1A(.LINETXT,"DOB: ",EPATDOB,57,20)
 D SET^VALM10(LINE,LINETXT) S LINETXT=""
 I $L(EPAT)>39 D
 .S LINE=LINE+1 D SET^VALM10(LINE,"             "_$E(EPAT,40,78))
 I $L(EPAT)>78 D
 .S LINE=LINE+1 D SET^VALM10(LINE,"             "_$E(EPAT,79,135))
 ;/BLB/ - END CHANGE
 S LINETXT=""
 S LINE=LINE+1
 D ADDITEM^PSOERX1A(.LINETXT,"Vista Patient"_$S(PAMANVAL:"[v]",1:"")_": ",$E(VPATNM,1,55),1,55)
 D ADDITEM^PSOERX1A(.LINETXT,"DOB: ",VPATDOB,57,20)
 D SET^VALM10(LINE,LINETXT) S LINETXT=""
 S LINE=LINE+1 D SET^VALM10(LINE,"")
 S LINE=LINE+1
 ;/BLB/ PSO*7.0*520 - BEGIN CHANGE
 D ADDITEM^PSOERX1A(.LINETXT,"eRx Provider: ",$E(EPRVNM,1,39),1,52)
 D ADDITEM^PSOERX1A(.LINETXT,"NPI: ",EPRVNPI,57,20)
 D SET^VALM10(LINE,LINETXT) S LINETXT=""
 I $L(EPRVNM)>39 D
 .S LINE=LINE+1 D SET^VALM10(LINE,"              "_$E(EPRVNM,40,77))
 I $L(EPRVNM)>77 D
 .S LINE=LINE+1 D SET^VALM10(LINE,"              "_$E(EPRVNM,78,135))
 ;/BLB/ - END CHANGE
 S LINE=LINE+1
 D ADDITEM^PSOERX1A(.LINETXT,"Vista Provider"_$S(PRMANVAL:"[v]",1:"")_": ",$E(VAPRVNM,1,55),1,55)
 D ADDITEM^PSOERX1A(.LINETXT,"NPI: ",VAPRVNPI,57,20)
 D SET^VALM10(LINE,LINETXT) S LINETXT=""
 S LINE=LINE+1 D SET^VALM10(LINE,"")
 S ERXDRUG=$$GET1^DIQ(52.49,PSOIEN,3.1,"E")
 S ERXQTY=$$GET1^DIQ(52.49,PSOIEN,5.1,"E")
 S ERXRFLS=$$GET1^DIQ(52.49,PSOIEN,5.6,"E")
 ;/BLB/ PSO*7.0*520 - BEGIN CHANGE
 I ERXRFLS="" D
 .S ERXRFLS=$$GET1^DIQ(52.49,PSOIEN,5.7,"I")
 ;/BLB/ - END CHANGE
 S ERXDS=$$GET1^DIQ(52.49,PSOIEN,5.5,"E")
 S ERXDT=$$GET1^DIQ(52.49,PSOIEN,.03,"E")
 S VADRG=$$GET1^DIQ(52.49,PSOIEN,3.2,"E")
 S VAREF=$$GET1^DIQ(52.49,PSOIEN,20.5,"E")
 S VAQTY=$$GET1^DIQ(52.49,PSOIEN,20.1,"E")
 S VADAYS=$$GET1^DIQ(52.49,PSOIEN,20.2,"E")
 ; only set the hold reason if the eRx has a hold status
 S CURSTATE=$$GET1^DIQ(52.49,PSOIEN,1,"E")
 S VAHREA=""
 I $E(CURSTATE,1)="H" D
 .S CURSTATI=$$GET1^DIQ(52.49,PSOIEN,1,"I")
 .S LHMATCH=999999,LHFOUND=0 F  S LHMATCH=$O(^PS(52.49,PSOIEN,19,LHMATCH),-1) Q:'LHMATCH!(LHFOUND)  D
 ..S LHSTATI=$$GET1^DIQ(52.4919,LHMATCH_","_PSOIEN_",",.02,"I") I LHSTATI=CURSTATI D  S LHFOUND=LHMATCH Q
 ...S VAHREA=$$GET1^DIQ(52.4919,LHMATCH_","_PSOIEN_",",1)
 ...S VAHSTA=$$GET1^DIQ(52.45,LHSTATI,.01,"E")_" - "_$$GET1^DIQ(52.45,LHSTATI,.02,"E")
 ...S VAHPER=$$GET1^DIQ(52.4919,LHMATCH_","_PSOIEN_",",.03,"E")
 I VADRG']"" S VADRG="NOT LINKED"
 D TXT2ARY^PSOERXD1(.DRGARY,ERXDRUG,,70)
 S LINE=LINE+1 D SET^VALM10(LINE,"eRx Drug: "_$G(DRGARY(1)))
 S DLP=1
 F  S DLP=$O(DRGARY(DLP)) Q:'DLP  D
 .S LINE=LINE+1 D SET^VALM10(LINE,"          "_$G(DRGARY(DLP)))
 S LINE=LINE+1
 D ADDITEM^PSOERX1A(.LINETXT,"eRx Qty: ",ERXQTY,1,17)
 D ADDITEM^PSOERX1A(.LINETXT,"eRx Refills: ",ERXRFLS,19,16)
 D ADDITEM^PSOERX1A(.LINETXT,"eRx Days Supply: ",ERXDS,37,20)
 D ADDITEM^PSOERX1A(.LINETXT,"eRx Date: ",$P(ERXDT,"@"),58,22)
 D SET^VALM10(LINE,LINETXT) S LINETXT=""
 D TXT2ARY^PSOERXD1(.SIGARY,EDIRECT,,70)
 S SFIRST=$O(SIGARY(0))
 S SGLOOP=0 F  S SGLOOP=$O(SIGARY(SGLOOP)) Q:'SGLOOP  D
 .S LINE=LINE+1 D SET^VALM10(LINE,$S(SGLOOP=SFIRST:"eRx Sig: ",1:"         ")_$G(SIGARY(SGLOOP)))
 S LINE=LINE+1 D SET^VALM10(LINE,"")
 S LINE=LINE+1 D SET^VALM10(LINE,"Vista Drug"_$S(DRMANVAL:"[v]",1:"")_": "_VADRG)
 S LINE=LINE+1
 D ADDITEM^PSOERX1A(.LINETXT,"Vista Qty: ",$G(VAQTY),1,25)
 D ADDITEM^PSOERX1A(.LINETXT,"Vista Refills: ",$G(VAREF),27,18)
 D ADDITEM^PSOERX1A(.LINETXT,"Vista Days Supply: ",$G(VADAYS),54,22)
 D SET^VALM10(LINE,LINETXT) S LINETXT=""
 S VASIG=""
 S SLOOP=0 F  S SLOOP=$O(^PS(52.49,PSOIEN,"SIG",SLOOP)) Q:'SLOOP  D
 .I '$L($G(VASIG)) S VASIG=$G(^PS(52.49,PSOIEN,"SIG",SLOOP,0)) Q
 .S VASIG=$G(VASIG)_" "_$G(^PS(52.49,PSOIEN,"SIG",SLOOP,0))
 D TXT2ARY^PSOERXD1(.VASARY,VASIG,,68)
 S FSSIG=$O(VASARY(0))
 S LINE=LINE+1 D SET^VALM10(LINE,"Vista Sig: "_$S(FSSIG:$G(VASARY(FSSIG)),1:""))
 S SLOOP=1 F  S SLOOP=$O(VASARY(SLOOP)) Q:'SLOOP  D
 .S LINE=LINE+1 D SET^VALM10(LINE,"                        "_VASARY(SLOOP))
 S VPATINST=$$GET1^DIQ(52.49,PSOIEN,27,"E")
 I VPATINST]"" S VPATINST=$$LSIG^PSOQUTIL(VPATINST)
 D TXT2ARY^PSOERXD1(.VAPIARY,VPATINST,68)
 S FSVPIN=$O(VAPIARY(0))
 S LINE=LINE+1 D SET^VALM10(LINE," Pat Inst: "_$S(FSVPIN:$G(VAPIARY(FSVPIN)),1:""))
 S VLOOP=1 F  S VLOOP=$O(VAPIARY(VLOOP)) Q:'VLOOP  D
 .S LINE=LINE+1 D SET^VALM10(LINE,"                        "_VAPIARY(VLOOP))
 S LINE=LINE+1 D SET^VALM10(LINE,"Hold Status: "_$G(VAHSTA))
 S LINE=LINE+1 D SET^VALM10(LINE,"Hold Reason: "_$G(VAHREA))
 S LINE=LINE+1 D SET^VALM10(LINE,"Placed on hold by: "_$G(VAHPER))
 S LINE=LINE+1 D SET^VALM10(LINE,"")
 S ERXCOMM=$$GET1^DIQ(52.49,PSOIEN,8,"E")
 D TXT2ARY^PSOERXD1(.COMARY,ERXCOMM," ",68)
 S COMFRST=$O(COMARY(0)) S LINE=LINE+1 D SET^VALM10(LINE,"eRx Notes: "_$G(ERXCOMM))
 F  S COMFRST=$O(COMARY(COMFRST)) Q:'COMFRST  D
 .S LINE=LINE+1 D SET^VALM10(LINE,"          "_$G(COMARY(COMFRST)))
 S LINE=LINE+1 D SET^VALM10(LINE,"")
 ;/BLB/ PSO*7.0*520 - BEGIN
 I $$GET1^DIQ(52.49,PSOIEN,.05,"I") D
 .D ALG^PSOERXU1(.LINE)
 ;/BLB/ - END
 N DIAGIEN,DIAGDAT,DIAGSEQ,PDIAGQ,PDIAGV,SDIAGQ,SDIAGV,DIAGDAT,DIACIC,DRES,SDRES
 N SDRESL,SDRESDAT,DRESL,DRESDAT,PDIAGARY,SDIAGARY,PDFRST,SDFRST,PDLOOP,SDLOOP,DIENS
 S DIAGIEN=0 F  S DIAGIEN=$O(^PS(52.49,PSOIEN,9,DIAGIEN)) Q:'DIAGIEN  D
 .S DIENS=DIAGIEN_","_PSOIEN_"," K PDIAGARY,SDIAGARY,DIAGDAT
 .D GETS^DIQ(52.499,DIENS,".01:.06","IE","DIAGDAT")
 .S DIAGSEQ=$G(DIAGDAT(52.499,DIENS,.01,"E"))
 .S DIACIC=$G(DIAGDAT(52.499,DIENS,.02,"E"))
 .S PDIAGQ=$G(DIAGDAT(52.499,DIENS,.03,"E"))
 .S PDIAGV=$G(DIAGDAT(52.499,DIENS,.04,"E"))
 .I PDIAGQ="ICD-10-CM"!(PDIAGQ="ICD-9-CM") D
 ..K DRES,PDIAGARY
 ..D ICDDESC^ICDXCODE(PDIAGQ,PDIAGV,,.DRES)
 ..I '$O(DRES(0)) D TXT2ARY^PSOERXD1(.PDIAGARY,PDIAGV," ",62) Q
 ..S DRESL=0 F  S DRESL=$O(DRES(DRESL)) Q:'DRESL  D
 ...S DRESDAT=$G(DRES(DRESL)) Q:DRESDAT=""
 ...I DRESL=1 S PDIAGV=PDIAGV_" - "_DRESDAT Q
 ...S PDIAGV=PDIAGV_" "_DRESDAT
 ..D TXT2ARY^PSOERXD1(.PDIAGARY,PDIAGV," ",62)
 .S SDIAGQ=$G(DIAGDAT(52.499,DIENS,.05,"E"))
 .S SDIAGV=$G(DIAGDAT(52.499,DIENS,.06,"E"))
 .I SDIAGQ="ICD-10-CM"!(SDIAGQ="ICD-9-CM") D
 ..K SDRES,SDIAGARY
 ..D ICDDESC^ICDXCODE(SDIAGQ,SDIAGV,,.SDRES)
 ..I '$O(DRES(0)) D TXT2ARY^PSOERXD1(.SDIAGARY,SDIAGV," ",62) Q
 ..S SDRESL=0 F  S SDRESL=$O(SDRES(SDRESL)) Q:'SDRESL  D
 ...S SDRESDAT=$G(SDRES(SDRESL)) Q:SDRESDAT=""
 ...I SDRESL=1 S SDIAGV=SDIAGV_" - "_SDRESDAT Q
 ...S SDIAGV=SDIAGV_" "_SDRESDAT
 ..D TXT2ARY^PSOERXD1(.SDIAGARY,SDIAGV," ",62)
 .S LINE=LINE+1 D SET^VALM10(LINE,"Diagnosis Sequence: "_DIAGSEQ)
 .S LINE=LINE+1 D SET^VALM10(LINE,"Primary DX Qualifier: "_PDIAGQ)
 .S PDFRST=$O(PDIAGARY(0))
 .S LINE=LINE+1 D SET^VALM10(LINE,"Primary Dx Value: "_$S(PDFRST:$G(PDIAGARY(PDFRST)),1:PDIAGV))
 .S PDLOOP=PDFRST F  S PDLOOP=$O(PDIAGARY(PDLOOP)) Q:'PDLOOP  D
 ..S LINE=LINE+1 D SET^VALM10(LINE,"                  "_$G(PDIAGARY(PDLOOP)))
 .S LINE=LINE+1 D SET^VALM10(LINE,"")
 .S LINE=LINE+1 D SET^VALM10(LINE,"Secondary DX Qualifier: "_SDIAGQ)
 .S SDFRST=$O(SDIAGARY(0))
 .S LINE=LINE+1 D SET^VALM10(LINE,"Secondary Dx Value: "_$S(SDFRST:$G(SDIAGARY(SDFRST)),1:SDIAGV))
 .S SDLOOP=SDFRST F  S SDLOOP=$O(SDIAGARY(SDLOOP)) Q:'SDLOOP  D
 ..S LINE=LINE+1 D SET^VALM10(LINE,"                    "_$G(SDIAGARY(SDLOOP)))
 .I $O(^PS(52.49,PSOIEN,9,DIAGIEN)) S LINE=LINE+1 D SET^VALM10(LINE,"")
 S VALMCNT=LINE
 Q
 ;
HELP ; -- help code
 S X="?" D DISP^XQORM1 W !!
 Q
 ;
EXIT ; -- exit code
 K @VALMAR
 ;D INIT^PSOERX
 Q
 ;
EXPND ; -- expand code
 Q
