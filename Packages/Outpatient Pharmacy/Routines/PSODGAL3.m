PSODGAL3 ; BIR/LC,SAB,CMF - enhanced DRUG ALLERGY REACTION CHECKING continued ;12/09/07  02:22
 ;;7.0;OUTPATIENT PHARMACY;**411,458**;DEC 1997;Build 2
 ;External reference to ^XTMP("ORRDI" supported by DBIA 4659
 ;
SORTN ;
 N STYP,INGLOC,ZALL,ZLOC,ZCDATE,PSOSYM,PSOCDATE,PSOCA2,INGREDS,PSOREACT,PSOSDATA,SEVT,PSOAFLG,INGRED,PSOACNT,PSOAFEND
 N INGREDS,INGREDZ,ZXX,PSOSSITE,PSOSARRY,SETTMP,PSOLOC,ZCNT,ZING,ZMSG,PSOATYPE,PSODGCLS,PSODGCL,PSODGCL1 K ZGMRA
 S ZCNT=0
 ;
 I $G(PSOTSTMD) D TSTREM
 ;  
 K PSOSORT,PSOSEV,PSOSEVP,PSOSTYP,PSOCAGNT,PSOCA
 S (ZALL,ZING,ZMSG,PSOSYMS)=""
 ;DRUG INGREDIENTS
 F  S ZMSG=$O(GMRARSLT(ZMSG)) Q:ZMSG=""  D 
 .S ZALL=$$GMSGPTR^PSODGAL1(ZMSG)
 .Q:ZALL=""
 .S PSODRCL=0
 .S PSOATYPE=2
 .S:$D(GMRARSLT(ZMSG,"MESSAGE","OFFENDERS","CLS")) PSODRCL=1,PSOATYPE=1
 .D SORTM
 .Q
 D TMP
 Q
 ;
SORTM ;
 N SITEARY,PSOONEA,ZSITES,ZSITE,ZYALL
 S (PSOSEV,PSOSEVT,PSOSTYP,PSOLOC,PSOSYM,INGREDS,PSOREACT,INGLOC,PSOSDATA,PSOSSITE,PSOSITT)=""
 S PSOSSITE=$P(GMRARSLT(ZMSG,ZALL),U)
 S PSOLOC=PSOSSITE_"|"_$P(GMRARSLT(ZMSG,ZALL),U)
 S PSOSEV=$$GETSEV(PSOSSITE,ZMSG,.GMRARSLT)
 S PSOSEVT=$S(PSOSEV="SEVERE":1,PSOSEV="MODERATE":2,PSOSEV="MILD":3,1:99)
 S PSOSEVP=PSOSEV
 S PSOSTYP=PSOSEVT
 S:PSOSEVT=1 PSOSEVT1("S")=1
 S ZSITES=GMRARSLT(ZMSG,"MESSAGE",1)
 S PSOSDATA=""
 S ZYALL=""
 F ZSITE=1:1:ZSITES D 
 .S ZYALL=$$GMSGPTR^PSODGAL1(ZMSG,ZYALL)
 .S INGLOC=$S($P(GMRARSLT(ZMSG,ZYALL),U,2)="L":"L",1:"R")
 .S PSOCDATE=$P($P(GMRARSLT(ZMSG,ZYALL),U,3),".")
 .S PSOCDATE=$E(PSOCDATE,4,5)_"/"_$E(PSOCDATE,6,7)_"/"_$E(PSOCDATE,2,3)
 .S PSOSYM=$P(GMRARSLT(ZMSG,ZYALL),U,5)
 .S PSOREACT=$P(GMRARSLT(ZMSG,"MESSAGE",2),U,2)
 .S PSOSITT=$P(GMRARSLT(ZMSG,ZYALL),U)
 .S PSOSDATA=ZMSG_"|"_PSOCDATE_"|"_INGLOC_"|"_PSOSITT
 .;S PSOSDATA=PSOSDATA_$S(ZSITE=1:"",1:U)_ZMSG_"|"_PSOCDATE_"|"_INGLOC_"|"_PSOSITT
 .S SITEARY=""
 .S SITEARY=INGLOC_"|"_$E(PSOSSITE,1,16)_"|"_PSOSITT_"|"_$S($D(^DIC(4,PSOSITT)):PSOSITT,1:"")
 .S PSOSARRY(PSOSTYP,SITEARY,PSOATYPE,PSOREACT,ZMSG)=$S(PSOATYPE=1:PSOREACT,1:"")
 .;
 .D:$D(PSOCAGNT(PSOSTYP,PSOREACT,PSOATYPE)) 
 ..S (OLDDATA,PSOCA)=""
 ..S OLDDATA=PSOCAGNT(PSOSTYP,PSOREACT,PSOATYPE)
 ..D:OLDDATA'[PSOSDATA 
 ...S PSOCA=PSOCAGNT(PSOSTYP,PSOREACT,PSOATYPE)_"^"_PSOSDATA
 ...S PSOCAGNT(PSOSTYP,PSOREACT,PSOATYPE)=PSOCA
 .D:'$D(PSOCAGNT(PSOSTYP,PSOREACT,PSOATYPE)) 
 ..S PSOCAGNT(PSOSTYP,PSOREACT,PSOATYPE)=PSOSDATA
 .D SYM
 .;
 .F INGREDZ="CLS","ING" D 
 ..S (INGRED,INGREDS)="",INGREDS=$G(GMRARSLT(ZMSG,"MESSAGE","OFFENDERS",INGREDZ))
 ..N III F III=1:1 S INGRED=$P(INGREDS,"~",III) Q:INGRED=""  D 
 ...S PSOCAGNT(PSOSTYP,PSOREACT,PSOATYPE,INGRED)=""
 ...Q:INGREDZ="ING"
 ...I $G(PSODRCL) S PSODRCL1(PSOSTYP,PSOREACT,PSOATYPE,INGRED)="" K PSOCAGNT(PSOSTYP,PSOREACT,PSOATYPE,INGRED)
 Q
 ;
SYM ; 
 I PSOSYM'["~"&(PSOSYM'="")&(PSOSYM'="|") D  Q
 .S PSOSYM("Name")=$$GETSYMNM(ZYALL,PSOSYM,1)
 .Q:PSOSYM("Name")=""
 .S PSOSYMS(PSOSTYP,PSOREACT,PSOATYPE,"ZZZSYMPTOMS",PSOSYM("Name"))=PSOSYM
 .S PSOSARRY(PSOSTYP,SITEARY,PSOATYPE,PSOREACT,ZMSG,"ZZZSYMPTOMS",PSOSYM("Name"))=PSOSYM
 Q:PSOSYM="|"
 N PSOQX,PSOY
 I $E(PSOSYM,1)="~" S PSOSYM=$E(PSOSYM,2,9999)
 F PSOQX=1:1:$L(PSOSYM,"~") D 
 .S PSOY=$P(PSOSYM,"~",PSOQX)
 .Q:PSOY=""
 .S PSOSYM("Name")=$$GETSYMNM(ZYALL,PSOY,PSOQX)
 .Q:PSOSYM("Name")=""
 .S PSOSYMS(PSOSTYP,PSOREACT,PSOATYPE,"ZZZSYMPTOMS",PSOSYM("Name"))=PSOY
 .S PSOSARRY(PSOSTYP,SITEARY,PSOATYPE,PSOREACT,ZMSG,"ZZZSYMPTOMS",PSOSYM("Name"))=PSOY
 Q
 ;
TMP ;
 ;PSOPAIEN - IEN TO PATIENT ALLERGY FILE 120.8
 N PSOSEVER,OLDDATA,PSOLCLAC,PSOPAIEN,TYPE,SITENM,PSOLCLAS,PSOLOCAL,PSOREACT,ZCNT2,DACNT,LOCREM,PSOATYP
 N PSOINSTL,PSOSTA,PSOHIS,PSOHISI,PSOASEV,PSOSEVI,PSOSEVT,PSOSTYP,PSOSTYPI,PSOMEDL,PSODGCL,PSOLOCI,SITE
 N PSOREACT,ZMSG
 S (TYPE,SITENM,PSOPAIEN,ZALL,PSOSYM,PSOLCLAC,PSOATYP,PSOSEVER,PSOATYPE,PSOREACT)="",(ZCNT2,DACNT)=0
 F  S PSOSEVER=$O(PSOSARRY(PSOSEVER)) Q:PSOSEVER=""  S TYPE="" F  S TYPE=$O(PSOSARRY(PSOSEVER,TYPE)) Q:TYPE=""  D 
 .S PSOATYPE=""
 .F  S PSOATYPE=$O(PSOSARRY(PSOSEVER,TYPE,PSOATYPE)) Q:PSOATYPE=""  S PSOREACT="" F  S PSOREACT=$O(PSOSARRY(PSOSEVER,TYPE,PSOATYPE,PSOREACT)) Q:PSOREACT=""  D 
 ..S ZMSG=""
 ..S ZMSG=$O(PSOSARRY(PSOSEVER,TYPE,PSOATYPE,PSOREACT,ZMSG))
 ..Q:ZMSG=""
 ..S PSOPAIEN=$$GMSGPTR^PSODGAL1(ZMSG) Q:PSOPAIEN=""  D 
 ...S (PSODRCL,ZALL,SITE,SITENM,LOCREM)=""
 ...S SITE=$P(TYPE,"|",3),LOCREM=$P(TYPE,"|"),SITENM=$P(TYPE,"|",2)
 ...S ZALL=GMRARSLT(ZMSG,PSOPAIEN)
 ...I PSOATYPE=2 S PSOATYP=2
 ...I PSOATYPE=1 S PSOATYP=1,PSODRCL=1
 ...Q:ZALL=""
 ...D TMPSET
 Q
 ;
TMPSET ;
 S DACNT=DACNT+1 N II,III,PTR,DATEI K SETTMP
 S PSOHISI=$P(ZALL,U,8)
 S PSOSEVI=$$GETSEVI(ZALL)
 S PSODGCL="",PSOLOCAL=$P(ZALL,"^",3),PSOLOCI=$P($P(ZALL,"^",3),"|",2)
 S DATEI=$P(ZALL,U,3)
 S PTR=$P(ZALL,U,7)
 ;ZERO NODE OF DRUG ALLERGY MULTIPLE
 S SETTMP(DACNT,0)=$E(PSOREACT,1,64)_U_PTR_U_LOCREM_U_SITE
 S SETTMP(DACNT,0)=SETTMP(DACNT,0)_U_DATEI_U_$$UPPER(PSOHISI)_U_PSOSEVI
 S ^TMP("PSODAOC",$J,"ALLERGY",PSJALGCT,4,DACNT,0)=SETTMP(DACNT,0)
 ;DRUG CLASS
 I PSOATYP'=1 D 
 .S PSODGCL=$G(GMRARSLT(ZMSG,PSOPAIEN,"CLS"))
 .I $E(PSODGCL,1)="~" S PSODGCL=$E(PSODGCL,2,999)
 I PSOATYP=1 D 
 .S PSODGCLS=$G(GMRARSLT(ZMSG,PSOPAIEN,"CLS"))
 .I $E(PSODGCLS,1)="~" S PSODGCLS=$E(PSODGCLS,2,999)
 .S PSODGCL=""
 .N II
 .F II=1:1 S PSODGCL=$P(PSODGCLS,"~",II) Q:PSODGCL=""  D 
 ..S ^TMP("PSODAOC",$J,"ALLERGY",PSJALGCT,4,DACNT,1,II,0)=PSODGCL
 ..Q
 ;DRUG INGREDIENT
 D:$D(GMRARSLT(ZMSG,PSOPAIEN,"ING")) 
 .S INGREDS=GMRARSLT(ZMSG,PSOPAIEN,"ING")
 .I $E(INGREDS,1)="~" S INGREDS=$E(INGREDS,2,999)
 .N II
 .F II=1:1 S INGRED=$P(INGREDS,"~",II) Q:INGRED=""  D 
 ..S ^TMP("PSODAOC",$J,"ALLERGY",PSJALGCT,4,DACNT,2,II,0)=INGRED
 ..Q
 ;SIGN/SYMPTOM
 N PSOSYMN,SYMCNT S SYMCNT=0,PSOSYMN=""
 F  S PSOSYMN=$O(PSOSARRY(PSOSEVER,TYPE,PSOATYPE,PSOREACT,ZMSG,"ZZZSYMPTOMS",PSOSYMN)) Q:PSOSYMN=""  D 
 .S SYMCNT=SYMCNT+1
 .S ^TMP("PSODAOC",$J,"ALLERGY",PSJALGCT,4,DACNT,3,SYMCNT,0)=PSOSARRY(PSOSEVER,TYPE,PSOATYPE,PSOREACT,ZMSG,"ZZZSYMPTOMS",PSOSYMN)
 ;DISPENSE DRUG
 S ^TMP("PSODAOC",$J,"ALLERGY",PSJALGCT,"ALLERGY DD",5,1,0)=$S(PSOATYP=1!(PSOATYP=2):$G(PSODRUG("IEN")),PSOATYP=3:$P(ZALL,"^",2),1:"")
 S ^TMP("PSODAOC",$J,"ALLERGY",PSJALGCT,"ALLERGY PKG")=$S($G(PSJAOC):"IP",1:"OP")
 S ^TMP("PSODAOC",$J,"ALLERGY","PROVR")=""
END ;
 Q
 ;
GETSEV(PSOSSITE,ZMSG,GMRARSLT) ; return external highest severity for possible local multiple
 N RESULT,ARRAY,I,SEV
 S RESULT=""
 Q:'$D(GMRARSLT) RESULT
 Q:'$D(PSOSSITE) RESULT
 Q:'$D(GMRARSLT(ZMSG)) RESULT
 S I=0
 F  S I=$O(GMRARSLT(ZMSG,"MESSAGE",1,PSOSSITE,1,I)) Q:'I  D 
 .S SEV=$P($G(GMRARSLT(ZMSG,"MESSAGE",1,PSOSSITE,1,I)),U,2)
 .S ARRAY($S(SEV="SEVERE":1,SEV="MODERATE":2,SEV="MILD":3,1:99))=SEV
 .Q
 I $D(ARRAY) S I=$O(ARRAY("")),RESULT=ARRAY(I)
 Q RESULT
 ;;
GETSEVI(ZALL) ; return internal highest severity for possible local multiple
 N RESULT,ARRAY,I,J,K,SEV
 S RESULT=""
 S SEV=$P($G(ZALL),U,4)
 I $E(SEV,1)="~" S SEV=$E(SEV,2,999)
 Q:SEV="" RESULT
 F I=1:1 S J=$P(SEV,"~",I) Q:J=""  D 
 .S K=$P(J,"|",2)
 .S:K'="" ARRAY(K)=J
 .Q
 I $D(ARRAY) S I=$O(ARRAY(""),-1),RESULT=I
 Q RESULT
 ;;
GETSYMNM(ZYALL,SYMIEN,SYMINC) ; getSymptomName(allergyResultIEN,symptomIEN,symptomIncrement)
 N RESULT
 S RESULT=""
 Q:$G(ZYALL)="" RESULT
 Q:$G(SYMIEN)="" RESULT
 Q:$G(SYMINC)="" RESULT
 Q:+SYMIEN'=30 $$GET1^DIQ(120.83,SYMIEN,.01)
 I +ZYALL=0 D  Q RESULT
 .;look at ^xtmp("orrdi","art" here for remote data 'OTHER REACTION' text
 .N ORRDIEN
 .S ORRDIEN=$P(ZYALL,"R",2)
 .Q:ORRDIEN=""
 .S:+$G(DFN) RESULT=$P($G(^XTMP("ORRDI","ART",DFN,ORRDIEN,"SIGNS/SYMPTOMS",SYMINC)),U,2)
 .Q
 I +ZYALL>0 D  Q RESULT
 .;call GMRADPT here, look at GMRAL for local data 'OTHER REACTION' text
 .N GMRAL
 .D ^GMRADPT
 .S RESULT=$P($G(GMRAL(ZYALL,"S",SYMINC)),";")
 .K GMRAL
 Q RESULT
 ;
UPPER(PSOUCS) ;
 Q $TR(PSOUCS,"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ")
 ;
TSTREM ;For remote developer testing
 ;1. complex mix of local/remote results: set a break at SORTN^PSODGAL3, results will not match the order but will be the complex dummy for all, at the break set S PSOTSTMD=1, type G for Go.
 ;2. Multiple Ingredient allergy:  set a break at SORTN^PSODGAL3, order PROPOFOL, and at the break set S PSOTSTMD=3, type G for Go.
 ;3. Drug class allergy:  set up allergy by drug class ANTILIPEMIC AGENTS and order LIPITOR and at the break set S PSOTSTMD=2, type G for Go.
 ;
 Q:'$G(PSOTSTMD)
 I PSOTSTMD=1 G BIGARRAY
 I PSOTSTMD=2 G DRGCLASS
 I PSOTSTMD=3 G MULTI
 ;
SINGLE ;Test Remote with a single ingredient
 ;ICR:           IEN     NAME          1       2            3                            4          5                 6                7                         8       9     10   11               
 ;PIECE:          1        2           3       4            5                            6          7                 8                9                        10      11     12   13        
 S GMRAING("R1")="GUAIFENESIN|744^MARTINSBURG VAMC|613^REMOTE|R^Jun 20, 2013@15:52|3130620.1552^SEVERE|3^DROWSINESS|66~HIVES|1~ANXIETY|39^GUAIFENESIN^GUAIFENESIN|1671;PSNDF(50.6,^HISTORICAL|h^VISTA|V"
 S GMRAING("R2")="GUAIFENESIN|744^HEARTLAND WEST VAMC|589^REMOTE|R^Aug 28, 2012@11:52|3120828.1552^SEVERE|3^DROWSINESS|66~HIVES|1^GUAIFENESIN^GUAIFENESIN|1671;PSNDF(50.6,^HISTORICAL|h^VISTA|V"
 S GMRAING("R3")="GUAIFENESIN|744^NEW ORLEANS, LA.|629^REMOTE|R^Oct 25, 2013@09:54|3131025.0954^MODERATE|2^DROWSINESS|66~HIVES|1^GUAIFENESIN^GUAIFENESIN|1671;PSNDF(50.6,^HISTORICAL|h^VISTA|V"
 S GMRAING("R4")="GUAIFENESIN|744^NEW YORK, NY|630^REMOTE|R^Apr 9, 2013@15:43|3130409.1143^MILD|1^DROWSINESS|66~HIVES|1^GUAIFENESIN^GUAIFENESIN|1671;PSNDF(50.6,^HISTORICAL|h^VISTA|V"
 Q
 ;
MULTI ;Test Remote with multiple ingredients
 S GMRAING("R7")="SODIUM METABISULFITE|69~PROPOFOL|2114~EGG|2361~SOYBEAN|3156~PEANUT|4048^HEARTLAND WEST VAMC|589^REMOTE|R^Oct 25, 2013@09:52|3131025.0952^SEVERVE|3^ANXIETY|39^PROPOFOL^PROPOFOL|2145;PSNDF(50.6,^OBSERVED|o^VISTA|V"
 S GMRAING("R8")="SODIUM METABISULFITE|69~PROPOFOL|2114~EGG|2361~SOYBEAN|3156~PEANUT|4048^MARTINSBURG VAMC|613^REMOTE|R^SEP 30, 2013@01:52|3130930.1352^SEVERVE|3^ANXIETY|39^PROPOFOL^PROPOFOL|2145;PSNDF(50.6,^OBSERVED|o^VISTA|V"
 S GMRAING("R10")="SODIUM METABISULFITE|69~PROPOFOL|2114~EGG|2361~SOYBEAN|3156~PEANUT|4048^NEW ORLEANS, LA.|629^REMOTE|R^Aug 15, 2013@11:52|3130515.1152^MODERATE|2^ANXIETY|39^PROPOFOL^PROPOFOL|2145;PSNDF(50.6,^OBSERVED|o^VISTA|V"
 S GMRAING("R11")="SODIUM METABISULFITE|69~PROPOFOL|2114~EGG|2361~SOYBEAN|3156~PEANUT|4048^NEW YORK, NY|630^REMOTE|R^Aug 15, 2013@11:52|3130515.1152^MILD|1^ANXIETY|39^PROPOFOL^PROPOFOL|2145;PSNDF(50.6,^OBSERVED|o^VISTA|V"
 S GMRAING("R12")="SODIUM METABISULFITE|69~PROPOFOL|2114~EGG|2361~SOYBEAN|3156~PEANUT|4048^MARTINSBURG VAMC|613^REMOTE|R^DEC 15, 2013@12:52|3131215.1252^MILD|1^ANXIETY|39^PROPOFOL^PROPOFOL|2145;PSNDF(50.6,OBSERVED|o^VISTA|V"
 Q
 ;
DRGCLASS ;Test Remote Interaction by drug class
 K GMRAING,GMRADRCL,GMRAREAC
 S GMRADRCL("R1")="ANTILIPEMIC AGENTS|58^HEARTLAND WEST VAMC|589^REMOTE|R^Jan 28, 2014@10:56|3140128.1056^SEVERE|3^DIARRHEA|9~DROWSINESS|66~DRY NOSE|69^ANTILIPEMIC AGENTS^ANTILIPEMIC AGENTS|58;PS(50.605,^OBSERVED|o^VISTA|V"
 S GMRADRCL("R2")="ANTILIPEMIC AGENTS|58^MARTINSBURG VAMC|613^REMOTE|R^Jan 15, 2014@09:56|3140115.0956^SEVERE|3^DIARRHEA|9~DROWSINESS|66~DRY NOSE|69^ANTILIPEMIC AGENTS^ANTILIPEMIC AGENTS|58;PS(50.605,^OBSERVED|o^VISTA|V"
 Q
 ;
BIGARRAY ;dummy big array with complex local/remote combinations.  Will not match drugs passed in
 K GMRARSLT
 S GMRARSLT=7
 S GMRARSLT(1,106534)="10881^L^3150528.1606^^^AMPICILLIN^79;PSNDF(50.6,^h^V"
 S GMRARSLT(1,106534,"CLS")=249
 S GMRARSLT(1,"MESSAGE",1)=1
 S GMRARSLT(1,"MESSAGE",1,10881)="CLE13 TEST LAB^LOCAL^MAY 28, 2015@16:06^HISTORICAL"
 S GMRARSLT(1,"MESSAGE",2)="^AMPICILLIN^AMPICILLIN"
 S GMRARSLT(1,"MESSAGE","OFFENDERS","CLS")="AM111 PENICILLINS,AMINO DERIVATIVES"
 S GMRARSLT(2,106535)="10881^L^3150528.1607^^^MEROPENEM^3391;PSNDF(50.6,^h^V"
 S GMRARSLT(2,106535,"CLS")=335
 S GMRARSLT(2,"MESSAGE",1)=2
 S GMRARSLT(2,"MESSAGE",1,10881)="CLE13 TEST LAB^LOCAL^MAY 28, 2015@16:07^HISTORICAL"
 S GMRARSLT(2,"MESSAGE",1,10882)="CHEYENNE HDR SQA^REMOTE^MAY 28, 2015@16:02^HISTORICAL"
 S GMRARSLT(2,"MESSAGE",2)="^MEROPENEM^MEROPENEM"
 S GMRARSLT(2,"MESSAGE","OFFENDERS","CLS")="AM119 BETA-LACTAMS ANTIMICROBIALS,OTHER"
 S GMRARSLT(2,"R9")="10882^R^3150528.1602^^^MEROPENEM^3391;PSNDF(50.6,^h^V"
 S GMRARSLT(2,"R9","CLS")=335
 S GMRARSLT(3,"MESSAGE",1)=1
 S GMRARSLT(3,"MESSAGE",1,10882)="CHEYENNE HDR SQA^REMOTE^MAY 28, 2015@16:03^OBSERVED"
 S GMRARSLT(3,"MESSAGE",2)="RASH^CEPHALEXIN^CEPHALEXIN"
 S GMRARSLT(3,"MESSAGE","OFFENDERS","CLS")="AM115 CEPHALOSPORIN 1ST GENERATION"
 S GMRARSLT(3,"R11")="10882^R^3150528.1603^^133^CEPHALEXIN^1290;PSNDF(50.6,^o^V"
 S GMRARSLT(3,"R11","CLS")=12
 S GMRARSLT(4,"MESSAGE",1)=1
 S GMRARSLT(4,"MESSAGE",1,613)="MARTINSBURG VAMC^REMOTE^MAY 28, 2015@15:46^OBSERVED"
 S GMRARSLT(4,"MESSAGE",2)="ANAPHYLAXIS and DYSPNEA^CEFAZOLIN^CEFAZOLIN"
 S GMRARSLT(4,"MESSAGE","OFFENDERS","CLS")="AM115 CEPHALOSPORIN 1ST GENERATION"
 S GMRARSLT(4,"MESSAGE","OFFENDERS","ING")="CEFAZOLIN"
 S GMRARSLT(4,"R13")="613^R^3150528.1546^^5~70^CEFAZOLIN^30;PSNDF(50.6,^o^V"
 S GMRARSLT(4,"R13","CLS")=12
 S GMRARSLT(4,"R13","ING")=2278
 S GMRARSLT(5,"MESSAGE",1)=1
 S GMRARSLT(5,"MESSAGE",1,10882)="CHEYENNE HDR SQA^REMOTE^MAY 28, 2015@16:01^HISTORICAL"
 S GMRARSLT(5,"MESSAGE",2)="ITCHING OF EYE and WHEEZING^PENICILLIN^PENICILLIN"
 S GMRARSLT(5,"MESSAGE","OFFENDERS","CLS")="AM110 PENICILLIN-G RELATED PENICILLINS"
 S GMRARSLT(5,"R2")="10882^R^3150528.1601^^20~311^PENICILLIN^16;PSNDF(50.6,^h^V"
 S GMRARSLT(5,"R2","CLS")=248
 S GMRARSLT(6,"MESSAGE",1)=1
 S GMRARSLT(6,"MESSAGE",1,613)="MARTINSBURG VAMC^REMOTE^MAY 28, 2015@15:50^OBSERVED"
 S GMRARSLT(6,"MESSAGE",2)="ANAPHYLAXIS^PENICILLIN^PENICILLIN"
 S GMRARSLT(6,"MESSAGE","OFFENDERS","CLS")="AM110 PENICILLIN-G RELATED PENICILLINS"
 S GMRARSLT(6,"R4")="613^R^3150528.155^^5~30^PENICILLIN^16;PSNDF(50.6,^o^V"
 S GMRARSLT(6,"R4","CLS")=248
 S ^XTMP("ORRDI","ART",DFN,4,"SIGNS/SYMPTOMS",2)="^Giggles^"
 S GMRARSLT(7,"MESSAGE",1)=1
 S GMRARSLT(7,"MESSAGE",1,613)="MARTINSBURG VAMC^REMOTE^MAY 28, 2015@15:50^HISTORICAL"
 S GMRARSLT(7,"MESSAGE",2)="^CEFEPIME^CEFEPIME"
 S GMRARSLT(7,"MESSAGE","OFFENDERS","CLS")="AM118 CEPHALOSPORIN 4TH GENERATION"
 S GMRARSLT(7,"R5")="613^R^3150528.155^^^CEFEPIME^3392;PSNDF(50.6,^h^V"
 S GMRARSLT(7,"R5","CLS")=524
 Q
 ;
