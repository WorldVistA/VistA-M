PSOEPU1 ;ALB/BI - DEA Manual Entry ;11/3/21  14:56
 ;;7.0;OUTPATIENT PHARMACY;**545**;DEC 1997;Build 270
 ;External reference to DEA NUMBERS file (#8991.9) is supported by DBIA 7002
 ;External reference to XUEPCS DATA file (#8991.6) is supported by DBIA 7015
 ;External reference to XUEPCS PSDRPH AUDIT file (#8991.7) is supported by DBIA 7016
 ;External reference to KEYS sub-file (#200.051) is supported by DBIA 7054
 ;References to Cache methods class.HttpResponse, class.Data, class.AtEnd, class.ReadLine() are supported by SAC exemption 20210601-01
 ;
 Q
 ;
WSGET(FG,DEA) ; Function to Get the Remote DEA information, Return in FG.
 ; INPUT:   DEA      ;Properly formatted DEA Number for lookup.
 ;
 ; OUTPUT:  FG       ;Web Service Response Global
 ;
 ; RETURN:  Status code with a text message.
 ;          If not filled successfully a "0^Error Message" will be returned.
 ;
 Q $$WSGET^PSODEAU0(.FG,DEA)
 ;
FILEFM(RET,DATA,NPIEN)  ; -- File DEA Information in the DEA NUMBERS FILE #8991.9
 ; Invoked by RPC: PSO EPCS ADD DEA
 N DNDEAIEN,DNDEATXT,FDA,IENROOT,IENS,MSGROOT,SUFFIX,XSTATE,XIP,WSDOWN
 S RET=0
 I '$D(DATA) S RET=0 G FILEFMX
 ;
 S DNDEATXT=$P(DATA,U,11) I DNDEATXT="" G FILEFMX
 S DNDEAIEN=$O(^XTV(8991.9,"B",DNDEATXT,0))
 S IENS=$S($G(DNDEAIEN):$G(DNDEAIEN)_",",1:"+1,")
 ;
 ; INPUT:  DATA - A STRING OF DEA INFORMATION DELIMITED BY THE "^"
 S FDA(1,8991.9,IENS,1.1)=$P(DATA,U,1)         ;  1 - PROVIDER NAME
 S FDA(1,8991.9,IENS,1.2)=$P(DATA,U,2)         ;  2 - ADDRESS 1
 S FDA(1,8991.9,IENS,1.3)=$P(DATA,U,3)         ;  3 - ADDRESS 2
 S FDA(1,8991.9,IENS,1.4)=$P(DATA,U,4)         ;  4 - ADDRESS 3
 S FDA(1,8991.9,IENS,1.5)=$P(DATA,U,5)         ;  5 - CITY
 ;
 ; Special State Processing
 D POSTAL^XIPUTIL($P(DATA,U,8),.XIP)
 S XSTATE=$G(XIP("STATE"))
 I XSTATE'="" S FDA(1,8991.9,IENS,1.6)=XSTATE  ;  6 - STATE
 ;
 S FDA(1,8991.9,IENS,1.7)=$P(DATA,U,8)         ;  8 - ZIP CODE
 S FDA(1,8991.9,IENS,.02)=$P(DATA,U,9)         ;  9 - ACTIVITY CODE
 I $P(DATA,U,9)]"" D
 . N BAC,FDB,ERR,SCREEN,RETURN,ERROR
 . S BAC=$P(DATA,U,9)
 . ;
 . ; Just in case there are multiple BAC's with the same name, D LIST^DIC instead of $$FIND1^DIC
 . ; Quit if this ACTIVITY CODE is already on file
 . S SCREEN="I $P($G(^(0)),U)="""_BAC_""""
 . D LIST^DIC(8991.8,,.01,,,,,,.SCREEN,,"RETURN","ERROR")
 . Q:+$G(RETURN("DILIST",0))>0
 . ;
 . S FDB(8991.8,"?+1,",.01)=BAC
 . S FDB(8991.8,"?+1,",.02)=$E(BAC)
 . S FDB(8991.8,"?+1,",.03)=$E(BAC,2,3)
 . D UPDATE^DIE("E","FDB",,"ERR")
 . Q
 S FDA(1,8991.9,IENS,.07)=$P(DATA,U,10)        ; 10 - TYPE
 S FDA(1,8991.9,IENS,.01)=$P(DATA,U,11)        ; 11 - DEA NUMBER
 S FDA(1,8991.9,IENS,.04)=$P(DATA,U,12)        ; 12 - EXPIRATION DATE
 S FDA(1,8991.9,IENS,10.2)="N"                 ; 13 - PROCESSED DATE
 I ($$DEANUM^PSOEPUT($P(DATA,U,14)))!($P(DATA,U,14)="") D                   ; ONLY CLEAR AND SET IF VALIDATED
 . I $P(DATA,U,14)'="" D CLEARDTX^PSOEPUT(NPIEN) ; REMOVE DETOX NUMBERS FROM OTHER DEA NUMBERS
 . S FDA(1,8991.9,IENS,.03)=$P(DATA,U,14)      ; 14 - DETOX NUMBER
 I $P(DATA,U,21)["Y" D CLEARINP^PSOEPU1(NPIEN) ; REMOVE INPATIENT FLAG FROM OTHER DEA NUMBERS
 I $P(DATA,U,10)="INDIVIDUAL" D
 . S FDA(1,8991.9,IENS,2.1)=$P(DATA,U,15)        ; 15 - SCHEDULE II NARCOTIC
 . S FDA(1,8991.9,IENS,2.2)=$P(DATA,U,16)        ; 16 - SCHEDULE II NON-NARCOTIC
 . S FDA(1,8991.9,IENS,2.3)=$P(DATA,U,17)        ; 17 - SCHEDULE III NARCOTIC
 . S FDA(1,8991.9,IENS,2.4)=$P(DATA,U,18)        ; 18 - SCHEDULE III NON-NARCOTIC
 . S FDA(1,8991.9,IENS,2.5)=$P(DATA,U,19)        ; 19 - SCHEDULE IV
 . S FDA(1,8991.9,IENS,2.6)=$P(DATA,U,20)        ; 20 - SCHEDULE V
 I $P(DATA,U,10)'="INDIVIDUAL" D
 . N SRET,SDEA
 . S SDEA=$P(DATA,U,11) ;dea number
 . D DEADOJ^PSOEPUT(.SRET,SDEA) ;call doj server for doj institutional schedules
 . I SRET(0) D  ;doj server is up
 . . S FDA(1,8991.9,IENS,2.1)=$P(SRET(1),"^",15) ; 15 - SCHEDULE II NARCOTIC
 . . S FDA(1,8991.9,IENS,2.2)=$P(SRET(1),"^",16) ; 16 - SCHEDULE II NON-NARCOTIC
 . . S FDA(1,8991.9,IENS,2.3)=$P(SRET(1),"^",17) ; 17 - SCHEDULE III NARCOTIC
 . . S FDA(1,8991.9,IENS,2.4)=$P(SRET(1),"^",18) ; 18 - SCHEDULE III NON-NARCOTIC
 . . S FDA(1,8991.9,IENS,2.5)=$P(SRET(1),"^",19) ; 19 - SCHEDULE IV
 . . S FDA(1,8991.9,IENS,2.6)=$P(SRET(1),"^",20) ; 20 - SCHEDULE V
 . ;
 S FDA(1,8991.9,IENS,.06)=$P(DATA,U,21)        ; 21 - USE FOR INPATIENT FLAG
 S SUFFIX=$P(DATA,U,22)                        ; 22 - DEA INSTITUTIONAL SUFFIX
 ;
 ; Last Date/Time updated by DEA/DOJ web service. Adds/removes the DEA number to/from the PSO EPCS MANUAL DEA REPORT
 S WSDOWN='$$CONNECT^PSODEADD
 S FDA(1,8991.9,IENS,10.3)=$S(WSDOWN:"",1:$$NOW^XLFDT)
 ;
 D UPDATE^DIE("E","FDA(1)","IENROOT","MSGROOT")
 I $D(MSGROOT) D  G FILEFMX
 . S RET="0^DATA DIDN'T FILE SUCCESSFULLY."
 . S RET(1)=$G(MSGROOT("DIERR",1,"TEXT",1))
 S DNDEAIEN=$S($D(IENROOT(1)):IENROOT(1)_",",1:IENS)
 I '+DNDEAIEN S RET="0^DATA DIDN'T FILE SUCCESSFULLY." G FILEFMX
 S FDA(2,8991.9,DNDEAIEN,10.1)=$G(DUZ) D FILE^DIE("","FDA(2)","MSGROOT")
 S:DNDEAIEN RET=+DNDEAIEN_"^SUCCESSFULLY SAVED/UPDATED IN 8991.9"
 I $L(DNDEATXT),$G(NPIEN),$G(DNDEAIEN) S RET=RET_"^"_$$NPFILE^PSOEPUT(DNDEATXT,NPIEN,DNDEAIEN,SUFFIX)
 ;I RET,$P(DATA,U,21)="YES" S FDA(200,NPIEN_",",53.2)=$P(DATA,U,11) D UPDATE^DIE(,"FDA")
 I $P(RET,"^",3),$G(NPIEN),$P($G(DATA),"^",10)'="INDIVIDUAL" S RET=RET_"^"_$$NPSFILE^PSOEPUT(NPIEN,DATA)
FILEFMX  ; -- Subroutine Exit Point
 Q
 ;
DNDEAGET(RET,DEA) ;
 I '$D(^XTV(8991.9,"B",DEA)) S RET(0)="0^7^DEA number not on file" Q
 I $D(^XTV(8991.9,"B",DEA)) S DNDEAIEN=$O(^XTV(8991.9,"B",DEA,0)) I +DNDEAIEN D
 . K DNDEADAT D GETS^DIQ(8991.9,DNDEAIEN,"**","","DNDEADAT")
 . K RET(1)
 . S RET(1)=""
 . S RET(1)=RET(1)_$G(DNDEADAT(8991.9,DNDEAIEN_",",1.1))_"^"    ; PROVIDER NAME
 . S RET(1)=RET(1)_$G(DNDEADAT(8991.9,DNDEAIEN_",",1.2))_"^"    ; ADDRESS 1
 . S RET(1)=RET(1)_$G(DNDEADAT(8991.9,DNDEAIEN_",",1.3))_"^"    ; ADDRESS 2
 . S RET(1)=RET(1)_$G(DNDEADAT(8991.9,DNDEAIEN_",",1.4))_"^"    ; ADDRESS 3
 . S RET(1)=RET(1)_$G(DNDEADAT(8991.9,DNDEAIEN_",",1.5))_"^"    ; CITY
 . ;
 . ; Special State Processing
 . N XSTATE,XSTATEAB,XIP,BAC,X,Y D POSTAL^XIPUTIL($G(DNDEADAT(8991.9,DNDEAIEN_",",1.7)),.XIP)
 . S XSTATEAB="" I $G(XIP("ERROR"))="" S XSTATEAB=$$GET1^DIQ(5,XIP("STATE POINTER"),1)
 . S RET(1)=RET(1)_XSTATEAB_"^"                         ; STATE ABREVIATION
 . S XSTATE=$G(XIP("STATE"))
 . S RET(1)=RET(1)_$G(XSTATE)_"^"                       ; STATE
 . ;
 . S RET(1)=RET(1)_$G(DNDEADAT(8991.9,DNDEAIEN_",",1.7))_"^"  ; ZIP CODE
 . S BAC=$G(DNDEADAT(8991.9,DNDEAIEN_",",.02))                ; ACTIVITY CODE
 . S RET(1)=RET(1)_BAC_"^"                             ; ACTIVITY CODE
 . S RET(1)=RET(1)_$P($$PROVTYPE^PSODEAUT($G(BAC)),"^",2)_"^"   ; TYPE
 . S RET(1)=RET(1)_$G(DNDEADAT(8991.9,DNDEAIEN_",",.01))_"^"  ; DEA NUMBER
 . S X=$P($G(DNDEADAT(8991.9,DNDEAIEN_",",.04)),"@") D ^%DT
 . S RET(1)=RET(1)_$$FMTHL7^XLFDT(Y)_"^"  ; EXPIRATION DATE
 . S X=$P($G(DNDEADAT(8991.9,DNDEAIEN_",",10.2)),"@") D ^%DT
 . S RET(1)=RET(1)_$$FMTHL7^XLFDT(Y)_"^" ; PROCESSED DATE
 . S RET(1)=RET(1)_$G(DNDEADAT(8991.9,DNDEAIEN_",",.03))_"^"  ; DETOX NUMBER
 . I $G(DNDEADAT(8991.9,DNDEAIEN_",",.07))="INDIVIDUAL" D
 . . S RET(1)=RET(1)_$G(DNDEADAT(8991.9,DNDEAIEN_",",2.1))_"^"  ; SCHEDULE II NARCOTIC
 . . S RET(1)=RET(1)_$G(DNDEADAT(8991.9,DNDEAIEN_",",2.2))_"^"  ; SCHEDULE II NON-NARCOTIC
 . . S RET(1)=RET(1)_$G(DNDEADAT(8991.9,DNDEAIEN_",",2.3))_"^"  ; SCHEDULE III NARCOTIC
 . . S RET(1)=RET(1)_$G(DNDEADAT(8991.9,DNDEAIEN_",",2.4))_"^"  ; SCHEDULE III NON-NARCOTIC
 . . S RET(1)=RET(1)_$G(DNDEADAT(8991.9,DNDEAIEN_",",2.5))_"^"  ; SCHEDULE IV
 . . S RET(1)=RET(1)_$G(DNDEADAT(8991.9,DNDEAIEN_",",2.6))      ; SCHEDULE V
 . I $G(DNDEADAT(8991.9,DNDEAIEN_",",.07))'="INDIVIDUAL" D
 . . S RET(1)=RET(1)_"^"  ; SCHEDULE II NARCOTIC
 . . S RET(1)=RET(1)_"^"  ; SCHEDULE II NON-NARCOTIC
 . . S RET(1)=RET(1)_"^"  ; SCHEDULE III NARCOTIC
 . . S RET(1)=RET(1)_"^"  ; SCHEDULE III NON-NARCOTIC
 . . S RET(1)=RET(1)_"^"  ; SCHEDULE IV
 . . S RET(1)=RET(1)_"^"  ; SCHEDULE V
 ;
CLEARINP(NPIEN)  ; REMOVE INPATIENT FLAG FROM ALL OF A PROVIDERS DEA NUMBERS
 N DNDEAIEN,FDA,NPDEAIEN
 S NPDEAIEN=0 F  S NPDEAIEN=$O(^VA(200,NPIEN,"PS4",NPDEAIEN)) Q:'NPDEAIEN  D
 . S DNDEAIEN=$$GET1^DIQ(200.5321,NPDEAIEN_","_NPIEN_",",.03,"I")
 . K FDA S FDA(1,8991.9,DNDEAIEN_",",.06)="@" D UPDATE^DIE("","FDA(1)") K FDA
 Q
 ;
DTXCHK(RET,DEA,DETOX) ; -- Check Detox Number
 S RET=0
 I $L(DETOX)>9!($L(DETOX)<9) S RET="0^1^DETOX length error" Q
 I $S("ABCDEFGHIJKLMNOPQRSTUVWXYZ"[$E(DETOX):1,1:0)=0 S RET="0^2^DETOX first character error" Q
 I $S("ABCDEFGHIJKLMNOPQRSTUVWXYZ"[$E(DETOX,2):2,1:0)=0 S RET="0^3^DETOX second character error" Q
 I $$DEANUM^PSOEPUT(DETOX)=0 S RET="0^4^DETOX number error" Q
 I $$DETOXDUP(DEA,DETOX,.DUPDEA)=1 S RET="0^5^Duplicate DETOX number with DEA number "_DUPDEA Q
 S DNDEA=$$PRVRDTX(DEA) I DNDEA]"" S RET="0^6^Provider's profile already has a DETOX number for DEA number "_DNDEA Q
 S RET=1
 Q
 ;
DETOXDUP(DEA,DETOX,DUPDEA)  ; -- Check for duplicate Detox number
 N I,NXTDEA,NPIEN,NPNAME S NXTDEA=0,DUPDEA=""
 I $G(DETOX)=""!($G(DEA)="") Q 0                   ; Missing required input, can't check
 I '$D(^XTV(8991.9,"D",$G(DETOX))) Q 0             ; If Detox not on file, not a duplicate
 I $D(^XTV(8991.9,"D",$G(DETOX))),'$D(^XTV(8991.9,"D",$G(DETOX),$G(DEA))) D  Q 1  ; On file for another prescriber, duplicate
 .S DUPDEA=$O(^XTV(8991.9,"D",$G(DETOX),$G(DUPDEA)))
 .S NPIEN=0 S NPIEN=$O(^VA(200,"PS4",DUPDEA,NPIEN)) Q:NPIEN=""
 .S NPNAME=$$GET1^DIQ(200,NPIEN_",",.01)
 .S DUPDEA=DUPDEA_"^"_NPNAME
 F  S NXTDEA=$O(^XTV(8991.9,"D",DETOX,NXTDEA)) Q:NXTDEA=""  I NXTDEA'=DEA S DUPDEA=$S($G(DUPDEA)'="":DUPDEA_","_NXTDEA,1:NXTDEA)
 I $G(DUPDEA)'="" Q 1                              ; If more than one entry on file for this Detox number, duplicate
 Q 0
 ;
PRVRDTX(DEA)  ; -- Check for DETOX numbers on provider profile
 N NPIEN,DNDEA
 S DNDEA=""
 S NPIEN=$O(^VA(200,"PS4",DEA,0)) I NPIEN']"" Q DNDEA
 S DNDEA=$$GTDNDTX(NPIEN)
 Q DNDEA
 ;
GTDNDTX(NPIEN)  ; GET A SINGLE DETOX NUMBER FROM ALL OF A PROVIDERS DEA NUMBERS IN 8991.9
 N GETDNDTX,DNDEAIEN,NPDEAIEN,DNDEA S GETDNDTX=""
 S NPDEAIEN=0 F  S NPDEAIEN=$O(^VA(200,NPIEN,"PS4",NPDEAIEN)) Q:'NPDEAIEN  D  Q:DNDEA]""
 . S DNDEA=""
 . S DNDEAIEN=$$GET1^DIQ(200.5321,NPDEAIEN_","_NPIEN_",",.03,"I") Q:'DNDEAIEN
 . S GETDNDTX=$$GET1^DIQ(8991.9,DNDEAIEN_",",.03)
 . I GETDNDTX]"" S DNDEA=$$GET1^DIQ(8991.9,DNDEAIEN_",",.01)
 Q DNDEA
 ;
VANUMCHK(RET,VANUM,NPIEN) ;Check that the VA# is unique
 N IEN,PSNM
 S RET="1^Success"
 I $D(^VA(200,"PS2",VANUM)) D
 . S IEN=0
 . F  S IEN=$O(^VA(200,"PS2",VANUM,IEN)) Q:'IEN  I IEN'=NPIEN K VANUM Q
 . Q
 I '$D(VANUM) S PSNM=$$GET1^DIQ(200,IEN,.01) S RET="0^VA# in use by "_PSNM
 Q
 ;
FILEFMA(RET,FIELD,DATA,NPIEN)  ; -- Filer for file #200
 ; Invoked by RPC: PSO EPCS FILER
 N FDA,IENS,MSGROOT
 S RET="1^Success"
 I '$D(FIELD)!'$D(DATA)!'$D(NPIEN) S RET=0 G FILEFMAX
 ;
 ; INPUT:  DATA - A SINGLE DATA ITEM
 S IENS=NPIEN_","
 S FDA(200,IENS,FIELD)=$P(DATA,U,1)
 ;
 D UPDATE^DIE("E","FDA",,"MSGROOT")
 I $D(MSGROOT) S RET="0^Data did not file successfully."
FILEFMAX  ; -- Subroutine Exit Point
 Q
 ;
SETINP(NPIEN) ;SET THE INPATIENT FLAG IF ONLY ONE INDIVIDUAL DEA NUMBER
 N DNDEAIEN,FDA,NPDEAIEN,CNT,DNDEA
 S CNT=0,DNDEA=""
 S NPDEAIEN=0 F  S NPDEAIEN=$O(^VA(200,NPIEN,"PS4",NPDEAIEN)) Q:'NPDEAIEN  D
 . S DNDEAIEN=$$GET1^DIQ(200.5321,NPDEAIEN_","_NPIEN_",",.03,"I")
 . Q:($$GET1^DIQ(8991.9,DNDEAIEN_",",.07,"I")'=2)  ;quit not individual
 . S CNT=CNT+1
 . I $$GET1^DIQ(8991.9,DNDEAIEN_",",.06)'="YES" S DNDEA=DNDEAIEN ;candidate dea
 I (CNT=1)&(DNDEA]"") D
 . K FDA S FDA(1,8991.9,DNDEA_",",.06)=1 D UPDATE^DIE("","FDA(1)") K FDA
 Q
 ;
SETINP2(NPIEN,NPDEAIEN) ;Set the inpatient flag if other dea has no inpat flag
 ; NPIEN - Provider ien in file #200 being filed
 ; NPDEAIEN - New dea multiple ien with no inp flag
 ;
 N NPDEAIET,FDA,CNT,DNDEA,QFLG
 S CNT=0,DNDEA="",QFLG=0
 S NPDEAIET=0 F  S NPDEAIET=$O(^VA(200,NPIEN,"PS4",NPDEAIET)) Q:'NPDEAIET  D  Q:QFLG
 . S DNDEAIEN=$$GET1^DIQ(200.5321,NPDEAIET_","_NPIEN_",",.03,"I")
 . Q:($$GET1^DIQ(8991.9,DNDEAIEN_",",.07,"I")'=2)  ;quit not individual
 . I $$GET1^DIQ(8991.9,DNDEAIEN_",",.06)="YES" S QFLG=1 ;a dea is set for inpat
 . S CNT=CNT+1
 . I $$GET1^DIQ(8991.9,DNDEAIEN_",",.06)'="YES" I NPDEAIET'=NPDEAIEN S DNDEA=DNDEAIEN ;candidate dea
 I ('QFLG)&(CNT=2)&(DNDEA]"") D
 . K FDA S FDA(1,8991.9,DNDEA_",",.06)=1 D UPDATE^DIE("","FDA(1)") K FDA
 Q
 ;
NPSCHDL(RET,NPIEN)  ; -- RPC to return the file #200 schedule information for a single provider.
 ; INPUT:  NPIEN - NEW PERSON FILE #200 INTERNAL ENTRY NUMBER
 ;
 ; OUTPUT: RET - NULL OR A STRING OF SCHEDULE INFORMATION DELIMITED BY THE "^"
 ;           1 - SCHEDULE II NARCOTIC
 ;           2 - SCHEDULE II NON-NARCOTIC
 ;           3 - SCHEDULE III NARCOTIC
 ;           4 - SCHEDULE III NON-NARCOTIC
 ;           5 - SCHEDULE IV
 ;           6 - SCHEDULE V
 ;
 N NPSCHED
 K RET S RET=""
 Q:'$G(NPIEN)
 Q:'$D(^VA(200,NPIEN))
 ;
 K NPSCHED D GETS^DIQ(200,NPIEN_",","55.1:55.6","E","NPSCHED")
 S RET=RET_NPSCHED(200,NPIEN_",",55.1,"E")_"^"  ; SCHEDULE II NARCOTIC
 S RET=RET_NPSCHED(200,NPIEN_",",55.2,"E")_"^"  ; SCHEDULE II NON-NARCOTIC
 S RET=RET_NPSCHED(200,NPIEN_",",55.3,"E")_"^"  ; SCHEDULE III NARCOTIC
 S RET=RET_NPSCHED(200,NPIEN_",",55.4,"E")_"^"  ; SCHEDULE III NON-NARCOTIC
 S RET=RET_NPSCHED(200,NPIEN_",",55.5,"E")_"^"  ; SCHEDULE IV
 S RET=RET_NPSCHED(200,NPIEN_",",55.6,"E")  ; SCHEDULE V
 Q
 ;
NPSCHDF(RET,NPIEN,DATA) ; -- RPC to file the file #200 schedule information for a single provider.
 ; INPUT:  NPIEN - NEW PERSON FILE #200 INTERNAL ENTRY NUMBER
 ;          
 ;         DATA - A STRING OF DEA INFORMATION DELIMITED BY THE "^"
 ;           1 - SCHEDULE II NARCOTIC
 ;           2 - SCHEDULE II NON-NARCOTIC
 ;           3 - SCHEDULE III NARCOTIC
 ;           4 - SCHEDULE III NON-NARCOTIC
 ;           5 - SCHEDULE IV
 ;           6 - SCHEDULE V
 ;
 ; OUTPUT: RET - 0 OR 1^SUCCESS OR FAILURE TEXT
 ;
 N FDA,IENROOT,MSGROOT
 K RET
 I '$G(NPIEN) S RET="0^DATA DIDN'T FILE SUCCESSFULLY IN NPSFILE." Q
 I '$D(^VA(200,NPIEN)) S RET="0^DATA DIDN'T FILE SUCCESSFULLY IN NPSFILE." Q
 S FDA(3,200,NPIEN_",",55.1)=$E($P(DATA,U,1),1)     ; 1 - SCHEDULE II NARCOTIC
 S FDA(3,200,NPIEN_",",55.2)=$E($P(DATA,U,2),1)     ; 2 - SCHEDULE II NON-NARCOTIC
 S FDA(3,200,NPIEN_",",55.3)=$E($P(DATA,U,3),1)     ; 3 - SCHEDULE III NARCOTIC
 S FDA(3,200,NPIEN_",",55.4)=$E($P(DATA,U,4),1)     ; 4 - SCHEDULE III NON-NARCOTIC
 S FDA(3,200,NPIEN_",",55.5)=$E($P(DATA,U,5),1)     ; 5 - SCHEDULE IV
 S FDA(3,200,NPIEN_",",55.6)=$E($P(DATA,U,6),1)     ; 6 - SCHEDULE V
 D UPDATE^DIE("E","FDA(3)","IENROOT","MSGROOT")
 I $D(MSGROOT) S RET="0^DATA DIDN'T FILE SUCCESSFULLY IN NPSFILE." Q
 S RET="1^SCHEDULES SUCCESSFULLY SAVED/UPDATED IN 200"
 Q
 ;
OPTNDESC(RET,OPTNM) ; -- RPC to return Option file #19 description word processing text
 ; INPUT:  OPTNM - NAME FIELD OF THE OPTION
 ;          
 ; OUTPUT: RET ARRAY - OPTION FILE #19 DESCRIPTION WORD PROCESSING TEXT
 ;
 N DA,ARR
 K RET
 S DA=$$FIND1^DIC(19,,,OPTNM)
 S ARR=$$GET1^DIQ(19,DA,3.5,"Z","ARR")
 S DA=0 F  S DA=$O(ARR(DA)) Q:'DA  S RET(DA)=ARR(DA,0)
 Q
