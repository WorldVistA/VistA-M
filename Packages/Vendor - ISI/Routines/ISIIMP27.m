ISIIMP27 ;ISI GROUP/MLS -- ISI DATA LOADER, (ENCOUNTERS related...)
 ;;2.0;;;May 15,2014;Build 93
 ;
 Q
 ;
VEXAM(ISIMISC)
 ; Populates V EXAM file #9000010.13
 ; INPUT DFN,VISIT_IEN,EXAM
 ;
 N COMMENT,ERR,EXAMIEN,EXIT,DFN,FDA,PACKAGE,VISIT,SOURCE,DATETIME
 S EXIT=-1
 S ISIRC=$$VALEXAM^ISIIMPUG() ;Validate Array
 Q:+ISIRC<0 ISIRC
 ;
 S DFN=+$G(ISIMISC("DFN")) I '$D(^DPT(DFN)) QUIT EXIT_"^ ~ Missing Patient ID, DFN"
 S VISIT=+$G(ISIMISC("VISIT_IEN")) I '$D(^AUPNVSIT(VISIT)) QUIT EXIT_"^ ~ Missing or invalid Vist IEN"
 S EXAMIEN=$G(ISIMISC("EXAM")) ;EXAM
 I 'EXAMIEN Q EXIT_"^ ~ Missing or invalid Exam"
 S DATETIME=$G(ISIMISC("DATETIME"))
 S PROVIDER=$G(ISIMISC("PROVIDER"))
 ;
 S COMMENT=$G(ISIMISC("COMMENT")) ;FREE TEXT COMMENTS
 S PACKAGE=35 ;ORDER ENTRY/RESULTS REPORTING
 S SOURCE=36 ;TEXT INTEGRATION UTILITIES
 ;
 S FDA(9000010.13,"+1,",.01)=EXAMIEN
 S FDA(9000010.13,"+1,",.02)=DFN
 S FDA(9000010.13,"+1,",.03)=VISIT
 S FDA(9000010.13,"+1,",1201)=DATETIME
 S FDA(9000010.13,"+1,",1202)=PROVIDER
 S FDA(9000010.13,"+1,",1204)=PROVIDER
 S FDA(9000010.13,"+1,",81101)=COMMENT
 S FDA(9000010.13,"+1,",81201)=1 ;Electronically Signed
 S FDA(9000010.13,"+1,",81202)=PACKAGE
 S FDA(9000010.13,"+1,",81203)=SOURCE
 ;
 D UPDATE^DIE("","FDA","","ERR")
 I $D(ERR) QUIT "-1^"_$G(ERR("DIERR",1,"TEXT",1))
 ;
 S EXIT=0
 QUIT EXIT
 ;
VIMMZ(ISIMISC)
 ; Populate V Immunization file
 ; Required:  DFN, VISIT_IEN, IZ
 ;
 N COMMENT,CONTRA,ERR,EXIT,DFN,FDA,IZIEN,PACKAGE,VISIT,SOURCE,DATETIME
 S EXIT=-1
 ;
 S ISIRC=$$VALIMZ^ISIIMPUG() ;Validate Array
 Q:+ISIRC<0 ISIRC
 ;
 S DFN=+$G(ISIMISC("DFN")) I '$D(^DPT(DFN)) QUIT EXIT_"^ ~ Missing Patient ID, DFN"
 S VISIT=+$G(ISIMISC("VISIT_IEN")) I '$D(^AUPNVSIT(VISIT)) QUIT EXIT_"^ ~ Missing or invalid Vist IEN"
 S IZIEN=$G(ISIMISC("IZ")) I 'IZIEN Q EXIT_" ~ Missing or invalid Immunization"
 S DATETIME=$G(ISIMISC("DATETIME"))
 S PROVIDER=+$G(ISIMISC("PROVIDER")) ;I 'PROVIDER QUIT EXIT_"^ ~ Missing PROVIDER"
 ;
 S CONTRA=$G(ISIMISC("CONTRAINDICATED")) I CONTRA="" S CONTRA=0
 S COMMENT=$G(ISIMISC("COMMENT")) ;FREE TEXT COMMENTS
 S PACKAGE=35 ;ORDER ENTRY/RESULTS REPORTING
 S SOURCE=36 ;TEXT INTEGRATION UTILITIES
 ;
 S FDA(9000010.11,"+1,",.01)=IZIEN
 S FDA(9000010.11,"+1,",.02)=DFN
 S FDA(9000010.11,"+1,",.03)=VISIT
 S FDA(9000010.11,"+1,",.07)=CONTRA
 S FDA(9000010.11,"+1,",1201)=DATETIME
 S FDA(9000010.11,"+1,",1202)=PROVIDER
 S FDA(9000010.11,"+1,",1204)=PROVIDER
 S FDA(9000010.11,"+1,",81101)=COMMENT
 S FDA(9000010.11,"+1,",81202)=PACKAGE
 S FDA(9000010.11,"+1,",81203)=SOURCE
 ;
 D UPDATE^DIE("","FDA","","ERR")
 I $D(ERR) QUIT "-1^"_$G(ERR("DIERR",1,"TEXT",1))
 ;
 S EXIT=0
 QUIT EXIT
 ;
VCPT(ISIMISC)
 ;
 N COMMENT,CPT,DFN,EXIT,FDA,MODIFIER,MODIEN,PACKAGE,SOURCE,VISIT
 N PROVIDER,PROVNARR,QUANTITY,ERR
 S EXIT=-1
 ;
 S ISIRC=$$VALCPT^ISIIMPUG() ;Validate Array
 I +ISIRC<0 Q ISIRC
 ;
 S DFN=+$G(ISIMISC("DFN")) I '$D(^DPT(DFN)) QUIT EXIT_" ~ Missing Patient ID, DFN"
 S VISIT=+$G(ISIMISC("VISIT_IEN")) I '$D(^AUPNVSIT(VISIT)) QUIT EXIT_" ~ Missing or invalid Vist IEN"
 S CPT=$G(ISIMISC("CPT")) I 'CPT QUIT EXIT_" ~ Missing CPT code"
 S PROVIDER=+$G(ISIMISC("PROVIDER")) I 'PROVIDER QUIT EXIT_"^ ~ Missing PROVIDER"
 S PROVNARR=$G(ISIMISC("PROVIDER_NARRATIVE")) I 'PROVNARR QUIT EXIT_" ~ Missing Provider Narrative"
 ;
 S QUANTITY=1
 S COMMENT=$G(ISIMISC("COMMENT")) ;FREE TEXT COMMENTS
 S PACKAGE=35 ;ORDER ENTRY/RESULTS REPORTING
 S SOURCE=36 ;TEXT INTEGRATION UTILITIES
 S MODIFIER=$G(ISIMISC("MODIFIER"))
 ;
 S FDA(9000010.18,"+1,",.01)=CPT
 S FDA(9000010.18,"+1,",.02)=DFN
 S FDA(9000010.18,"+1,",.03)=VISIT
 S FDA(9000010.18,"+1,",.04)=PROVNARR
 S FDA(9000010.18,"+1,",.16)=QUANTITY
 S FDA(9000010.18,"+1,",1204)=PROVIDER
 S FDA(9000010.18,"+1,",81101)=COMMENT
 S FDA(9000010.18,"+1,",81202)=PACKAGE
 S FDA(9000010.18,"+1,",81203)=SOURCE
 I MODIFIER'="" D
 . S FDA(9000010.181,"+2,","+1,",.01)=MODIFIER
 ;
 I $G(ISIPARAM("DEBUG"))>0 D  
 . W !,"+++FDA  values+++",!
 . I $D(FDA) S X="" F  S X=$O(FDA(9000010.18,"+1,",X)) Q:X=""  W !,"FDA(9000010.18,+1,",X,")=",FDA(9000010.18,"+1,",X)
 . W !,"<HIT RETURN TO PROCEED>" R X:5
 . Q
 ;
 D UPDATE^DIE("","FDA","","ERR")
 I $D(ERR) QUIT "-1^"_$G(ERR("DIERR",1,"TEXT",1))
 ;
 S EXIT=0
 QUIT EXIT
 ;
VHF(ISIMISC)  ;ADD HEALTH FACTOR (#9000010.23)
 ; Creates entry in #9000010.23
 ; Required Input:  DFN, HFACTOR, VISIT_IEN, PROVIDER
 ;
 N HFACTOR,DFN,VISIT,COMMENT,PACKAGE,SOURCE,COMMENT,SEVERITY
 N PROVIDER,EXIT,PROVNM,DATETIME,ERR
 S ISIRC=$$VALHF^ISIIMPUG() ;Validate Array
 I +ISIRC<0 Q ISIRC
 ;
 S EXIT=-1
 S HFACTOR=+$G(ISIMISC("HFACTOR")) I 'HFACTOR Q EXIT_"^ Missing Health Factor"
 S DFN=+$G(ISIMISC("DFN")) I '$D(^DPT(DFN)) QUIT EXIT_"^ ~ Missing Patient ID, DFN"
 S VISIT=+$G(ISIMISC("VISIT_IEN")) I '$D(^AUPNVSIT(VISIT)) QUIT EXIT_"^ ~ Missing or invalid Vist IEN"
 S PROVIDER=+$G(ISIMISC("PROVIDER")) I 'PROVIDER QUIT EXIT_"^ ~ Missing PROVIDER"
 S DATETIME=$G(ISIMISC("DATETIME"))
 S COMMENT=$G(ISIMISC("COMMENT")) ;FREE TEXT COMMENTS
 S PACKAGE=35 ;ORDER ENTRY/RESULTS REPORTING
 S SOURCE=36 ;TEXT INTEGRATION UTILITIES
 ;
 S X=$G(ISIMISC("SEVERITY")),SEVERITY=$S(X="M":X,X="MO":"MO",X="H":X,1:"M")
 ;
 N FDA K FDA
 S FDA(9000010.23,"+1,",.01)=HFACTOR ;HEALTH FACTOR
 S FDA(9000010.23,"+1,",.02)=DFN
 S FDA(9000010.23,"+1,",.03)=VISIT
 S FDA(9000010.23,"+1,",.04)=SEVERITY ;LEVEL/SEVERITY
 S FDA(9000010.23,"+1,",1201)=DATETIME ;EVENT DATE AND TIME
 S FDA(9000010.23,"+1,",1202)=PROVIDER ;ORDERING PROVIDER
 S FDA(9000010.23,"+1,",1204)=PROVIDER ;ENCOUNTER PROVIDER
 S FDA(9000010.23,"+1,",80101)=DATETIME ;        EDITED
 S FDA(9000010.23,"+1,",80102)="" ;      AUDIT TRAIL
 S FDA(9000010.23,"+1,",81101)=COMMENT ;COMMENTS
 S FDA(9000010.23,"+1,",81201)=1 ;Electronically Signed
 S FDA(9000010.23,"+1,",81202)=PACKAGE ;PACKAGE
 S FDA(9000010.23,"+1,",81203)=SOURCE ;DATA SOURCE
 ;
 I $G(ISIPARAM("DEBUG"))>0 D  
 . W !,"+++FDA  values+++",!
 . I $D(FDA) S X="" F  S X=$O(FDA(9000010.23,"+1,",X)) Q:X=""  W !,"FDA(9000010.23,+1,",X,")=",FDA(9000010.23,"+1,",X)
 . W !,"<HIT RETURN TO PROCEED>" R X:5
 . Q
 D UPDATE^DIE("","FDA",,"ERR")
 I $D(ERR) QUIT "-1^Fileman Error: "_$G(ERR("DIERR",1,"TEXT",1))
 S EXIT=0
 Q EXIT
 ;
IVPOV(ISIMISC)
 ; *****
 ; Entry point from ISIIMP07 (PROBLEM Creation) to automate V POV creation
 ; If you provide the 
 N ISIRC S ISIRC=0
 I '$D(ISIMISC) Q
 I $G(ISIMISC("ENTERED")) S ISIMISC("DATETIME")=ISIMISC("ENTERED")
 S ISIMISC("ICD9")=$G(ISIMISC("ICD"))
 ; DFN,PROVIDER should be set
 S ISIRC=$$VPOV(.ISIMISC)
 I $G(ISIPARAM("DEBUG"))>0 D  
 . W !,"+++ IVPOV^ISIIMP27 passed from ISIIMP07+++",!
 . W !,"ISIRC: ",$G(ISIRC)
 . W !,"PROBIEN: ",$G(ISIMISC("PROBIEN"))
 . I $D(ISIMISC) S X="" F  S X=$O(ISIMISC(X)) Q:X=""  W !,"ISIMISC(",X,")=",ISIMISC(X)
 . W !,"<HIT RETURN TO PROCEED>" R X:5
 Q
 ;
VPOV(ISIMISC)
 ; Populate V POV file (#9000010.07)
 ;
 N IICD9,COMMENT,ERR,EXIT,DFN,FDA,PACKAGE,VISIT,SOURCE,DATETIME,PRIMSEC,PROBIEN,MODIFIER
 S EXIT=-1
 ;
 S ISIRC=$$VALPOV^ISIIMPUG() ;Validate Array
 Q:+ISIRC<0 ISIRC
 ;
 S DFN=$G(ISIMISC("DFN")) I '$D(^DPT(DFN)) QUIT EXIT_"^ ~ Missing Patient ID, DFN"
 S VISIT=+$G(ISIMISC("VISIT_IEN")) I '$D(^AUPNVSIT(VISIT)) QUIT EXIT_"^ ~ Missing or invalid Vist IEN"
 S IICD9=$G(ISIMISC("ICD9")) I '$D(^ICD9(IICD9,0)) Q "-1^Missing ICD9 IEN (#80)"
 S DATETIME=$G(ISIMISC("DATETIME")) I 'DATETIME S DATETIME=$$NOW^XLFDT()
 S PROVIDER=+$G(ISIMISC("PROVIDER")) ;I 'PROVIDER QUIT EXIT_"^ ~ Missing PROVIDER"
 S PRIMSEC=$E($G(ISIMISC("PRIMSEC"))),PRIMSEC=$S(PRIMSEC="P":"P",PRIMSEC="S":"S",1:"P")
 S MODIFIER=$G(ISIMISC("MODIFIER")) ; MODIFIER ;9000010.07,.06 MODIFIER SET (C,D,F,M,O,P,R,S,T)
 S PROBIEN=+$G(ISIMISC("PROBIEN"))  ;9000010.07,.16 PROBLEM LIST ENTRY -> POINTER TO PROBLEM FILE (#9000011)
 I PROBIEN,'$D(^AUPNPROB(PROBIEN,0)) S PROBIEN=0
 ;
 S COMMENT=$G(ISIMISC("COMMENT")) ;FREE TEXT COMMENTS
 S PACKAGE=35 ;ORDER ENTRY/RESULTS REPORTING
 S SOURCE=36 ;TEXT INTEGRATION UTILITIES
 ;
 S FDA(9000010.07,"+1,",.01)=IICD9 ;POINTER TO ICD DIAGNOSIS FILE (#80)
 S FDA(9000010.07,"+1,",.02)=DFN
 S FDA(9000010.07,"+1,",.03)=VISIT
 I $L(MODIFIER),"|C|D|F|M|O|P|R|S|T|"[MODIFIER_"|" D  
 . S FDA(9000010.07,"+1,",.06)=MODIFIER
 . Q
 I PROBIEN S FDA(9000010.07,"+1,",.16)=PROBIEN
 S FDA(9000010.07,"+1,",.12)=PRIMSEC
 S FDA(9000010.07,"+1,",1201)=DATETIME
 S FDA(9000010.07,"+1,",1202)=PROVIDER ;Ordering PROV
 S FDA(9000010.07,"+1,",1204)=PROVIDER ;Encounter PROV
 S FDA(9000010.07,"+1,",81101)=COMMENT
 S FDA(9000010.07,"+1,",81202)=PACKAGE
 S FDA(9000010.07,"+1,",81203)=SOURCE
 ;
 D UPDATE^DIE("","FDA","","ERR")
 I $D(ERR) QUIT "-1^"_$G(ERR("DIERR",1,"TEXT",1))
 ;
 S EXIT=0
 QUIT EXIT
 ;
VPNTED(ISIMISC)
 N ETOPIC,COMMENT,ERR,EXIT,DFN,FDA,PACKAGE,VISIT,SOURCE,DATETIME
 S EXIT=-1
 ;
 S ISIRC=$$VALVPTED^ISIIMPUG() ;Validate Array
 Q:+ISIRC<0 ISIRC
 ;
 S DFN=$G(ISIMISC("DFN")) I '$D(^DPT(DFN)) QUIT EXIT_"^ ~ Missing Patient ID, DFN"
 S VISIT=+$G(ISIMISC("VISIT_IEN")) I '$D(^AUPNVSIT(VISIT)) QUIT EXIT_"^ ~ Missing or invalid Vist IEN"
 S ETOPIC=$G(ISIMISC("ED_TOPIC")) I '$D(^AUTTEDT(ETOPIC,0)) Q "-1^Missing EDUCATION TOPIC (#9999999.09)"
 S DATETIME=$G(ISIMISC("DATETIME"))
 S PROVIDER=+$G(ISIMISC("PROVIDER")) ;I 'PROVIDER QUIT EXIT_"^ ~ Missing PROVIDER"
 S LEVEL=+$G(ISIMISC("LEVEL_OF_UNDERSTAND"))
 ;
 S COMMENT=$G(ISIMISC("COMMENT")) ;FREE TEXT COMMENTS
 S PACKAGE=35 ;ORDER ENTRY/RESULTS REPORTING
 S SOURCE=36 ;TEXT INTEGRATION UTILITIES
 ;
 S FDA(9000010.16,"+1,",.01)=ETOPIC ;POINTER TO EDUCATION TOPIC (#9999999.09)
 S FDA(9000010.16,"+1,",.02)=DFN
 S FDA(9000010.16,"+1,",.03)=VISIT
 I $G(LEVEL) S FDA(9000010.16,"+1,",.06)=LEVEL ; LEVEL OF UNDERSTANDING
 S FDA(9000010.16,"+1,",1201)=DATETIME
 S FDA(9000010.16,"+1,",1202)=PROVIDER ;Ordering PROV
 S FDA(9000010.16,"+1,",1204)=PROVIDER ;Encounter PROV
 S FDA(9000010.16,"+1,",81101)=COMMENT
 S FDA(9000010.16,"+1,",81202)=PACKAGE
 S FDA(9000010.16,"+1,",81203)=SOURCE
 ;
 D UPDATE^DIE("","FDA","","ERR")
 I $D(ERR) QUIT "-1^"_$G(ERR("DIERR",1,"TEXT",1))
 ;
 S EXIT=0
 Q EXIT
 ;
VPROV(ISIMISC)
 ; 9000010.06  V PROVIDER
 ; Called internally APPT^ISIIMP05 (Appointment) when Provider is given
 N PROV,COMMENT,ERR,EXIT,DFN,FDA,PACKAGE,VISIT,SOURCE,DATETIME
 ;
 S DFN=$G(ISIMISC("DFN")) I '$D(^DPT(DFN)) QUIT EXIT_"^ ~ Missing Patient ID, DFN"
 S VISIT=+$G(ISIMISC("VISIT_IEN")) I '$D(^AUPNVSIT(VISIT)) QUIT EXIT_"^ ~ Missing or invalid Vist IEN"
 S PROV=$G(ISIMISC("PROVIDER")) I '$D(^VA(200,PROV,0)) Q "-1^Missing PROVIDER (#9000010.06,.01)"
 S DATETIME=$G(ISIMISC("ADATE"))
 S COMMENT=$G(ISIMISC("COMMENT")) ;FREE TEXT COMMENTS
 S PACKAGE=35 ;ORDER ENTRY/RESULTS REPORTING
 S SOURCE=36 ;TEXT INTEGRATION UTILITIES
 ;
 S FDA(9000010.06,"+1,",.01)=PROV
 S FDA(9000010.06,"+1,",.02)=DFN
 S FDA(9000010.06,"+1,",.03)=VISIT
 S FDA(9000010.06,"+1,",.04)="P" ;Primary (Primary/Secondary)
 S FDA(9000010.06,"+1,",.05)="A" ;Attending (Attending/Operating)
 S FDA(9000010.06,"+1,",1201)=DATETIME
 S FDA(9000010.06,"+1,",81101)=COMMENT
 S FDA(9000010.06,"+1,",81202)=PACKAGE
 S FDA(9000010.06,"+1,",81203)=SOURCE
 ;
 D UPDATE^DIE("","FDA","","ERR")
 I $D(ERR) QUIT "-1^"_$G(ERR("DIERR",1,"TEXT",1))
 Q 1
