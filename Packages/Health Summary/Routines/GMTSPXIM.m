GMTSPXIM ; SLC/SBW,KER - PCE Immunization component ;08/31/15  16:53
 ;;2.7;Health Summary;**8,10,28,56,89,114**;Oct 20, 1995;Build 11
 ;
 ; External References
 ;   DBIA  1239  IMMUN^PXRHS03
 ;   DBIA 10011  ^DIWP
 ;
IMMUNCDT ;Main entry point for chron (SIMC)
 N PXFG
 S PXFG="C" D IMMUN
 Q
 ;
IMMUNRDT ;Main entry point for reverse chron (SIMR)
 N PXFG
 S PXFG="R" D IMMUN
 Q
 ;
IMMUND ;Main entry point for detailed display (DIM)
 N GMTSF
 S GMTSF=""  ; set flag to display detailed
IMMUN ; Main Entry Point for simple format (IM,SIM)
 K ^TMP("PXI",$J) I '$D(PXFG) S PXFG="A"
 D IMMUN^PXRHS03(DFN,PXFG) Q:'$D(^TMP("PXI",$J))
 N GMSX1,GMSX2,GMIFN,GMW,GMSITE,GMN0,GMN1,GMSIR,GMSIC,X,GMTSDAT,GML,CNT
 N GMTSX,GMCKP,GMTAB,COMMENT,GMTSLN,GMICL,DIWL,GMIX
 N GMTSEDAT,GMD,GMFOOTC,GMFOOTR
 D CKP^GMTSUP Q:$D(GMTSQIT)  I '$D(GMTSF) D HDR
 S GMSX1="" F  S GMSX1=$O(^TMP("PXI",$J,GMSX1)) Q:GMSX1=""  D  Q:$D(GMTSQIT)
 . S GMSX2="",GMW=0 S:$G(PXFG)="A" GMD=0
 . F  S GMSX2=$O(^TMP("PXI",$J,GMSX1,GMSX2)) Q:GMSX2=""  D  Q:$D(GMTSQIT)
 . . S GMIFN=0
 . . S:$G(PXFG)'="A" GMD=0
 . . F  S GMIFN=$O(^TMP("PXI",$J,GMSX1,GMSX2,GMIFN)) Q:GMIFN'>0  D @($S($D(GMTSF):"IMMDET",1:"IMMDSP")) Q:$D(GMTSQIT)   ;+
 I '$D(GMTSF),$D(GMNOTE) D CKP^GMTSUP Q:$D(GMTSQIT)  D FOOT(.GMNOTE)
 K ^TMP("PXI",$J),PXFG
 Q
 ;
IMMDSP ; Display Immunization data
 ;GMW used to space between data grouped by primary sort
 ;GMD used to suppress/display of primary sort
 ;    based on sort type (Imm Name, FM date or Inverse FM date)
 S (GMFOOTR,GMFOOTC)=""
 S DIWL=0,CNT=0,COMMENT="",GMN0=$G(^TMP("PXI",$J,GMSX1,GMSX2,GMIFN,0)) Q:GMN0']""
 S GMN1=$G(^TMP("PXI",$J,GMSX1,GMSX2,GMIFN,1))
 S GMSITE=$S($P(GMN1,U,3)]"":$P(GMN1,U,3),$P(GMN1,U,4)]"":$P(GMN1,U,4),1:"No Site")
 S X=$P(GMN0,U,3) D REGDT4^GMTSU S GMTSDAT=X
 S GMSIR=$S($P(GMN0,U,6)="NONE":"",1:$P(GMN0,U,6)),GMSIC=$S(+$P(GMN0,U,7):"DO NOT REPEAT",1:"")
 I GMSIC]"",GMSIR]"" S GMSIR=$$TRUNCATE(GMSIR,20)_"; "  ;
 I GMSIC]""!(GMSIR]"") S GMFOOTR="<**>",GMNOTE("R")=""  ;set notation <**> or <C>
 S GMSIR=GMSIR_GMSIC
 D CKP^GMTSUP Q:$D(GMTSQIT)  D:GMTSNPG HDR
 I GMW=0 D CKP^GMTSUP Q:$D(GMTSQIT)  W !
 I $G(GMD)'>0!GMTSNPG D
 . ;Add 19
 .I $L($P(GMN0,U,1))>33 D  S GML=$L($P(GMN0,U,1))+1 Q
 .. I $E($P(GMN0,U,1),33)=" " W $E($P(GMN0,U,1),1,32)_"*"  Q
 .. W $E($P(GMN0,U,1),1,33)_"*"
 . W $P(GMN0,U,1) S GML=$L($P(GMN0,U,1))+1
 ; Comments
 D CKP^GMTSUP Q:$D(GMTSQIT)
 S COMMENT=$P(^TMP("PXI",$J,GMSX1,GMSX2,GMIFN,"COM"),U)
 I COMMENT]"" S GMNOTE("C")="",GMFOOTC="<C>"
 W ?35,$P(GMN0,U,4),?42,GMTSDAT,?53,$S($L(GMSITE)>10:$E(GMSITE,1,10)_"*",1:GMSITE),?65,GMFOOTR,?74,GMFOOTC,!
 ; Footer
 I GMFOOTR]"" D
 . S GMIX=$S('$D(GMIX):1,1:GMIX+1)
 . S GMNOTE("R",GMIX)=$P(GMN0,U,1)_U_GMTSDAT_U_GMSIR
 S GMW=1,GMD=1  ;end of record
 Q
HDR ; Header
 W "Immunization",?35,"Series",?42,"Date",?53,"Facility",?65,"Reaction",?74,"Info",!!
 Q
 ;
FORMAT ; Format Line
 N DIWR,DIWF,X S DIWL=3,DIWR=80-(GMICL+GMTAB) K ^UTILITY($J,"W")
 S X=$S(CNT=1:"Reaction: "_GMSIR,CNT=2:"COMMENTS: "_COMMENT,CNT=3:"FULL NAME: "_FULLNAME,CNT=4:"READING COMMENT: "_RCOMMENT,1:"") D ^DIWP
 Q
 ;
LINE ; Writes Line
 D CKP^GMTSUP Q:$D(GMTSQIT)
 W ?($S(CNT=1:5,CNT=2:2,CNT=3:2,CNT=4:2,1:0)),^UTILITY($J,"W",DIWL,GMTSLN,0),! ;+
 Q
 ;
FOOT(GMNOTE) ;displays footnotes for reaction and comments
 N GMF,GMIX
 D CKP^GMTSUP Q:$D(GMTSQIT)
 W !
 I $D(GMNOTE("R")) D  Q:$D(GMTSQIT)
 . S GMIX="" F  S GMIX=$O(GMNOTE("R",GMIX)) Q:GMIX=""!($D(GMTSQIT))  D
 . . D CKP^GMTSUP Q:$D(GMTSQIT)
 . . W !,"<**> ",$$TRUNCATE($P(GMNOTE("R",GMIX),U),23),?30,$P(GMNOTE("R",GMIX),U,2),?42,$P(GMNOTE("R",GMIX),U,3)
 I $D(GMNOTE("C")) D
 . D CKP^GMTSUP Q:$D(GMTSQIT)
 . W !,"<C>  See the Detailed Immunizations Health Summary Component[DIM] for Comments"
 W !!
 K GMNOTE
 Q
 ;
IMMDET  ;Main entry point for Detailed format (DIM)
 N GMNVIS,PXVI,PXV,GMN2,GMN3,GMN4,GMN0,GMN1,FULLNAME,GMSIR,GMSIC,GMTSRDAT,GMTSRRDAT
 S DIWL=0
 S GMN0=$G(^TMP("PXI",$J,GMSX1,GMSX2,GMIFN,0)) Q:GMN0']""
 S GMN1=$G(^TMP("PXI",$J,GMSX1,GMSX2,GMIFN,1))
 S GMN2=$G(^TMP("PXI",$J,GMSX1,GMSX2,GMIFN,2))
 S GMN3=$G(^TMP("PXI",$J,GMSX1,GMSX2,GMIFN,3))
 S GMN4=$G(^TMP("PXI",$J,GMSX1,GMSX2,GMIFN,4))
 S GMSIR=$P(GMN0,U,6),GMSIC=$S(+$P(GMN0,U,7):"DO NOT REPEAT",1:"")
 S PXVI="" F  S PXVI=$O(^TMP("PXI",$J,GMSX1,GMSX2,GMIFN,"VIS",PXVI)) Q:PXVI'>0  D
 . S GMNVIS(PXVI)=^TMP("PXI",$J,GMSX1,GMSX2,GMIFN,"VIS",PXVI)
 S GMSITE=$S($P(GMN1,U,3)]"":$P(GMN1,U,3),$P(GMN1,U,4)]"":$P(GMN1,U,4),1:"No Site")
 S X=$P(GMN0,U,3) D REGDT4^GMTSU S GMTSDAT=X
 S X=$P($G(GMN3),U,3) D REGDT4^GMTSU S GMTSEDAT=X
 S X=$P($G(GMN4),U,3) D REGDT4^GMTSU S GMTSRDAT=X
 S X=$P($G(GMN4),U,5) D REGDT4^GMTSU S GMTSRRDAT=X
 D CKP^GMTSUP Q:$D(GMTSQIT)
 W !!,"IMMUNIZATION: ",$$TRUNCATE($P(GMN0,U),70)
 D CKP^GMTSUP Q:$D(GMTSQIT)
 S FULLNAME=$G(^TMP("PXI",$J,GMSX1,GMSX2,GMIFN,"FN",1))
 W ! S GMICL=2,GMTAB=2,CNT=3 D FORMAT I $D(^UTILITY($J,"W")) D CKP^GMTSUP Q:$D(GMTSQIT)  D
 . F GMTSLN=1:1:^UTILITY($J,"W",DIWL) D LINE Q:$D(GMTSQIT)
 D CKP^GMTSUP Q:$D(GMTSQIT)
 W ?2,"DOSAGE: "_$S($P(GMN2,U,3)="":"",1:$P(GMN2,U,3)),?40,"SERIES: "_$P(GMN0,U,4)
 D CKP^GMTSUP Q:$D(GMTSQIT)
 W !,?2,"DATE ADMINISTERED: "_GMTSDAT
 D CKP^GMTSUP Q:$D(GMTSQIT)
 W !,?2,"MANUFACTURER: ",$$TRUNCATE($P($G(GMN3),U,2),64)
 D CKP^GMTSUP Q:$D(GMTSQIT)
 W !,?2,"LOT #: "_$P($G(GMN3),U,1),?40,"EXP DATE: "_GMTSEDAT
 D CKP^GMTSUP Q:$D(GMTSQIT)
 W !,?2,"ADMIN ROUTE/SITE: "_$$TRUNCATE($P(GMN2,U,1),29)_" ",$$TRUNCATE($P(GMN2,U,2),30)
 D CKP^GMTSUP Q:$D(GMTSQIT)
 W !,?2,"REACTION/CONTRAINDICATED: "_GMSIR_$S(GMSIC]"":"; "_GMSIC,1:"")
 D CKP^GMTSUP Q:$D(GMTSQIT)
 W !,?2,"LOCATION: "_($$TRUNCATE(GMSITE,66))
 S RCOMMENT=$P(^TMP("PXI",$J,GMSX1,GMSX2,GMIFN,"RCOM"),U)
 I $$TRIM^XLFSTR($TR(GMN4,"^"," "))'=""!(RCOMMENT'="") D  ;If any of the reading fields are populated, display all of them
 . D CKP^GMTSUP Q:$D(GMTSQIT)
 . W !,?2,"RESULTS: "_($$TRUNCATE($P(GMN4,U,1),27)),?40,"READING: "_$P(GMN4,U,2)
 . D CKP^GMTSUP Q:$D(GMTSQIT)
 . W !,?2,"DATE READ: "_GMTSRDAT,?40,"READER: "_$$TRUNCATE($P(GMN4,U,4),32)
 . D CKP^GMTSUP Q:$D(GMTSQIT)
 . W !,?2,"READING RECORDED: "_GMTSRRDAT,?40,"HOURS READ POST-INOCULATION: "_$P(GMN4,U,6)
 . I RCOMMENT="" D  Q
 . . D CKP^GMTSUP Q:$D(GMTSQIT)
 . . W !,?2,"READING COMMENT:"
 . S GMICL=2,GMTAB=2,CNT=4
 . D FORMAT
 . I $D(^UTILITY($J,"W")) D CKP^GMTSUP Q:$D(GMTSQIT)  D
 . . F GMTSLN=1:1:^UTILITY($J,"W",DIWL) D CKP^GMTSUP Q:$D(GMTSQIT)  W !,?2,$G(^UTILITY($J,"W",DIWL,GMTSLN,0))
 I $D(GMNVIS) D
 . F PXV=0:0 S PXV=$O(GMNVIS(PXV)) Q:PXV=""  D
 . . D CKP^GMTSUP Q:$D(GMTSQIT)
 . . W !,?2,"VIS: ",$$TRUNCATE($P(GMNVIS(PXV),U),30)
 . . S X=$P(GMNVIS(PXV),U,2) D REGDT4^GMTSU W ?40,"Edition Date: "_X
 E  D CKP^GMTSUP Q:$D(GMTSQIT)  W !,?2,"VIS: ",?40,"Edition Date:"
 D CKP^GMTSUP Q:$D(GMTSQIT)
 W !,?2,"ADMINISTERED BY: "_$P(GMN0,U,9)
 I $P(GMN0,U,8)'="" D
 . D CKP^GMTSUP Q:$D(GMTSQIT)
 . W !,?2,"ORDERED BY: "_$P(GMN0,U,8)
 I $P(GMN2,U,4)'="",$P(GMN2,U,4)'=$P(GMN0,U,9) D
 . D CKP^GMTSUP Q:$D(GMTSQIT)
 . W !,?2,"DOCUMENTED BY: "_$P(GMN2,U,4)
 D CKP^GMTSUP Q:$D(GMTSQIT)
 S COMMENT=$P(^TMP("PXI",$J,GMSX1,GMSX2,GMIFN,"COM"),U)
 I COMMENT]"" W ! D
 . S GMICL=2,GMTAB=2,CNT=2 D FORMAT I $D(^UTILITY($J,"W")) D CKP^GMTSUP Q:$D(GMTSQIT)  D
 . . F GMTSLN=1:1:^UTILITY($J,"W",DIWL) D LINE Q:$D(GMTSQIT)
 E  W !,?2,"COMMENTS:"
 Q
 ;
TRUNCATE(PXVDAT,PXVVAL) ;truncate
 ;
 I $L(PXVDAT)>+PXVVAL Q $E(PXVDAT,1,PXVVAL-1)_"*"
 Q PXVDAT
