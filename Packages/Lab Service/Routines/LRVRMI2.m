LRVRMI2 ;DALOI/STAFF - LAH/TMP TO FILE #63 ;03/24/11  17:09
 ;;5.2;LAB SERVICE;**350**;Sep 27, 1994;Build 230
 ;
 ; Continuation of LRVRMI4 and is used for extracting results from the LAH global and storing it into LAB DATA FILE (#63).
 ;
 Q
 ;
N7 ; Process Parasite Remarks
 N DIERR,IEN,LRCMT,LRFDA,LRFDAIEN,LRIEN,LRIENS,LRMSG,LRPL,LRX,X,STAT
 S LRX=$G(^TMP("LRMI",$J,LRDFN,"MI",LRIDT,7,0))
 S LRPL=$P(LRX,"^"),STAT=$P(LRX,"^",4)
 D BLDSTAT^LRVRMI4A(63.05,15,STAT,.LRSTATUS)
 M LRCMT=^LR(LRDFN,"MI",LRIDT,7)
 ;
 S IEN=0
 F  S IEN=$O(^TMP("LRMI",$J,LRDFN,"MI",LRIDT,7,IEN)) Q:IEN<1  D  ;
 . S LRX=^TMP("LRMI",$J,LRDFN,"MI",LRIDT,7,IEN,0),LRX=$S(LRX'="":LRX,1:" ")
 . I LRX'=" ",$$DUPCHK^LRVRMI3(LRLL,LRPROF,.LRCMT,LRX) Q
 . S LRIEN="+"_IEN_","_LRIDT_","_LRDFN_","
 . S LRFDA(7,63.36,LRIEN,.01)=LRX
 ;
 I $D(LRFDA) D
 . D UPDATE^DIE("","LRFDA(7)","LRFDAIEN","LRMSG")
 . S IEN=0
 . F  S IEN=$O(LRFDAIEN(IEN)) Q:'IEN  I $G(LRPL) D SETPL^LRRPLUA(LRDFN_",MI,"_LRIDT_",7,"_LRFDAIEN(IEN),LRPL)
 ;
 K LRFDA,LRIEN,LRMSG,DIERR
 S LRIEN=LRIDT_","_LRDFN_","
 S LRFDA(7,63.05,LRIEN,14)=LRNOW
 S LRFDA(7,63.05,LRIEN,15.5)=$S($G(LRDUZ):LRDUZ,1:$G(DUZ))
 D FILE^DIE("","LRFDA(7)","LRMSG")
 Q
 ;
 ;
N10 ; Process Mycology Remarks
 N DIERR,IEN,LRCMT,LRFDA,LRIEN,LRIENS,LRMSG,LRPL,LRX,STAT
 S LRX=$G(^TMP("LRMI",$J,LRDFN,"MI",LRIDT,10,0))
 S LRPL=$P(LRX,"^"),STAT=$P(LRX,U,4)
 D BLDSTAT^LRVRMI4A(63.05,19,STAT,.LRSTATUS)
 M LRCMT=^LR(LRDFN,"MI",LRIDT,10)
 ;
 S IEN=0
 F  S IEN=$O(^TMP("LRMI",$J,LRDFN,"MI",LRIDT,10,IEN)) Q:IEN<1  D
 . S LRX=^TMP("LRMI",$J,LRDFN,"MI",LRIDT,10,IEN,0),LRX=$S(LRX'="":LRX,1:" ")
 . I LRX'=" ",$$DUPCHK^LRVRMI3(LRLL,LRPROF,.LRCMT,LRX) Q
 . S LRIEN="+"_IEN_","_LRIDT_","_LRDFN_","
 . S LRFDA(10,63.38,LRIEN,.01)=LRX
 I $D(LRFDA) D
 . D UPDATE^DIE("","LRFDA(10)","LRFDAIEN","LRMSG")
 . S IEN=0
 . F  S IEN=$O(LRFDAIEN(IEN)) Q:'IEN  I $G(LRPL) D SETPL^LRRPLUA(LRDFN_",MI,"_LRIDT_",10,"_LRFDAIEN(IEN),LRPL)
 ;
 K LRFDA,LRIEN,LRMSG,DIERR
 S LRIEN=LRIDT_","_LRDFN_","
 S LRFDA(10,63.05,LRIEN,18)=LRNOW
 S LRFDA(10,63.05,LRIEN,19.5)=$S($G(LRDUZ):LRDUZ,1:$G(DUZ))
 D FILE^DIE("","LRFDA(10)","LRMSG")
 Q
 ;
 ;
N13 ; Process TB Rpt Remarks
 N DIERR,IEN,LRCMT,LRFDA,LRIEN,LRIENS,LRMSG,LRPL,LRX,STAT
 S LRX=$G(^TMP("LRMI",$J,LRDFN,"MI",LRIDT,13,0))
 S LRPL=$P(LRX,"^"),STAT=$P(LRX,U,4)
 D BLDSTAT^LRVRMI4A(63.05,23,STAT,.LRSTATUS)
 M LRCMT=^LR(LRDFN,"MI",LRIDT,13)
 ;
 S IEN=0
 F  S IEN=$O(^TMP("LRMI",$J,LRDFN,"MI",LRIDT,13,IEN)) Q:IEN<1  D
 . S LRX=^TMP("LRMI",$J,LRDFN,"MI",LRIDT,13,IEN,0),LRX=$S(LRX'="":LRX,1:" ")
 . I LRX'=" ",$$DUPCHK^LRVRMI3(LRLL,LRPROF,.LRCMT,LRX) Q
 . S LRIEN="+"_IEN_","_LRIDT_","_LRDFN_","
 . S LRFDA(13,63.41,LRIEN,.01)=LRX
 I $D(LRFDA) D
 . D UPDATE^DIE("","LRFDA(13)","LRFDAIEN","LRMSG")
 . S IEN=0
 . F  S IEN=$O(LRFDAIEN(IEN)) Q:'IEN  I $G(LRPL) D SETPL^LRRPLUA(LRDFN_",MI,"_LRIDT_",13,"_LRFDAIEN(IEN),LRPL)
 ;
 K LRFDA,LRIEN,LRMSG,DIERR
 S LRIEN=LRIDT_","_LRDFN_","
 S LRFDA(13,63.05,LRIEN,22)=LRNOW
 S LRFDA(13,63.05,LRIEN,25.5)=$S($G(LRDUZ):LRDUZ,1:$G(DUZ))
 D FILE^DIE("","LRFDA(13)","LRMSG")
 Q
 ;
 ;
N15 ; Mycology smear/prep
 N DIERR,IEN,LRCMT,LRFDA,LRIEN,LRIENS,LRMSG,LRPL,LRX
 S LRX=$G(^TMP("LRMI",$J,LRDFN,"MI",LRIDT,15,0))
 S LRPL=$P(LRX,"^"),STAT=$P(LRX,U,4)
 D BLDSTAT^LRVRMI4A(63.05,19,STAT,.LRSTATUS)
 M LRCMT=^LR(LRDFN,"MI",LRIDT,15)
 ;
 S IEN=0
 F  S IEN=$O(^TMP("LRMI",$J,LRDFN,"MI",LRIDT,15,IEN)) Q:IEN<1  D  ;
 . S LRX=^TMP("LRMI",$J,LRDFN,"MI",LRIDT,15,IEN,0),LRX=$S(LRX'="":LRX,1:" ")
 . I LRX'=" ",$$DUPCHK^LRVRMI3(LRLL,LRPROF,.LRCMT,LRX) Q
 . S LRIEN="+"_IEN_","_LRIDT_","_LRDFN_","
 . S LRFDA(15,63.371,LRIEN,.01)=LRX
 ;
 I $D(LRFDA) D
 . D UPDATE^DIE("","LRFDA(15)","LRFDAIEN","LRMSG")
 . S IEN=0
 . F  S IEN=$O(LRFDAIEN(IEN)) Q:'IEN  I $G(LRPL) D SETPL^LRRPLUA(LRDFN_",MI,"_LRIDT_",15,"_LRFDAIEN(IEN),LRPL)
 ;
 K LRFDA,LRIEN,LRMSG,DIERR
 S LRIEN=LRIDT_","_LRDFN_","
 S LRFDA(15,63.05,LRIEN,18)=LRNOW
 S LRFDA(15,63.05,LRIEN,19.5)=$S($G(LRDUZ):LRDUZ,1:$G(DUZ))
 D FILE^DIE("","LRFDA(15)","LRMSG")
 Q
 ;
 ;
N18 ; Process Virology Rpt Remarks
 N DIERR,IEN,LRCMT,LRFDA,LRIEN,LRIENS,LRMSG,LRPL,LRX,STAT
 ;
 S LRX=$G(^TMP("LRMI",$J,LRDFN,"MI",LRIDT,18,0))
 S LRPL=$P(LRX,"^"),STAT=$P(LRX,U,4)
 D BLDSTAT^LRVRMI4A(63.05,34,STAT,.LRSTATUS)
 M LRCMT=^LR(LRDFN,"MI",LRIDT,18)
 ;
 S IEN=0
 F  S IEN=$O(^TMP("LRMI",$J,LRDFN,"MI",LRIDT,18,IEN)) Q:IEN<1  D  ;
 . S LRX=^TMP("LRMI",$J,LRDFN,"MI",LRIDT,18,IEN,0),LRX=$S(LRX'="":LRX,1:" ")
 . I LRX'=" ",$$DUPCHK^LRVRMI3(LRLL,LRPROF,.LRCMT,LRX) Q
 . S LRIEN="+"_IEN_","_LRIDT_","_LRDFN_","
 . S LRFDA(18,63.44,LRIEN,.01)=LRX
 I $D(LRFDA) D
 . D UPDATE^DIE("","LRFDA(18)","LRFDAIEN","LRMSG")
 . S IEN=0
 . F  S IEN=$O(LRFDAIEN(IEN)) Q:'IEN  I $G(LRPL) D SETPL^LRRPLUA(LRDFN_",MI,"_LRIDT_",18,"_LRFDAIEN(IEN),LRPL)
 ;
 K LRFDA,LRIEN,LRMSG,DIERR
 S LRIEN=LRIDT_","_LRDFN_","
 S LRFDA(18,63.05,LRIEN,33)=LRNOW
 S LRFDA(18,63.05,LRIEN,35)=$S($G(LRDUZ):LRDUZ,1:$G(DUZ))
 D FILE^DIE("","LRFDA(18)","LRMSG")
 Q
 ;
 ;
HEAD ;
 ; Start by getting the demographic data from LAH
 S EOL=$G(^LAH(LWL,1,ISQN,.1,"OBR","EOL"))
 S FID=$G(^LAH(LWL,1,ISQN,.1,"OBR","FID"))
 S ORCDT=$G(^LAH(LWL,1,ISQN,.1,"OBR","ORCDT"))
 S ORDNLT=$G(^LAH(LWL,1,ISQN,.1,"OBR","ORDNLT"))
 S ORDP=$G(^LAH(LWL,1,ISQN,.1,"OBR","ORDP"))
 S PEB=$G(^LAH(LWL,1,ISQN,.1,"OBR","PEB"))
 S PVB=$G(^LAH(LWL,1,ISQN,.1,"OBR","PVB"))
 S SID=$G(^LAH(LWL,1,ISQN,.1,"OBR","SID"))
 S DFN=$G(^LAH(LWL,1,ISQN,.1,"PID","DFN"))
 S DOB=$G(^LAH(LWL,1,ISQN,.1,"PID","DOB"))
 S ICN=$G(^LAH(LWL,1,ISQN,.1,"PID","ICN"))
 S LRTDFN=$G(^LAH(LWL,1,ISQN,.1,"PID","LRTDFN"))
 S PNM=$G(^LAH(LWL,1,ISQN,.1,"PID","PNM"))
 S SEX=$G(^LAH(LWL,1,ISQN,.1,"PID","SEX"))
 S SSN=$G(^LAH(LWL,1,ISQN,.1,"PID","SSN"))
 S LRUID=$G(^LAH(LWL,1,ISQN,.3))
 Q
