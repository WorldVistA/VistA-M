LRHYT2 ;DALOI/HOAK - RCEV CHECK FOR ALL SCANNED ;9/17/2000
 ;;5.2;LAB SERVICE;**405**;Sep 27, 1994;Build 93
 ;
BUILD ;
 K ^TMP("LRHY3TST",$J)
 ; build a tmp global for all tests on order
 W !,"All tests on this order:"
 N LR3DT,LR3SN,LR3TST,LRIEN
 S LR3DT=0
 F  S LR3DT=$O(^LRO(69,"C",LRCE,LR3DT)) Q:+LR3DT'>0  D
 .  S LR3SN=0
 .  F  S LR3SN=$O(^LRO(69,"C",LRCE,LR3DT,LR3SN)) Q:+LR3SN'>0  D
 ..  S LR3TST=0 S LRIEN=0
 ..  S LR3NX0=$G(^LRO(69,LR3DT,1,LR3SN,0))
 ..  F  S LR3TST=$O(^LRO(69,LR3DT,1,LR3SN,2,"B",LR3TST)) Q:+LR3TST'>0  D
 ...  S LRIEN=0 S LRIEN=$O(^LRO(69,LR3DT,1,LR3SN,2,"B",LR3TST,0))
 ...  K LRNPEED
 ...  I $D(^LRO(69,LR3DT,1,LR3SN,2,LRIEN,1.1)) D
 ....  S LRNPEED=$G(^LRO(69,LR3DT,1,LR3SN,2,LRIEN,1.1,1,0))
 ...  S ^TMP("LRHY3TST",$J,LRCE,LR3TST)=""
 ...  ;
 ...  S LRACC6="" N LRAD,LRAN,LRAA,LRN  D ACCN
 ...  S LRURGNCY=+$P(^LRO(69,LR3DT,1,LR3SN,2,LRIEN,0),U,2)
 ...  I LRURGNCY S LRURGNCY=$P(^LAB(62.05,LRURGNCY,0),U)
 ...  W !,$E($G(LRACC6),1,14),?15,$P($G(^LAB(60,LR3TST,0)),U)
 ...  W ?40,$G(LRURGNCY)
 ...  I $G(LRNPEED)'="" W !,?20,$G(LRNPEED)
 W ! F LRHYI=1:1:79 W "-"
 W !
 ;
 QUIT
REMOVE ;
 ;
 ;
 QUIT
ACCN ;
 S LRN=$G(^LRO(69,LR3DT,1,LR3SN,2,LRIEN,0))
 Q:'LRN
 S LRAD=$P(LRN,U,3)
 I 'LRAD S LRACC="--" QUIT
 S LRAA=$P(LRN,U,4) S LRAN=$P(LRN,U,5)
 S LRACC6=$E($P(^LRO(68,LRAA,0),U,11),1,2)_" "_$E(LRAD,4,7)_" "_LRAN
 QUIT
