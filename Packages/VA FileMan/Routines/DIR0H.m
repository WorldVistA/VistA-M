DIR0H ;SFISC/MKO-HELP FOR SCREENS ;27JUN2004
 ;;22.2;MSC Fileman;;Jan 05, 2015;
 ;;Submitted to OSEHRA 5 January 2015 by the VISTA Expertise Network.
 ;;Based on Medsphere Systems Corporation's MSC Fileman 1051.
 ;;Licensed under the terms of the Apache License, Version 2.0.
 ;;GFT;**999,1004**
 ;
 N DIR0DX,DIR0DY,DIR0X,DIREPLIN
 S DIR0DX=DX,DIR0DY=DY
 S DIREPLIN=$P($$EZBLD^DIALOG(7002),U,$S($G(DIR0("REP")):2,1:1)) ;INSERT/REPLACE
 W $P(DDGLVID,DDGLDEL,10)_$P(DDGLCLR,DDGLDEL,2)_$P(DDGLVID,DDGLDEL)
 D HLP^DDGLIBH(9231,9233,"DDSH",IOSL-1)
 I $D(DDS)#2 D
 .D R^DDS3 I $D(DDO)#2 D
 .. I 'DDO D CMD
 .. E  D
 ... K DDSH
EGP ... S DX=0,DY=IOSL-1 X DDXY W $$EZBLD^DIALOG(8000) ;**CCO/NI 'COMMAND:'
 ... S DX=IOM-35 X IOXY W $P(DDGLVID,DDGLDEL,10)_$$EZBLD^DIALOG(8074) ;** 'PF1-H'
 E  W $P(DDGLCLR,DDGLDEL,2)
 ;
 S DX=IOM-$L(DIREPLIN)-1,DY=IOSL-1 X IOXY
 W $P(DDGLVID,DDGLDEL,10)_$S('$D(DDGLVAN):$P(DDGLVID,DDGLDEL,6),1:"")_DIREPLIN_$P(DDGLVID,DDGLDEL,10)
 ;
 S DY=$P(DIR0,U,4),DX=$P(DIR0,U,5)
 I $D(DIR("A"))=11 D
 . S DIR0X=""
 . F  S DIR0X=$O(DIR("A",DIR0X)) Q:DIR0X=""  D
 .. X IOXY
 .. W DIR("A",DIR0X)
 .. S DY=DY+1
 ;
 I $D(DIR("A"))#2 D
 . X IOXY W DIR("A")
 . I $D(DDS),DDO,DY=IOSL-1 W $P(DDGLCLR,DDGLDEL)
 ;
 S DIR0X=$E(DIR0A,DIR0C-DIR0DX+DIR0S,DIR0C+DIR0F-DIR0DX-1)
 S DX=DIR0S,DY=DIR0DY X IOXY W $S('$D(DDGLVAN):$P(DDGLVID,DDGLDEL,6),1:"")_DIR0X,$E(DIR0SP,$L(DIR0X)+1,999)
 S DX=DIR0DX X IOXY
 Q
 ;
 ;
 ;
H ;MOUSE COMES HERE
 N HELP,I
 F I=0:0 S I=$O(^DI(.84,9234,2,I)) Q:'I  I $G(^(I,0))'?." " S HELP($O(HELP(""),-1)+1)=^(0)
 D HLP^DDSUTL(.HELP) Q
 D:$D(DDS)#2 R^DDS3 ;REFRESH
 Q
 ;
 ;
 ;
CMD ;
 K DDH,DDQ
 F DDH=1:1 Q:$D(DIR("?",DDH))[0  S DDH(DDH,"T")=DIR("?",DDH)
 S:$D(DIR("?"))#2 DDH(DDH,"T")=DIR("?")
 D LIST^DDSU
 Q
