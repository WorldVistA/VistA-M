KMPTASK ;SP/JML - Cache TaskManager Task ;6/1/2020
 ;;4.0;CAPACITY MANAGEMENT;**1**;3/1/2018;Build 27
 ;
 ;
TASK(KMPVNSP) ; CHECK CREATE OR RESUME KMPVRUN TASK IN CACHE TASKMGR
 N I,KMPVMSG,KMPVNSPE,KMPVROLS,KMPVSTAT,KMPVTASK,KMPVTFLG,KMPVTSK,KMPVTSKS
 ;
 ; Start: only in KMPTASK version - comment out for ZSTU
 S KMPVROLS=$ROLES
 I (KMPVROLS'["%All")&(KMPVROLS'["%Manager") D  Q
 .W !,"You must have either the %Manager or the %All Role",!
 ; End: only in KMPV version ----
 ;
 I '$D(KMPVNSP)||'##Class(%SYS.Namespace).Exists(KMPVNSP) S KMPVNSP=$ZDEFNSP
 S KMPVTSK="KMPVRUN"
 I KMPVNSP'=$ZDEFNSP S KMPVTSK=KMPVTSK_"_"_KMPVNSP
 S KMPVMSG="CHECKING KMPV SETUP IN "_KMPVNSP_" NAMESPACE..."
 W !,KMPVMSG,!!
 DO ##class(%SYS.System).WriteToConsoleLog("ZSTU: "_KMPVMSG,0,0)
 ; Start: only in ZSTU version ----
 ;I '$$EXIST^%R("KMPVRUN.int",KMPVNSP) D  Q "VSMRUN Check Complete... No Routine."
 ;.S KMPVMSG(1)="KMPVRUN routine does not exist in "_KMPVNSP_" namespace."
 ;.S KMPVMSG(2)="VSM is not installed on this instance."
 ;.F I=1:1:2 W !,KMPVMSG(I) D WCCLOG(KMPVMSG(I))
 ;.W !
 ; End: only in ZSTU version ----
 S KMPVTFLG=0
 S KMPVTSKS=##class(%ResultSet).%New("%SYS.TaskSuper.TaskListDetail")
 S KMPVSTAT=KMPVTSKS.Execute()
 D DisplayError^%apiOBJ(KMPVSTAT)
 while KMPVTSKS.Next() {
 if (KMPVTSKS.GetDataByName("Task Name")=KMPVTSK) {
 	set KMPVTID=KMPVTSKS.GetDataByName("ID")
 	set KMPVTRUN=KMPVTSKS.GetDataByName("Next Scheduled Date")_" at "_KMPVTSKS.GetDataByName("Next Scheduled Time")
 	if KMPVTSKS.GetDataByName("Suspended")'="" {
 		do ##class(%SYS.Task).Resume(KMPVTID)
 		S KMPVMSG=KMPVTSK_" Task #"_KMPVTID_" Exists and Resumed to Run at "_KMPVTRUN
 	} Else {
 		S KMPVMSG=KMPVTSK_" Task #"_KMPVTID_" Exists and Scheduled to Run at "_KMPVTRUN
 		}
 	set KMPVTFLG=1
 	W !,KMPVMSG
 	DO ##class(%SYS.System).WriteToConsoleLog("ZSTU: "_KMPVMSG,0,0)
 	quit
 		}
 }
 ;
 ;create task if it doesn't exist
 I 'KMPVTFLG D
 .S KMPVTASK=##Class(%SYS.Task).%New()
 .S KMPVTASK.Name=KMPVTSK
 .S KMPVTASK.Description = "Start VSM Collection Drivers"
 .S KMPVTASK.NameSpace=KMPVNSP
 .S KMPVTASK.TaskClass="%SYS.Task.RunLegacyTask"
 .S KMPVTASK.Settings=$lb("ExecuteCode","D RUN^KMPVRUN")
 .S KMPVTASK.RunAsUser = "_SYSTEM"
 .S KMPVTASK.Priority=0
 .S KMPVTASK.StartDate = $p($h,",",1)+1
 .S KMPVTASK.DailyFrequency=0 ;task.DailyFrequencyDisplayToLogical("Once")
 .S KMPVTASK.DailyFrequencyTime=""
 .S KMPVTASK.DailyIncrement=""
 .S KMPVTASK.DailyStartTime=60
 .S KMPVTASK.Expires=0
 .S KMPVTASK.DailyEndTime=""
 .S KMPVTASK.RescheduleOnStart=1
 .S KMPVSTAT=KMPVTASK.%Save()
 .I $System.Status.IsError(KMPVSTAT) D  Q
 ..S KMPVMSG(1)="Error #"_$System.Status.GetErrorCodes(KMPVSTAT)
 ..S KMPVMSG(2)=$System.Status.GetOneStatusText(KMPVSTAT,1)
 ..S KMPVMSG(3)="Failed to Create and Schedule Task "_KMPVTSK_" in Cache Task Manager"
 ..F I=1:1:3 W !,KMPVMSG(I) DO ##class(%SYS.System).WriteToConsoleLog("ZSTU: "_KMPVMSG(I),0,1)
 .S KMPVMSG="Created and scheduled Task "_KMPVTSK_" in Cache Task Manager"
 .W !,KMPVMSG DO ##class(%SYS.System).WriteToConsoleLog("ZSTU: "_KMPVMSG,0,0)
 Q
 ;
SETRT(KMPMKEY,KMPRT,KMPFVAL) ;
 I KMPMKEY="VTCM" D
 .K ^KMPTMP("KMPV","VTCM")
 .I KMPRT=1 D VTCMRT(.KMPFVAL)
 .I KMPRT=0 D VTCMLEG(.KMPFVAL)
 I KMPMKEY="VSTM" D
 .K ^KMPTMP("KMPV","VSTM")
 .I KMPRT=1 D VSTMRT(.KMPFVAL)
 .I KMPRT=0 D VSTMLEG(.KMPFVAL)
 I KMPMKEY="VBEM" D
 .K ^KMPTMP("KMPV","VBEM")
 .I KMPRT=1 D VBEMRT(.KMPFVAL)
 .I KMPRT=0 D VBEMLEG(.KMPFVAL)
 I KMPMKEY="VMCM" D
 .K ^KMPTMP("KMPV","VMCM")
 .I KMPRT=1 D VMCMRT(.KMPFVAL)
 .I KMPRT=0 D VMCMLEG(.KMPFVAL)
 I KMPMKEY="VHLM" D
 .K ^KMPTMP("KMPV","VHLM")
 .I KMPRT=1 D VHLMRT(.KMPFVAL)
 .I KMPRT=0 D VHLMLEG(.KMPFVAL)
 I KMPMKEY="VCSM" D
 .K ^KMPTMP("KMPV","VCSM")
 .K ^KMPTMP("KMPDT")
 .I KMPRT=1 D VCSMRT(.KMPFVAL)
 .I KMPRT=0 D VCSMLEG(.KMPFVAL)
 I KMPMKEY="VETM" D
 .K ^KMPTMP("KMPV","VETM")
 .I KMPRT=1 D VETMRT(.KMPFVAL)
 .I KMPRT=0 D VETMLEG(.KMPFVAL)
 Q 
 ;
VTCMLEG(KMPFVAL) ;
 S KMPFVAL("VERSION")=2
 S KMPFVAL("COLLECTION INTERVAL")=15
 S KMPFVAL("CACHE DAILY TASK")="KMPVVTCM"
 S KMPFVAL("TASKMAN OPTION")="KMPV VTCM DATA TRANSMISSION"
 Q
 ;
VTCMRT(KMPFVAL) ;
 S KMPFVAL("VERSION")=3
 S KMPFVAL("COLLECTION INTERVAL")=5
 S KMPFVAL("CACHE DAILY TASK")="KMPTCMRT"
 S KMPFVAL("TASKMAN OPTION")="KMPV VTCM DATA RETRANSMISSION"
 Q
 ;
VSTMLEG(KMPFVAL) ;
 N KMPVERROR
 D ENABLE("KMPS",1)
 D RESCH^XUTMOPT("KMPS SAGG REPORT","T+1@01",,"28D","L",.KMPVERROR)
 S KMPFVAL("VERSION")=1
 S KMPFVAL("CACHE DAILY TASK")="KMPVVSTM"
 S KMPFVAL("TASKMAN OPTION")="KMPV VSTM DATA TRANSMISSION"
 Q
 ;
VSTMRT(KMPFVAL) ;
 D ENABLE("KMPS",0)
 D RESCH^XUTMOPT("KMPS SAGG REPORT","@",,,.KMPVERROR)
 S KMPFVAL("VERSION")=3
 S KMPFVAL("CACHE DAILY TASK")="KMPSTMRT"
 S KMPFVAL("TASKMAN OPTION")="KMPV VSTM DATA RETRANSMISSION"
 Q
 ;
VBEMLEG(KMPFVAL) ;
 S KMPFVAL("VERSION")=1
 S KMPFVAL("COLLECTION INTERVAL")=60
 S KMPFVAL("CACHE DAILY TASK")=""
 S KMPFVAL("TASKMAN OPTION")="KMPV VBEM DATA TRANSMISSION"
 Q
 ;
VBEMRT(KMPFVAL) ;
 S KMPFVAL("VERSION")=3
 S KMPFVAL("COLLECTION INTERVAL")=5
 S KMPFVAL("CACHE DAILY TASK")="KMPBEMRT"
 S KMPFVAL("TASKMAN OPTION")="KMPV VBEM DATA RETRANSMISSION"
 Q
 ;
VMCMLEG(KMPFVAL) ;
 S KMPFVAL("VERSION")=1
 S KMPFVAL("COLLECTION INTERVAL")=60
 S KMPFVAL("CACHE DAILY TASK")="KMPVVMCM"
 S KMPFVAL("TASKMAN OPTION")="KMPV VMCM DATA TRANSMISSION"
 Q
 ;
VMCMRT(KMPFVAL) ;
 S KMPFVAL("VERSION")=3
 S KMPFVAL("COLLECTION INTERVAL")=15
 S KMPFVAL("CACHE DAILY TASK")="KMPMCMRT"
 S KMPFVAL("TASKMAN OPTION")="KMPV VMCM DATA RETRANSMISSION"
 Q
 ;
VHLMLEG(KMPFVAL) ;
 S KMPFVAL("VERSION")=1
 S KMPFVAL("COLLECTION INTERVAL")=720
 S KMPFVAL("CACHE DAILY TASK")=""
 S KMPFVAL("TASKMAN OPTION")="KMPV VHLM DATA TRANSMISSION"
 Q
 ;
VHLMRT(KMPFVAL) ;
 S KMPFVAL("VERSION")=3
 S KMPFVAL("COLLECTION INTERVAL")=5
 S KMPFVAL("CACHE DAILY TASK")="KMPHLMRT"
 S KMPFVAL("TASKMAN OPTION")="KMPV VHLM DATA RETRANSMISSION"
 Q
 ;
VCSMLEG(KMPFVAL) ;
 N KMPDA
 ; ENABLE LEGACY OPTIONS
 D ENABLE("KMPD",1)
 ; Manually delete - required fields for VSM, not for legacy collection
 ; Version and Collection Interval set to 0 for legacy to not break Lambda function
 S KMPDA=$O(^KMPV(8969,"B","VCSM",""))
 S $P(^KMPV(8969,KMPDA,0),"^",4)="0" ; VERSION
 S $P(^KMPV(8969,KMPDA,1),"^",2)="0" ; RESET COLLECTION INTERVAL
 S $P(^KMPV(8969,KMPDA,1),"^",3)="" ; CACHE DAILY TASK
 S KMPFVAL("TASKMAN OPTION")="KMPD BACKGROUND DRIVER"
 Q
 ;
VCSMRT(KMPFVAL) ; 
 ; DISABLE LEGACY OPTIONS
 D ENABLE("KMPD",0)
 S KMPFVAL("VERSION")=3
 S KMPFVAL("COLLECTION INTERVAL")=5
 S KMPFVAL("CACHE DAILY TASK")="KMPCSMRT"
 S KMPFVAL("TASKMAN OPTION")="KMPV VCSM DATA RETRANSMISSION"
 Q
 ;
VETMLEG(KMPFVAL) ;
 ; set version=0, null cache daily task and taskman option, turn off
 ; Manually set required fields for VSM, not for legacy collection
 ; Version and Collection Interval set to 0 for legacy to not break Lambda function
 S KMPDA=$O(^KMPV(8969,"B","VETM",""))
 S $P(^KMPV(8969,KMPDA,0),"^",4)="0" ; VERSION
 S $P(^KMPV(8969,KMPDA,1),"^",2)="0" ; RESET COLLECTION INTERVAL
 S $P(^KMPV(8969,KMPDA,1),"^",3)="" ; CACHE DAILY TASK
 S $P(^KMPV(8969,KMPDA,1),"^",7)="" ; TASKMAN OPTION
 S KMPFVAL("ONOFF")=0
 Q
 ;
VETMRT(KMPFVAL) ;
 S KMPFVAL("VERSION")=3
 S KMPFVAL("CACHE DAILY TASK")="KMPETMRT"
 S KMPFVAL("TASKMAN OPTION")="KMPV VETM DATA RETRANSMISSION"
 Q
 ;
ENABLE(KMPNS,KMPABLE) ;
 N DA,DIC,DIE,DR,KMPMSG,KMPOPT,KMPOPTS,KMPTIEN,X,Y
 D KMPOPTS(KMPNS,.KMPOPTS)
 S KMPMSG=$S(KMPABLE=1:"@",1:"Migrated to VSM MANAGEMENT. Contact CPEVSM@DOMAIN.EXT")
 S KMPOPT=""
 F  S KMPOPT=$O(KMPOPTS(KMPOPT)) Q:KMPOPT=""  D
 .S X=KMPOPT
 .S DIC=19,DIC(0)="B"
 .D ^DIC
 .S KMPTIEN=+Y
 .I KMPTIEN>0 D
 ..S DIE=19,DA=KMPTIEN,DR="2///"_KMPMSG D ^DIE
 Q
 ;
KMPOPTS(KMPNS,KMPOPTS) ;
 K KMPOPTS
 I KMPNS="KMPS" D
 .S KMPOPTS("KMPS SAGG FILE")="",KMPOPTS("KMPS SAGG MANAGER")=""
 .S KMPOPTS("KMPS SAGG REPORT")="",KMPOPTS("KMPS SAGG STATUS")=""
 .S KMPOPTS("KMPS SAGG STOP")=""
 I KMPNS="KMPD" D
 .S KMPOPTS("KMPD BACKGROUND DRIVER")="",KMPOPTS("KMPD CM DEVELOPER TOOLS")=""
 .S KMPOPTS("KMPD CM TOOLS MANAGER MENU")="",KMPOPTS("KMPD CM TOOLS REPORTS")=""
 .;S KMPOPTS("KMPD ECHO")="" - LEAVE OPTION UNTIL REPLACED
 .S KMPOPTS("KMPD PARAM EDIT")=""
 .S KMPOPTS("KMPD STATUS")="",KMPOPTS("KMPD TMG AVG HR TT")=""
 .S KMPOPTS("KMPD TMG AVG TTL")="",KMPOPTS("KMPD TMG DLY TTL DETAIL")=""
 .S KMPOPTS("KMPD TMG HRLY TTL")="",KMPOPTS("KMPD TMG HRLY TTL DETAIL")=""
 .S KMPOPTS("KMPD TMG HRLY TTL RT")="",KMPOPTS("KMPD TMG MONITOR")=""
 .S KMPOPTS("KMPD TMG REPORTS")="",KMPOPTS("KMPD TMG START/STOP")=""
 .S KMPOPTS("KMPD TMG TTL ALERT")="",KMPOPTS("KMPD TMG TTL ALERT RT")=""
 Q
 ;
GLOSTATS(KMPDIRS) ;
 N B,KMPDIR,KMPRES,KMPSTAT,KMPSTATE,KMPVLN
 S KMPVLN=1,B="|"
 S KMPSTATE=##class(%SQL.Statement).%New()
 S KMPSTAT=KMPSTATE.%PrepareClassQuery("%SYS.GlobalQuery","Size")
 S KMPDIR=""
 F  S KMPDIR=$O(KMPDIRS(KMPDIR)) Q:KMPDIR=""  D
 .S KMPRES=KMPSTATE.%Execute(KMPDIR,"","*")
 .D GLOSET(KMPDIR)
 Q
 ;
GLOSET(KMPDIR) ;
 N KMPALL,KMPNAME,KMPUSE
 WHILE KMPRES.%Next() {
  S KMPNAME=KMPRES.%Get("Name")
  S KMPALL=KMPRES.%Get("Allocated MB")
  S KMPUSE=KMPRES.%Get("Used MB")
  S ^KMPTMP("KMPV","VSTM","TRANSMIT","GLOBALS",$J,KMPVLN)=KMPDIR_B_KMPNAME_B_KMPALL_B_KMPUSE,KMPVLN=KMPVLN+1
 }
 Q
 ;
ZERO() ;
 N B,KMPDATA,KMPFNUM,KMPGNAM,KMPVLN
 S U="^",B="|",KMPVLN=1
 S KMPFNUM=0 F  S KMPFNUM=$O(^DIC(KMPFNUM)) Q:'+KMPFNUM  D
 .Q:$G(^DIC(KMPFNUM,0))=""
 .Q:'$D(^DIC(KMPFNUM,0,"GL"))
 .S KMPGNAM=$G(^DIC(KMPFNUM,0,"GL")) Q:KMPGNAM=""
 .;   file num ^ file name ^ global root ^ version ^ entries ^ last id 
 .S KMPDATA=KMPFNUM_B_$P(^DIC(KMPFNUM,0),U)_B_KMPGNAM_B_+$G(^DD(+$P(^DIC(KMPFNUM,0),U,2),0,"VR"))_B_+$P($G(@(KMPGNAM_"0)")),U,4)_B_+$P($G(@(KMPGNAM_"0)")),U,3)
 .S ^KMPTMP("KMPV","VSTM","TRANSMIT","ZERO",$J,KMPVLN)=KMPDATA,KMPVLN=KMPVLN+1
 Q
