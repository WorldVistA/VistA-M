KMPPST42 ;SP/JML - Collect Cache Metrics for the Real-Time Error Trap Monitor ;5/1/2021
 ;;4.0;CAPACITY MANAGEMENT;**1,2**;3/1/2018;Build 3
 ;
 ; Integration Agreements
 ;  $$PKGVER^XPDIP supported by ICR #2067
 ;  KILL^%ZTLOAD supported by ICR #10063
 ;  OPTION^%ZTLOAD() supported by ICR #10063
 ;
PRE ;
 N KMPPIEN,KMPVER,KMPVIEN
 ; stop current KMP monitors
 W !,"Stopping Monitors"
 D STOPALL^KMPVCBG
 ; kill current period data
 K ^KMPTMP("KMPV","VBEM")
 K ^KMPTMP("KMPV","VCSM")
 K ^KMPTMP("KMPV","VETM")
 K ^KMPTMP("KMPV","VHLM")
 K ^KMPTMP("KMPV","VMCM")
 K ^KMPTMP("KMPV","VSTM")
 K ^KMPTMP("KMPV","VTCM")
 K ^KMPTMP("KMPDT")
 ; Update package file Current Version
 S KMPPIEN=$$FIND1^DIC(9.4,"","O","CAPACITY MANAGEMENT","","","ERR")
 S KMPVER="4^3180419^"_DT_"^"_DUZ
 S KMPVIEN=$$PKGVER^XPDIP(KMPPIEN,KMPVER)
 Q
 ;
POST ;
 N KMPVERROR
 ; Unschedule any legacy TaskMan tasks
 W !,"De-scheduling TaskMan tasks"
 D RESCH^XUTMOPT("KMPV VBEM DATA TRANSMISSION","@",,,.KMPVERROR)
 D RESCH^XUTMOPT("KMPV VHLM DATA TRANSMISSION","@",,,.KMPVERROR)
 D RESCH^XUTMOPT("KMPV VMCM DATA TRANSMISSION","@",,,.KMPVERROR)
 D RESCH^XUTMOPT("KMPV VSTM DATA TRANSMISSION","@",,,.KMPVERROR)
 D RESCH^XUTMOPT("KMPV VTCM DATA TRANSMISSION","@",,,.KMPVERROR)
 ; REMOVE ANY VSM TASKMAN TASKS
 D RESCH^XUTMOPT("KMPV VBEM DATA RETRANSMISSION","@",,,.KMPVERROR)
 D RESCH^XUTMOPT("KMPV VHLM DATA RETRANSMISSION","@",,,.KMPVERROR)
 D RESCH^XUTMOPT("KMPV VMCM DATA RETRANSMISSION","@",,,.KMPVERROR)
 D RESCH^XUTMOPT("KMPV VSTM DATA RETRANSMISSION","@",,,.KMPVERROR)
 D RESCH^XUTMOPT("KMPV VTCM DATA RETRANSMISSION","@",,,.KMPVERROR)
 D RESCH^XUTMOPT("KMPV VCSM DATA RETRANSMISSION","@",,,.KMPVERROR)
 D RESCH^XUTMOPT("KMPV VETM DATA RETRANSMISSION","@",,,.KMPVERROR)
 ; Delete all existing VSM TaskMan tasks
 W !,"Deleting Task Entries"
 D TASKS
 ; Delete TaskMan field data in ^KMPV(8969
 W !,"Deleting TaskMan configuration fields in ^KMPV(8969"
 D FIELDS
 ; job off restart-will hang to allow current jobs to exit before starting
 J RESTART^KMPPST42
 W !,"Post Install Complete"
 Q
 ;
TASKS ;
 N KMPMON,KMPTLEG,KMPTNEW
 F KMPMON="VBEM","VCSM","VETM","VHLM","VMCM","VSTM","VTCM" D
 .S KMPTLEG="KMPV "_KMPMON_" DATA TRANSMISSION" D DELETE(KMPTLEG)
 .S KMPTNEW="KMPV "_KMPMON_" DATA RETRANSMISSION" D DELETE(KMPTNEW)
 D DELETE("KMPD BACKGROUND DRIVER")
 D DELETE("KMPS SAGG REPORT")
 D DELETE("KMPR BACKGROUND DRIVER")
 Q
 ;
DELETE(KMPTNAME) ;
 N ZTSK,KMPOUT
 D OPTION^%ZTLOAD(KMPTNAME,.KMPOUT)
 S ZTSK=""
 F  S ZTSK=$O(@KMPOUT@(ZTSK)) Q:ZTSK=""  D
 .D KILL^%ZTLOAD
 K @KMPOUT
 Q
 ;
FIELDS ;
 N DIE,DA,DR,KMPKEY,KMPFIELD
 S DIE=8969
 S KMPKEY=""
 F  S KMPKEY=$O(^KMPV(8969,"B",KMPKEY)) Q:KMPKEY=""  D
 .S DA=$O(^KMPV(8969,"B",KMPKEY,""))
 .F KMPFIELD=1.05,1.06,1.07 D
 ..S DR=KMPFIELD_"///@"
 ..D ^DIE
 Q
 ;
RESTART ;
 ; Restart monitors
 H 60*20
 W !,"Restarting Monitors"
 D STARTALL^KMPVCBG
 D CFGMSG^KMPUTLW
 Q
