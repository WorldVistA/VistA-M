KMPSTMRT ;SP/JML - Collect Cache Metrics for the VistA Storage Monitor ;5/1/2021
 ;;4.0;CAPACITY MANAGEMENT;**1,2**;3/1/2018;Build 3
 ;
 ; Integration Agreements
 ;  Reference to $$WORKDAY^XUWORKDY supported by ICR #10046
 ;  Reference to GETENV^%ZOSV supported by ICR #10097
 ;  Reference to KMPVVSTM^%ZOSVKSD supported by ICR #6342
 ;  Reference to $$HTFM^XLFDT supported by ICR #10103
 ;
RUN ;
 N $ES,$ETRAP S $ETRAP="D ERR^ZU Q"
 I $$GETVAL^KMPVCCFG("VSTM","ONOFF",8969,"I") D RU^%ZOSVKR("KMP VSTM COLLECTOR")
 N B,KMPBPM,KMPBS,KMPCSMB,KMPDATE,KMPDB,KMPDFS,KMPDIR,KMPDISK,KMPDNUM,KMPDSTR,KMPES,KMPFB,KMPFSMB,KMPINST
 N KMPISD,KMPMSMB,KMPNDTYP,KMPSC,KMPSTAT,KMPSTR,KMPTI,KMPVDATA,KMPVDIR,KMPVLN,KMPVNODE,KMPVSINF,Y
 N KMPDATA,KMPDST,KMPETSO,KMPFMDAY,KMPTIMES,KMPUTCE,KMPUTCO,KMPWORK
 ; ALWAYS - verify data is not building past configured number of days - if so for any reason, delete it
 D PURGEDLY^KMPVCBG("VSTM")
 ; Quit if monitor is not turned on
 I $$GETVAL^KMPVCCFG("VSTM","ONOFF",8969)'="ON" D  Q
 .I $$GETVAL^KMPVCCFG("VSTM","ONOFF",8969,"I") D RU^%ZOSVKR("KMP VSTM COLLECTOR")
 ; Check environment, quit if test and test systems not allowed
 I $$PROD^KMPVCCFG()'="prod",$$GETVAL^KMPVCCFG("VSTM","ALLOW TEST SYSTEM",8969,"I")'=1 Q
 ; Only run if 1st or 15th day of the month
 S KMPDNUM=$SYSTEM.SQL.DAYOFMONTH(+$H)
 ; SET KMPTEST=1 TO RUN TEST ON DAYS OTHER THAN THE 1ST OR 15TH DAY OF MONTH
 I $G(KMPTEST) S KMPDNUM=1 K KMPTEST
 I (KMPDNUM=1)!(KMPDNUM=15) D
 .S KMPVSINF=$$SITEINFO^KMPVCCFG()
 .S KMPSC=$P(KMPVSINF,"^",5),B="|"
 .D GETENV^%ZOSV S KMPVNODE=$P(Y,U,3)_":"_$P($P(Y,U,4),":",2)
 .S KMPINST=$P(KMPVNODE,":",2),KMPNDTYP=$$NODETYPE^KMPUTLW(KMPINST)
 .S KMPFMDAY=+$$HTFM^XLFDT($H,1)
 .S KMPWORK=$$WORKDAY^XUWORKDY(KMPFMDAY)
 .S KMPTIMES=$$TSTAMP^KMPUTLW($H,"HOROLOG",1) ; yyy-mm-dd hh:mm:ssZts
 .S KMPETSO=$P(KMPTIMES,"^")
 .S KMPUTCO=$P(KMPTIMES,"^",2)
 .S KMPUTCE=$P(KMPTIMES,"^",3)
 .S KMPDST=$P(KMPTIMES,"^",4)
 .D KMPVVSTM^%ZOSVKSD(.KMPVDATA)
 .D STORAGE
 .D ZERONODE
 .D GLOBALS
 I $$GETVAL^KMPVCCFG("VSTM","ONOFF",8969,"I") D RU^%ZOSVKR("KMP VSTM COLLECTOR END")
 Q
 ;
STORAGE ;
 S KMPVDIR="",KMPVLN=1
 F  S KMPVDIR=$O(KMPVDATA(KMPVDIR)) Q:KMPVDIR=""  D
 .D GETDB(KMPVDIR,.KMPRES)
 .S KMPDB=KMPRES("DB"),KMPDISK=KMPRES("DISK"),KMPDIR=KMPRES("DIR")
 .S KMPDATA=$G(KMPVDATA(KMPVDIR))
 .;MaxSize(MB)^Current Size(MB)^Block Size(int)^Bocks per Map(int)^Free space(MB)^Free Space(int-Blocks)^System Dir(bool)^Expansion size^FreeSpace
 .S KMPMSMB=$$CALC($P(KMPDATA,"^",1)),KMPCSMB=$$CALC($P(KMPDATA,"^",2)),KMPBS=$P(KMPDATA,"^",3)
 .S KMPBPM=$P(KMPDATA,"^",4),KMPFSMB=$$CALC($P(KMPDATA,"^",5)),KMPFB=$P(KMPDATA,"^",6)
 .S KMPISD=$P(KMPDATA,"^",7),KMPES=$$CALC($P(KMPDATA,"^",8)),KMPDFS=$$CALC($P(KMPDATA,"^",9))
 .S KMPDSTR=KMPDB_B_KMPDISK_B_KMPDIR_B_KMPMSMB_B_KMPCSMB_B_KMPBS_B_KMPBPM_B_KMPFSMB_B_KMPFB_B_KMPISD_B_KMPES_B_KMPDFS
 .S ^KMPTMP("KMPV","VSTM","TRANSMIT","STORAGE",$J,KMPVLN)=KMPDSTR,KMPVLN=KMPVLN+1
 Q:'$D(^KMPTMP("KMPV","VSTM","TRANSMIT","STORAGE",$J))
 ; package data into JSON object and send
 N KMPDARR,KMPJMSG,KMPJSON
 S KMPJSON=##class(%DynamicObject).%New()
 S KMPJSON.Function="VSTM-STORAGE"
 D SITE^KMPUTLW(KMPJSON)
 S KMPJMSG=##class(%DynamicObject).%New()
 S KMPJMSG.Timestamp=KMPETSO,KMPJMSG.UtcOdbc=KMPUTCO
 S KMPJMSG.UtcEpoch=KMPUTCE,KMPJMSG.IsDst=KMPDST
 S KMPJMSG.Node=$P(KMPVNODE,":"),KMPJMSG.NodeType=KMPNDTYP
 S KMPJMSG.Instance=KMPINST,KMPJMSG.Date=$$SHORTDAT^KMPUTLW($H,"HOROLOG")
 S KMPJSON.MessageData=KMPJMSG
 S KMPDARR=##class(%DynamicArray).%New()
 S KMPTI=""
 F  S KMPTI=$O(^KMPTMP("KMPV","VSTM","TRANSMIT","STORAGE",$J,KMPTI))  Q:KMPTI=""  D
 .S KMPDATA=$G(^KMPTMP("KMPV","VSTM","TRANSMIT","STORAGE",$J,KMPTI))
 .D KMPDARR.%Push(KMPDATA)
 S KMPJSON.Details=KMPDARR
 S KMPSTAT=$$POST^KMPUTLW(KMPJSON,"/storage",,"VSTM")
 I +KMPSTAT'=200 S ^KMPTMP("KMPV","VSTM","RETRY",KMPVNODE,+$H,"/storage",$H)=KMPJSON.%ToJSON()
 K ^KMPTMP("KMPV","VSTM","TRANSMIT","STORAGE",$J)
 Q
 ;
ZERONODE ;
 D ZERO^KMPTASK()
 Q:'$D(^KMPTMP("KMPV","VSTM","TRANSMIT","ZERO",$J))
 ; package data into JSON object and send
 N KMPDARR,KMPJMSG,KMPJSON
 S KMPJSON=##class(%DynamicObject).%New()
 S KMPJSON.Function="VSTM-ZERONODE"
 D SITE^KMPUTLW(KMPJSON)
 S KMPJMSG=##class(%DynamicObject).%New()
 S KMPJMSG.Timestamp=KMPETSO,KMPJMSG.UtcOdbc=KMPUTCO
 S KMPJMSG.UtcEpoch=KMPUTCE,KMPJMSG.IsDst=KMPDST
 S KMPJMSG.Node=$P(KMPVNODE,":"),KMPJMSG.NodeType=KMPNDTYP
 S KMPJMSG.Instance=KMPINST,KMPJMSG.Date=$$SHORTDAT^KMPUTLW($H,"HOROLOG")
 S KMPJSON.MessageData=KMPJMSG
 S KMPDARR=##class(%DynamicArray).%New()
 S KMPTI=""
 F  S KMPTI=$O(^KMPTMP("KMPV","VSTM","TRANSMIT","ZERO",$J,KMPTI))  Q:KMPTI=""  D
 .S KMPDATA=$G(^KMPTMP("KMPV","VSTM","TRANSMIT","ZERO",$J,KMPTI))
 .D KMPDARR.%Push(KMPDATA)
 S KMPJSON.Details=KMPDARR
 S KMPSTAT=$$POST^KMPUTLW(KMPJSON,"/zeronode",1,"VSTM")
 I +KMPSTAT'=200 S ^KMPTMP("KMPV","VSTM","RETRY",KMPVNODE,+$H,"/zeronode",$H)=KMPJSON.%ToJSON()
 K ^KMPTMP("KMPV","VSTM","TRANSMIT","ZERO",$J)
 Q 
 ;
GLOBALS ;
 S KMPVDIR="",KMPVLN=1
 D GLOSTATS^KMPTASK(.KMPVDATA)
 Q:'$D(^KMPTMP("KMPV","VSTM","TRANSMIT","GLOBALS",$J))
 ; package data into JSON object and send
 N KMPDARR,KMPJMSG,KMPJSON
 S KMPJSON=##class(%DynamicObject).%New()
 S KMPJSON.Function="VSTM-GLOBALS"
 D SITE^KMPUTLW(KMPJSON)
 S KMPJMSG=##class(%DynamicObject).%New()
 S KMPJMSG.Timestamp=KMPETSO,KMPJMSG.UtcOdbc=KMPUTCO
 S KMPJMSG.UtcEpoch=KMPUTCE,KMPJMSG.IsDst=KMPDST
 S KMPJMSG.Node=$P(KMPVNODE,":"),KMPJMSG.NodeType=KMPNDTYP
 S KMPJMSG.Instance=KMPINST,KMPJMSG.Date=$$SHORTDAT^KMPUTLW($H,"HOROLOG")
 S KMPJSON.MessageData=KMPJMSG
 S KMPDARR=##class(%DynamicArray).%New()
 S KMPTI=""
 F  S KMPTI=$O(^KMPTMP("KMPV","VSTM","TRANSMIT","GLOBALS",$J,KMPTI))  Q:KMPTI=""  D
 .S KMPDATA=$G(^KMPTMP("KMPV","VSTM","TRANSMIT","GLOBALS",$J,KMPTI))
 .D KMPDARR.%Push(KMPDATA)
 S KMPJSON.Details=KMPDARR
 S KMPSTAT=$$POST^KMPUTLW(KMPJSON,"/globals",1,"VSTM")
 I +KMPSTAT'=200 S ^KMPTMP("KMPV","VSTM","RETRY",KMPVNODE,+$H,"/globals",$H)=KMPJSON.%ToJSON()
 K ^KMPTMP("KMPV","VSTM","TRANSMIT","GLOBALS",$J)
 Q
 ;
CALC(KMPVAL) ; Convert to MB if units specified in value, round to 3 decimal places
 I KMPVAL="Unlimited" Q 0   ; maximum can be "Unlimited"
 I KMPVAL["MB" Q $NUM(+KMPVAL*1,3)
 I KMPVAL["GB" Q $NUM(+KMPVAL*1024,3)
 Q $NUM(KMPVAL,3)
 ;
GETDB(DBSTR,KMPRES) ;
 N KMPSTYP
 I DBSTR["$" S KMPSTYP="VMS"
 I DBSTR["/" S KMPSTYP="UNIX"
 I DBSTR["\" S KMPSTYP="NT"
 I KMPSTYP="UNIX" D  Q
 .S KMPRES("DISK")=""
 .S KMPRES("DIR")=DBSTR
 .S KMPRES("DB")=$P(DBSTR,"/",$L(DBSTR,"/")-1)
 I KMPSTYP="VMS" D  Q
 .S KMPRES("DISK")=$P(DBSTR,":")
 .S KMPRES("DIR")=$P(DBSTR,":",2)
 .S KMPRES("DB")=$P($P(KMPRES("DIR"),".",$L(KMPRES("DIR"),".")),"]")
 I KMPSTYP="NT" D  Q
 .S KMPRES("DISK")=$P(DBSTR,"\")
 .S KMPRES("DIR")=$P(DBSTR,":",2)
 .S KMPRES("DB")=$P(DBSTR,"\",$L(DBSTR,"\")-1)
 E  D
 .S KMPRES("DISK")=""
 .S KMPRES("DIR")=""
 .S KMPRES("DB")=DBSTR
 Q
 ;
RETRY ;  retry failed POSTS
 N KMPDAY,KMPI,KMPJSON,KMPSTAT,KMPLOC,KMPVNODE,Y
 ;
 D GETENV^%ZOSV S KMPVNODE=$P(Y,"^",3)_":"_$P($P(Y,"^",4),":",2)
 S KMPDAY=""
 F  S KMPDAY=$O(^KMPTMP("KMPV","VSTM","RETRY",KMPVNODE,KMPDAY)) Q:KMPDAY=""  D
 .S KMPLOC=""
 .F  S KMPLOC=$O(^KMPTMP("KMPV","VSTM","RETRY",KMPVNODE,KMPDAY,KMPLOC)) Q:KMPLOC=""  D
 ..S KMPI=""
 ..F  S KMPI=$O(^KMPTMP("KMPV","VSTM","RETRY",KMPVNODE,KMPDAY,KMPLOC,KMPI)) Q:KMPI=""  D
 ...S KMPJSON=$G(^KMPTMP("KMPV","VSTM","RETRY",KMPVNODE,KMPDAY,KMPLOC,KMPI))
 ...S KMPSTAT=$$POST^KMPUTLW({}.%FromJSON(KMPJSON),KMPLOC,1,"VSTM")
 ...I +KMPSTAT=200 K ^KMPTMP("KMPV","VSTM","RETRY",KMPVNODE,KMPDAY,KMPLOC,KMPI)
 Q
