DVB67PT ;ALB/JAM - UPDATE DISABILITY CONDITION FILE ; 4/8/14 11:30am
 ;;4.0;HINQ;**67**;03/25/92;Build 8
 ;
 Q
 ;
 ;
EN ;Initialize variables and validate user.
 N DVBI,DVBJ,DVBREF,DIC,DO,X,Y
 I '$D(DUZ) D BMES^XPDUTL("*** PROGRAMMER NOT DEFINED ***") Q
DVBNEW ;Add new codes.
 D BMES^XPDUTL("** Updating DISABILITY CONDITION(#31) file **")
 F DVBI=1:1 S DVBJ=$P($T(NEWCODE+DVBI),";;",2) Q:DVBJ="QUIT"  D
 .S DVBREF=+DVBJ
 .I $D(^DIC(31,"C",DVBREF)) D DVBERR1 Q
 .K DO
 .S DIC="^DIC(31,",DIC(0)="L",DIC("DR")="2///"_DVBREF,X=$P(DVBJ,"^",2)
 .D FILE^DICN
 .I Y=-1 D DVBERR2 Q
 .D BMES^XPDUTL("** "_DVBREF_"    "_X_" has been added **")
 D BMES^XPDUTL("** Done **")
 Q
DVBERR1 ;Message to the user that the file entry already exists.
 D BMES^XPDUTL("*** A FILE ENTRY FOR DISABILITY CODE "_DVBREF_" HAS ALREADY BEEN CREATED ***")
 Q
DVBERR2 ;Message to the user that an error occurred.
 D BMES^XPDUTL("*** AN ERROR OCCURRED WHEN ATTEMPTING TO ADD NEW FILE ENTRIES. PLEASE CONTACT PRODUCT SUPPORT ***")
 Q
NEWCODE ;Code to be added.
 ;;7717^AL AMYLOIDOSIS (PRIMARY AMYLOIDOSIS)
 ;;QUIT
 Q
