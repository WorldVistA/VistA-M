FBXIP155 ;ALB/BJR-PATCH INSTALL ROUTINE ; 10/8/14 11:22am
 ;;3.5;FEE BASIS;**155**;JAN 30, 1995;Build 2
 ;;Per VA Directive 6402, this routine should not be modified.
 Q
 ;
UPDPOV ; Update Selected Purpose of Visits (POV)
 N FBCODE,FBDA,FBFDA,FBI,FBNAME,FBPROG,FBX,X,Y,FBCCODE
 D BMES^XPDUTL("  Updating selected POVs in the FEE BASIS PURPOSE OF VISIT (161.82) file...")
 ; verify IEN of OUTPATIENT program in FEE BASIS PROGRAM file
 I $P($G(^FBAA(161.8,2,0)),U)'="OUTPATIENT" D  Q
 . D MES^XPDUTL("    ERROR: Fee Program with IEN 2 is not OUTPATIENT.")
 . D MES^XPDUTL("    Purpose of Visits could not be updated.")
 . D MES^XPDUTL("    Please contact your IRM for assistance.")
 F FBI=1:1 S FBX=$P($T(POV+FBI),";;",2) Q:FBX="END"  D
 . S FBCODE=$P(FBX,U)
 . S FBNAME=$P(FBX,U,2)
 . S FBPROG=$P(FBX,U,3)
 . N DA,DD,DIC,DINUM,DLAYGO,DO,X
 . S FBCCODE=$O(^FBAA(161.82,"B",FBNAME,"")) I FBCCODE,$P(^FBAA(161.82,FBCCODE,0),U,3)=FBCODE D MES^XPDUTL("POV WITH CODE "_FBCODE_" ALREADY EXISTS AND HAS NOT BEEN ADDED") Q
 . S DIC="^FBAA(161.82,",DIC(0)="L",DLAYGO=161.82
 . S X=FBNAME
 . S DIC("DR")="2///^S X=FBPROG;3///^S X=FBCODE"
 . I +FBCODE,'$D(^FBAA(161.82,+FBCODE,0)) S DINUM=+FBCODE
 . D FILE^DICN
 . I Y<0 D MES^XPDUTL("ERROR ADDING POV WITH CODE "_FBCODE) Q
 . D MES^XPDUTL("POV WITH CODE "_FBCODE_" HAS BEEN ADDED")
 D BMES^XPDUTL("Post Install Completed.")
 Q
 ;
POV ;austin code^name^fee program for Purpose of Visit (POV) code(s)
 ;;25^NON-VA MEDICAL CARE, CAREGIVER OVERSIGHT VISITS^2
 ;;26^PROGRAM OF ALL-INCLUSIVE CARE OF THE ELDERLY (PACE)^2
 ;;27^VETERAN-DIRECTED HOME AND COMMUNITY BASED SERVICES (VD-HCBS)^2
 ;;28^AMBULANCE/TRAVEL - FOR INPATIENT OR OUTPATIENT SERVICES^2
 ;;END
 ;
 ;FBXIP155
