GMRAPENC ;SLC/JMH - Pharmacy API calls ;1/7/08  09:38
 ;;4.0;Adverse Reaction Tracking;**41**;Mar 29, 1996;Build 8
 ;
 ;$$DRP2CLP^GMRAPENC(50 IEN) => 50.605 IEN
 ;$$CODE2CLP^GMRAPENC(DRUG CLASS CODE) => 50.605 IEN
 ;$$CODE2CL^GMRAPENC(DRUG CLASS CODE) => 50.605 CLASS NAME
 ;$$DRP2CODE^GMRAPENC(50 IEN) => 50.605 CLASS CODE
 ;$$CLP2CODE^GMRAPENC(50.605 IEN) => 50.605 CLASS CODE
 ;$$CLP2CLDA^GMRAPENC(50.605 IEN) => 50.605 CLASS CODE ^ 50.605 CLASSIFICATION
 ;$$INP2INNA^GMRAPENC(50.416 IEN) => 50.416 INGREDIENT NAME
 ;
DRP2CLP(GMRAIEN) ;
 ;GMRAIEN = FILE 50 POINTER
 ;RETURN = FILE 50.605 POINTER
 N GMRARET
 K ^TMP($J,"GMRADATA")
 D DATA^PSS50(GMRAIEN,,,,,"GMRADATA")
 S GMRARET=+$G(^TMP($J,"GMRADATA",GMRAIEN,25))
 K ^TMP($J,"GMRADATA")
 Q GMRARET
CODE2CLP(GMRACODE) ;
 ;GMRACODE = DRUG CLASS CODE
 ;RETURN = FILE 50.605 POINTER
 N GMRARET
 K ^TMP($J,"GMRADATA")
 D IEN^PSN50P65(,GMRACODE,"GMRADATA")
 S GMRARET=$O(^TMP($J,"GMRADATA","B",GMRACODE,""))
 K ^TMP($J,"GMRADATA")
 Q GMRARET
CODE2CL(GMRACODE) ;
 ;GMRACODE = DRUG CLASS CODE
 ;RETURN = DRUG CLASS NAME
 N GMRARET
 K ^TMP($J,"GMRADATA")
 D IEN^PSN50P65(,GMRACODE,"GMRADATA")
 S GMRARET=$O(^TMP($J,"GMRADATA","B",GMRACODE,""))
 I GMRARET S GMRARET=$G(^TMP($J,"GMRADATA",GMRARET,1))
 K ^TMP($J,"GMRADATA")
 Q GMRARET
DRP2CODE(GMRAIEN) ;
 ;GMRAIEN = FILE 50 POINTER
 ;RETURN = DRUG CLASS CODE
 N GMRARET
 K ^TMP($J,"GMRADATA")
 D DATA^PSS50(GMRAIEN,,,,,"GMRADATA")
 S GMRARET=$P($G(^TMP($J,"GMRADATA",GMRAIEN,25)),U,2)
 K ^TMP($J,"GMRADATA")
 Q GMRARET
CLP2CODE(GMRAIEN) ;
 ;GMRAIEN = FILE 50.605 POINTER
 ;RETURN = DRUG CLASS CODE
 N GMRARET
 K ^TMP($J,"GMRADATA")
 D C^PSN50P65(GMRAIEN,,"GMRADATA")
 S GMRARET=$G(^TMP($J,"GMRADATA",GMRAIEN,.01))
 K ^TMP($J,"GMRADATA")
 Q GMRARET
CLP2CLDA(GMRAIEN) ;
 ;GMRAIEN = FILE 50.605 POINTER
 ;RETURN = DRUG CLASS CODE^DRUG CLASS CLASSIFICATION
 N GMRARET
 K ^TMP($J,"GMRADATA")
 D C^PSN50P65(GMRAIEN,,"GMRADATA")
 S GMRARET=$G(^TMP($J,"GMRADATA",GMRAIEN,.01))_U_$G(^TMP($J,"GMRADATA",GMRAIEN,1))
 K ^TMP($J,"GMRADATA")
 Q GMRARET
INP2INNA(GMRAIEN) ;
 ;GMRAIEN = FILE 50.416 POINTER
 ;RETURN = DRUG INGREDIENT NAME
 N GMRARET
 K ^TMP($J,"GMRADATA")
 D ZERO^PSN50P41(GMRAIEN,,,"GMRADATA")
 S GMRARET=$G(^TMP($J,"GMRADATA",GMRAIEN,.01))
 K ^TMP($J,"GMRADATA")
 Q GMRARET
