YSFORM ;SLC/TGA-HEADER & FOOTER FOR FORMS ;3/27/90  15:01 ;
 ;;5.01;MENTAL HEALTH;;Dec 30, 1994
ENHD ;  Called by routines YSDX3R, YSDX3R1, YSDX3RU, YSDSR, YSHX1R YSPDR1
 ;  YSPDXR, YSPHYR, YSPP, YSPP1, YSPP1A, YSPP3, YSPP4, YSPP5, YSPP6
 ;  YSPP7, YSPP8, YSPP9, YSPRBR1, YSPRBR2, YSPROB5, YSPROBR, YSPROSE
 ;  YSPTXR
 ;Generates page header
 S:'$D(YSCON) YSCON=0 S:'$D(YSFTR) YSFTR="" S:'$D(YSFHDR(1)) YSFHDR(1)="" W @IOF
 S:'$D(YSLCN) YSLCN=$$SITE,YSLCN="VAMC "_YSLCN
 I IOST?1"C-".E W YSNM,?47,"SSN ",YSSSN,?65,"DOB ",YSDOB,!
 I IOST?1"P".E W ! F I0=1:1:80 W "-"
 I IOST?1"P".E W ! W:YSFTR]"" "MEDICAL RECORD"
 W ?80-$L(YSFHDR)/2,YSFHDR I IOST?1"P".E W ! F I0=1:1:IOM W "-"
 W:YSCON !?25,"(Continued from previous page)" X:YSFHDR(1)]"" YSFHDR(1)
 K I0 S YSCON=0 Q
 ;
ENFT ;  Called by routines  YSDX3R1, YSDX3RU, YSDXR, YSHX1R YSPDR1
 ;  YSPDXR, YSPHYR, YSPP, YSPP1, YSPP1A, YSPP3, YSPP4, YSPP5
 ;  YSPP7, YSPP8, YSPP9, YSPRBR1, YSPRBR2, YSPROB5, YSPROBR1, YSPROSE
 ;  YSPTXR
 ;Generates page footer
 S:'$D(YSFTR) YSFTR="" S:'$D(YSCON) YSCON=0
 S:'$D(YSLCN) YSLCN=$$SITE,YSLCN="VAMC "_YSLCN
 F I0=1:1:(IOSL-$Y-9) W !
 W ! W:YSCON ?28,"(Continued on next page)"
 W ! F I0=1:1:IOM W "_"
 W !,YSNM,?(31+(37-$L(YSLCN)/2)),YSLCN W:YSFTR]"" ?69,"VAF 10-9034"
 I YSFTR]"" S YSFTR(1)="(VICE "_YSFTR_")"
 W !,YSSSN W:YSDOB]"" "   DOB ",YSDOB W:YSFTR]"" ?(80-$L(YSFTR(1))),YSFTR(1)
 W:IOST?1"P".E ! K I0 Q
 ;
SITE() ;Set YSLOC to location using KERNEL site parameter file entry
 N DIC,DA,DIQ,DR
 S YSDA=+$P(^XMB(1,1,"XUS"),U,17)
 S DIC=4,DR=".01",DA=YSDA,DIQ(0)="EN",DIQ="YSLOC" D EN^DIQ1
 S YSLOC=$P(YSLOC(4,+YSDA,.01,"E"),",")
 QUIT YSLOC
