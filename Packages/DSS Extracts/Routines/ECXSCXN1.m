ECXSCXN1 ;ALB/JAP  Clinic Extract No Shows; 8/28/02 1:11pm ;5/16/12  16:33
 ;;3.0;DSS EXTRACTS;**71,105,127,132,136**;Dec 22, 1997;Build 28
NOSHOW(ECXSD,ECXED) ;get noshows from file #44
 ;      ECXSD  = start date, ECXED  = end date
 N ALEN,CLIN,JDATE,JJ,NODE,NOSHOW,PP,STAT,MDIV,ECXEDIS,CNT,ECXARR,ECXSDSC,ECXEDSC ;136
 S CLIN=0
 F  S CLIN=$O(^TMP($J,"ECXCL",CLIN)) Q:'CLIN!($G(QFLG))  D  ;136
 .Q:$P($G(^TMP($J,"ECXCL",CLIN)),U,3)'="C"
 .S (P1,P2,P3)=""
 .D FEEDER^ECXSCX1(CLIN,ECXSD,.P1,.P2,.P3,.TOSEND,.ECXDIV)
 .Q:TOSEND=6
 .;find appts in date range
 .S ECXSDSC=ECXSD+.1,ECXEDSC=$P(ECXED,".") ;136 get dates back into correct fileman format
 .S (ALEN,NOSHOW)="" ;136
 .K ^TMP($J,"SDAMA301") ;136
 .S ECXARR(1)=ECXSDSC_";"_ECXEDSC,ECXARR(2)=CLIN,ECXARR(3)="NS;NSR",ECXARR("FLDS")="5;7;10;12;18" ;136
 .S CNT=$$SDAPI^SDAMA301(.ECXARR) ;136
 .Q:CNT=0  I CNT=-1 D ERR^ECXUTL1 S QFLG=1 Q  ;136 Stop if no data (CNT=0) and send error message if scheduling system down (CNT=-1)
 .S JJ=0 F  S JJ=$O(^TMP($J,"SDAMA301",CLIN,JJ)) Q:'+JJ  S JDATE="" F  S JDATE=$O(^TMP($J,"SDAMA301",CLIN,JJ,JDATE)) Q:'+JDATE  D  ;136
 ..S ECXDATE=JDATE,ECXTI=$P($$FMTE^XLFDT(JDATE,1),"@",2) ;136
 ..S ECXTI=$E(($TR(ECXTI,":","")_"000000"),1,6)
 ..S:ECXTI="000000" ECXTI="000300"
 ..S ECXDFN=JJ ;136
 ..S ECXPATCAT=$$PATCAT^ECXUTL(ECXDFN) ;added in patch 127
 ..S NODE=$G(^TMP($J,"SDAMA301",CLIN,JJ,JDATE)),MDIV=$P($G(^SC(CLIN,0)),U,15) ;136
 ..Q:NODE=""  ;136
 ..S ECXSHAD=$$GETSHAD ;added in patch 127, finds shad status for appt
 ..S ECXOBI=$S($P(NODE,U,7)="Y":"O",1:"") ;136
 ..S NOSHOW="N" ;136 only no-show entries are returned from the SDAMA301 call
 ..D INTPAT^ECXSCX2 S ECXERR=0
 ..D PAT1^ECXSCX2(ECXDFN,ECXDATE,.ECXERR) Q:ECXERR
 ..S ALEN=$$RJ^XLFSTR($P(NODE,U,5),3,0) ;136
 ..D PAT2^ECXSCX2(ECXDFN,ECXDATE)
 ..S ECXPVST=+$P(NODE,U,18),ECXATYP=+$P(NODE,U,10)  ;136 Get POV & appt type
 ..S:+ALEN=0 ALEN=$P($G(^TMP($J,"ECXCL",CLIN)),U,2)
 ..S ECXCLIN=CLIN,ECXSTOP=P1
 ..S ECXEDIS=$$EDIS^ECXUTL1(ECXDFN,ECXDATE,"N",,ECXSTOP) ;136
 ..S:ECXCPT1="" ECXCPT1="9919901"
 ..S ECXCBOC=$S(MDIV'="":$$CBOC^ECXSCX2(.MDIV),1:"")
 ..S (ECXDSSD,ECXENEL,ECXIR,ECXAO,ECXMIL,ECXPROV,ECXPROVP,ECXPROVN)=""
 ..F I=1:1:5 S (@("ECSP"_I),@("ECSPPC"_I),@("ECSPNPI"_I))=""
 ..I TOSEND'=3 D
 ...S ECXKEY=P1_P2_ALEN_P3_NOSHOW,ECXOBS=$$OBSPAT^ECXUTL4(ECXA,ECXTS,ECXKEY)
 ...S ECXENC=$$ENCNUM^ECXUTL4(ECXA,ECXSSN,ECXADMDT,ECXDATE,ECXTS,ECXOBS,ECHEAD,ECXKEY,) D:ECXENC'="" FILE^ECXSCXN
 ..I TOSEND=3 D
 ...S ECXKEY=P1_"000"_ALEN_P3_NOSHOW,ECXOBS=$$OBSPAT^ECXUTL4(ECXA,ECXTS,ECXKEY)
 ...S ECXENC=$$ENCNUM^ECXUTL4(ECXA,ECXSSN,ECXADMDT,ECXDATE,ECXTS,ECXOBS,ECHEAD,ECXKEY,) D:ECXENC'="" FILE^ECXSCXN
 ..I TOSEND=3 D
 ...S ECXKEY=P2_"000"_ALEN_P3_NOSHOW,ECXOBS=$$OBSPAT^ECXUTL4(ECXA,ECXTS,ECXKEY)
 ...S ECXENC=$$ENCNUM^ECXUTL4(ECXA,ECXSSN,ECXADMDT,ECXDATE,ECXTS,ECXOBS,ECHEAD,ECXKEY,) D:ECXENC'="" FILE^ECXSCXN
 ..;create a record for noshow appended ekg. The code was removed for CTX-0604-70970 CLI Extract Problem EXPANDED to NoShows
 Q
 ;GETSHAD section added with patch 127
GETSHAD() ;Function returns shad value
 N DIC,LOCARR,DA,DR,SHAD,ECXERR,ECXVIST
 S SHAD=""
 I '+$P($G(NODE),U,12) Q SHAD  ;136 Quit if no outpatient encounter pointer
 S DIC=409.68,DA=$P(NODE,U,12),DR=.05,DIQ(0)="I",DIQ="LOCARR" ;136
 D EN^DIQ1
 I $G(LOCARR(409.68,DA,.05,"I")) D
 .S ECXERR=0
 .D VISIT^ECXSCX1(ECXDFN,LOCARR(409.68,DA,.05,"I"),.ECXVIST,.ECXERR)
 .I 'ECXERR S SHAD=ECXVIST("SHAD")
 Q SHAD
