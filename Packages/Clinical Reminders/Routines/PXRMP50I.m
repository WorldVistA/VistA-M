PXRMP50I ;SLC/PKR - Inits for PXRM*2.0*50. ;10/29/2014
 ;;2.0;CLINICAL REMINDERS;**50**;Feb 04, 2005;Build 212
 ;==========================================
INACT ;Rename and inactivate obsolete national taxonomies.
 N IEN,NAME,TAXLIST,ZZNAME
 D BMES^XPDUTL("Checking for obsolete national taxonomies to be inactivated.")
 D TAXLIST(.TAXLIST)
 S NAME=""
 F  S NAME=$O(TAXLIST(NAME)) Q:NAME=""  D
 . S IEN=+$O(^PXD(811.2,"B",NAME,""))
 . I IEN=0 Q
 . D MES^XPDUTL(" Inactivating taxonomy "_NAME)
 . S $P(^PXD(811.2,IEN,0),U,6)=1
 . S ZZNAME="ZZ"_NAME
 . D RENAME^PXRMUTIL(811.2,NAME,ZZNAME)
 Q
 ;
 ;==========================================
PRE ;Pre-init
 ;Disable options and protocols
 D OPTION^PXRMUTIL("DISABLE")
 D PROTOCOL^PXRMUTIL("DISABLE")
 D INACT^PXRMP50I
 D DELEXE^PXRMEXSI("EXARRAY","PXRMP50E")
 Q
 ;
 ;==========================================
POST ;Post-init
 ;Enable options and protocols
 D OPTION^PXRMUTIL("ENABLE")
 D PROTOCOL^PXRMUTIL("ENABLE")
 D SETPVER^PXRMUTIL("2.0P50")
 ;Install Exchange File entries.
 D SMEXINS^PXRMEXSI("EXARRAY","PXRMP50E")
 D SENDIM^PXRMP50I
 Q
 ;
 ;==========================================
SENDIM ;Send install message.
 N FROM,NODE,PARAM,SYSTEM,SUBJECT,TO,VALUE
 S NODE="PXRM*2.0*50"
 K ^TMP(NODE,$J)
 ;DBIA #1131 for ^XMB("NETNAME")
 S FROM="PXRM*2.0*50 Install@"_^XMB("NETNAME")
 ;DBIA #2541
 S SYSTEM=$$KSP^XUPARAM("WHERE")
 I $$PROD^XUPROD(1) S TO("G.CLINICAL REMINDERS SUPPORT@DOMAIN.EXT")=""
 E  D
 . N MGIEN,MGROUP
 . S MGIEN=$G(^PXRM(800,1,"MGFE"))
 . S MGROUP=$S(MGIEN'="":"G."_$$GET1^DIQ(3.8,MGIEN,.01),1:DUZ)
 . S TO(MGROUP)=""
 S SUBJECT="Install of PXRM*2.0*50"
 S ^TMP(NODE,$J,1,0)="PXRM*2.0*50 has been installed."
 S ^TMP(NODE,$J,2,0)="System is "_SYSTEM
 D SEND^PXRMMSG(NODE,SUBJECT,.TO,FROM)
 Q
 ;
 ;==========================================
TAXLIST(LIST) ;Populate the list of taxonomies to inactivate.
 S LIST("VA-ALCOHOLISM SCREENING")=""
 S LIST("VA-CERVICAL CANCER SCREEN")=""
 S LIST("VA-HYPERTENSION SCREEN")=""
 S LIST("VA-ISCHEMIC HEART 412 DISEASE")=""
 S LIST("VA-ISCHEMIC HEART DISEASE")=""
 S LIST("VA-HIGH RISK FOR INFLUENZA")=""
 S LIST("VA-HIGH RISK FOR FLU/PNEUMONIA")=""
 S LIST("VA-HIGH RISK FOR PNEUMOCOCCAL DZ")=""
 S LIST("VA-HIGH RISK FOR TB")=""
 S LIST("VA-INFLUENZA IMMUNIZATION")=""
 S LIST("VA-PNEUMOCOCCAL VACCINE")=""
 S LIST("VA-TETANUS DIPHTHERIA")=""
 S LIST("VA-CHOLESTEROL")=""
 S LIST("VA-COLORECTAL CA")=""
 S LIST("VA-COLORECTAL CANCER SCREEN")=""
 S LIST("VA-EXERCISE COUNSELING")=""
 S LIST("VA-FLEXISIGMOIDOSCOPY")=""
 S LIST("VA-FOBT")=""
 S LIST("VA-MHV IHD AND ATHERSCLEROSIS")=""
 S LIST("VA-NUTRITION")=""
 S LIST("VA-OBESITY")=""
 S LIST("VA-PROSTATE CA")=""
 S LIST("VA-PSA")=""
 S LIST("VA-SAFETY COUNSELING")=""
 S LIST("VA-WEIGHT AND NUTRITION SCREEN")=""
 S LIST("VA-POLYTRAUMA AMPUTATION")=""
 S LIST("VA-POLYTRAUMA AUDITORY")=""
 S LIST("VA-POLYTRAUMA BRAIN INJURY")=""
 S LIST("VA-POLYTRAUMA BURN")=""
 S LIST("VA-POLYTRAUMA INPT REHAB")=""
 S LIST("VA-POLYTRAUMA ORTHO")=""
 S LIST("VA-POLYTRAUMA PTSD")=""
 S LIST("VA-POLYTRAUMA SCI")=""
 S LIST("VA-POLYTRAUMA VISION")=""
 S LIST("VA-POLYTRAUMA WAR INJURY")=""
 S LIST("VA-WH IUD INSERTION (TAXONOMY)")=""
 S LIST("VA-WH IUD REMOVAL (TAXONOMY)")=""
 Q
 ;
