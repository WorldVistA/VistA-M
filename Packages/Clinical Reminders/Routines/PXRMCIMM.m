PXRMCIMM ;SLC/AGP - Computed findings for IMMUNIZATIONS. ; 09/01/2021
 ;;2.0;CLINICAL REMINDERS;**65**;Feb 4, 2005;Build 438
 ;
 ;   API                   ICR#
 ;GETLOT^PXAPIIM           6387
 Q
LOTINFO(DFN,NGET,BDT,EDT,NFOUND,TEST,DATE,DATA,TEXT) ;
 N CNT,DTE,LOC,I,IMMIEN,IMMNAME,INPUTS,NODE,PXRMARR,PXRMCDAT,TCNT
 S INPUTS="",NFOUND=0,IMMIEN=""
 S DTE=$S(PXRMDATE'="":$P(PXRMDATE,"."),1:DT)
 I TEST["I:" D
 .S IMMNAME=$P(TEST,":",2)
 .S IMMIEN=$O(^AUTTIMM("B",IMMNAME,""))
 I TEST["C:" D
 .D CVXTOIEN^PXAPIIM(.PXRMCDAT,$P(TEST,":",2))
 .S IMMIEN=$P($G(PXRMCDAT),U)
 .S IMMNAME=$P($G(PXRMCDAT),U,2)
 .;S I=$O(^AUTTIMM("C",$P(TEST,":",2),""))
 .;S IMMNAME=$P(^AUTTIMM(I,O),U)
 S TEST=""
 S LOC=$P($G(^TMP($J,"PXRM DIALOG VISIT INFO","LOCATION")),U)
 I +LOC<1 Q
 S LOC="L:"_LOC
 I $G(IMMIEN)="" Q
 D GETLOT^PXAPIIM(.PXRMARR,IMMIEN,DTE,LOC)
 S CNT=0 F  S CNT=$O(PXRMARR(CNT)) Q:CNT'>0  D
 .S NODE=$G(PXRMARR(CNT,0))
 .S NFOUND=NFOUND+1,TEST(NFOUND)=1,DATE(NFOUND)=DTE
 .S DATA(NFOUND,"LOT NUMBER")=$P(NODE,U,2)
 .S DATA(NFOUND,"MANUFACTURER")=$P(NODE,U,3)
 .S DATA(NFOUND,"EXPIRATION DATE")=$P(NODE,U,4)
 .S DATA(NFOUND,"DOSES UNUSED")=$P(NODE,U,5)
 .S DATA(NFOUND,"LOW SUPPLY ALERT")=$P(NODE,U,6)
 .S DATA(NFOUND,"NDC CODE")=$P(NODE,U,7)
 .S TCNT=1
 .S TEXT(NFOUND,TCNT)="Immunization: "_IMMNAME
 .S TCNT=TCNT+1,TEXT(NFOUND,TCNT)="Lot: "_$P(NODE,U,2)
 .S TCNT=TCNT+1,TEXT(NFOUND,TCNT)="Manufacturer: "_$P(NODE,U,3)_"  Expiration Date: "_$TR($$FMTE^XLFDT($P(NODE,U,4),"2ZM"),"@"," ")
 .S TCNT=TCNT+1,TEXT(NFOUND,TCNT)="Doses unused: "_$P(NODE,U,5)_"  NDC Code: "_$P(NODE,U,7)
 Q
