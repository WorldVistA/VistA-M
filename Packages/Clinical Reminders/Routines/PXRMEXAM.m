PXRMEXAM ;SLC/PKR - Handle examination findings. ;04/15/2022
 ;;2.0;CLINICAL REMINDERS;**42,65**;Feb 04, 2005;Build 438
 ;
 ;=============================================================
EVALFI(DFN,DEFARR,ENODE,FIEVAL) ;Evaluate examination findings.
 D EVALFI^PXRMINDX(DFN,.DEFARR,ENODE,.FIEVAL)
 Q
 ;
 ;=============================================================
EVALPL(FINDPA,ENODE,TERMARR,PLIST) ;Evaluate examination term findings
 ;for patient lists.
 D EVALPL^PXRMINDL(.FINDPA,ENODE,.TERMARR,PLIST)
 Q
 ;
 ;=============================================================
EVALTERM(DFN,FINDPA,ENODE,TERMARR,TFIEVAL) ;Evaluate examination terms.
 D EVALTERM^PXRMINDX(DFN,.FINDPA,ENODE,.TERMARR,.TFIEVAL)
 Q
 ;
 ;=============================================================
GETDATA(DAS,FIEVT) ;Return data, for a specified V Exam entry.
 ;DBIA #4250.
 D VXAM^PXPXRM(DAS,.FIEVT)
 Q
 ;
 ;=============================================================
MHVOUT(INDENT,IFIEVAL,NLINES,TEXT) ;Produce the MHV output.
 N CAPTION,EM,EXAM220,FIEN,IND,JND,MAGNITUDE,NAME,NOUT,PNAME,RESULT,TEMP,TEXTOUT
 N UCUMIEN,UCUMDISPLAY,UCUMFIELD,UNITS,VDATE
 S FIEN=$P(IFIEVAL("FINDING"),";",1)
 S PNAME=$P($G(^AUTTEXAM(FIEN,200)),U,1)
 I PNAME="" S PNAME=$P(^AUTTEXAM(FIEN,0),U,1)
 S NAME=$$INSCHR^PXRMEXLC(INDENT," ")_"Exam: "_PNAME_" = "
 S EXAM220=$G(^AUTTEXAM(FIEN,220))
 S UCUMIEN=$P(EXAM220,U,4)
 I UCUMIEN'="" D
 . S UCUMDISPLAY=$P(EXAM220,U,6)
 . I UCUMDISPLAY="N" S UNITS="" Q
 . S UCUMFIELD=$S(UCUMDISPLAY="C":"UCUM CODE",1:"DESCRIPTION")
 . S UNITS=$$UCUMFIELDS^PXRMUCUM(UCUMIEN,UCUMFIELD)
 E  S UNITS=""
 S IND=0
 F  S IND=+$O(IFIEVAL(IND)) Q:IND=0  D
 . S RESULT=$G(IFIEVAL(IND,"RESULT"))
 . I RESULT'="" S RESULT=$$EXTERNAL^DILFD(9000010.13,.04,"",RESULT,.EM)
 . S VDATE=IFIEVAL(IND,"DATE")
 . S TEMP=NAME_RESULT_" ("_$$EDATE^PXRMDATE(VDATE)_")"
 . D FORMATS^PXRMTEXT(INDENT+2,PXRMRM,TEMP,.NOUT,.TEXTOUT)
 . F JND=1:1:NOUT S NLINES=NLINES+1,TEXT(NLINES)=TEXTOUT(JND)
 . I IFIEVAL(IND,"MEASUREMENT")'="" D
 .. S MAGNITUDE=$P(IFIEVAL(IND,"MEASUREMENT"),U,1)
 .. I MAGNITUDE="" Q
 .. S NLINES=NLINES+1
 .. S CAPTION=$S(UNITS="":"Magnitude: ",1:"Measurement: ")
 .. S TEXT(NLINES)=$$INSCHR^PXRMEXLC(INDENT+1," ")_CAPTION_MAGNITUDE
 .. I UNITS'="" S TEXT(NLINES)=TEXT(NLINES)_" "_UNITS
 S NLINES=NLINES+1,TEXT(NLINES)=""
 Q
 ;
 ;=============================================================
OUTPUT(INDENT,IFIEVAL,NLINES,TEXT) ;Produce the clinical
 ;maintenance output.
 N CAPTION,EM,EXAM220,FIEN,IND,JND,MAGNITUDE,NOUT,PNAME,RESULT,TEMP,TEXTOUT
 N UCUMIEN,UCUMDISPLAY,UCUMFIELD,UNITS,VDATE
 S FIEN=$P(IFIEVAL("FINDING"),";",1)
 S PNAME=$P($G(^AUTTEXAM(FIEN,200)),U,1)
 I PNAME="" S PNAME=$P(^AUTTEXAM(FIEN,0),U,1)
 S EXAM220=$G(^AUTTEXAM(FIEN,220))
 S UCUMIEN=$P(EXAM220,U,4)
 I UCUMIEN'="" D
 . S UCUMDISPLAY=$P(EXAM220,U,6)
 . I UCUMDISPLAY="N" S UNITS="" Q
 . S UCUMFIELD=$S(UCUMDISPLAY="C":"UCUM CODE",1:"DESCRIPTION")
 . S UNITS=$$UCUMFIELDS^PXRMUCUM(UCUMIEN,UCUMFIELD)
 E  S UNITS=""
 I INDENT+6+$L(PNAME)<81 D
 . S NLINES=NLINES+1
 . S TEXT(NLINES)=$$INSCHR^PXRMEXLC(INDENT," ")_"Exam: "_PNAME
 E  D
 . N COL1W,COL2W,FMTSTR
 . S TEMP="Exam:^"_PNAME
 . S COL1W=INDENT+5,COL2W=80-COL1W
 . S FMTSTR=COL1W_"R1^"_COL2W_"L"
 . D COLFMT^PXRMTEXT(FMTSTR,TEMP," ",.NOUT,.TEXTOUT)
 . F JND=1:1:NOUT S NLINES=NLINES+1,TEXT(NLINES)=TEXTOUT(JND)
 S IND=0
 F  S IND=+$O(IFIEVAL(IND)) Q:IND=0  D
 . S VDATE=IFIEVAL(IND,"DATE")
 . S TEMP=$$EDATE^PXRMDATE(VDATE)
 . S RESULT=$G(IFIEVAL(IND,"RESULT"))
 . I RESULT'="" D
 .. S TEMP=TEMP_" result - "
 .. S TEMP=TEMP_$$EXTERNAL^DILFD(9000010.13,.04,"",RESULT,.EM)
 . D FORMATS^PXRMTEXT(INDENT+2,PXRMRM,TEMP,.NOUT,.TEXTOUT)
 . F JND=1:1:NOUT S NLINES=NLINES+1,TEXT(NLINES)=TEXTOUT(JND)
 . I IFIEVAL(IND,"MEASUREMENT")'="" D
 .. S MAGNITUDE=$P(IFIEVAL(IND,"MEASUREMENT"),U,1)
 .. I MAGNITUDE="" Q
 .. S NLINES=NLINES+1
 .. S CAPTION=$S(UNITS="":"Magnitude: ",1:"Measurement: ")
 .. S TEXT(NLINES)=$$INSCHR^PXRMEXLC(INDENT+1," ")_CAPTION_MAGNITUDE
 .. I UNITS'="" S TEXT(NLINES)=TEXT(NLINES)_" "_UNITS
 . I IFIEVAL(IND,"COMMENTS")'="" D
 .. S TEMP="Comments: "_IFIEVAL(IND,"COMMENTS")
 .. D FORMATS^PXRMTEXT(INDENT+3,PXRMRM,TEMP,.NOUT,.TEXTOUT)
 .. F JND=1:1:NOUT S NLINES=NLINES+1,TEXT(NLINES)=TEXTOUT(JND)
 S NLINES=NLINES+1,TEXT(NLINES)=""
 Q
 ;
