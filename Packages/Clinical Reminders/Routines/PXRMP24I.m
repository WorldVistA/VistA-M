PXRMP24I ;SLC/PKR - Inits for PXRM*2.0*24. ;10/12/2012
 ;;2.0;CLINICAL REMINDERS;**24**;Feb 04, 2005;Build 193
 Q
 ;==========================================
CFINC(Y) ;List of computed findings to include in the build.
 N CFLIST,CFNAME,INC
 S CFLIST("VA-ADMISSIONS FOR A DATE RANGE")=""
 S CFLIST("VA-APPOINTMENTS FOR A PATIENT")=""
 S CFLIST("VA-ASU USER CLASS")=""
 S CFLIST("VA-EMPLOYEE")=""
 S CFLIST("VA-PATIENT RECORD FLAG LIST")=""
 S CFLIST("VA-PATIENTS WITH APPOINTMENTS")=""
 S CFLIST("VA-PCMM MHTC")=""
 ;S CFLIST("")=""
 S CFNAME=$P($G(^PXRMD(811.4,Y,0)),U)
 S INC=$S($D(CFLIST(CFNAME)):1,1:0)
 I INC D RMEHIST^PXRMUTIL(811.4,Y)
 Q INC
 ;
 ;==========================================
MPBPRE ;Pre-init for mult-package build.
 S XPDNOQUE=1
 Q
 ;
 ;==========================================
PRE ;Pre-init
 ;Disable options and protocols
 D OPTION^PXRMUTIL("DISABLE")
 D PROTOCOL^PXRMUTIL("DISABLE")
 D DELEXE^PXRMEXSI("EXARRAY","PXRMP24E")
 D REEXCH^PXRMP24I
 D RENAME^PXRMUTIL(811.5,"VA-MH APPTS FOR MHTC ASSIGNMENT","VA-MH KEPT APPTS FOR MHTC ASSIGNMENT")
 Q
 ;
 ;==========================================
POST ;Post-init
 ;Enable options and protocols
 D OPTION^PXRMUTIL("ENABLE")
 D PROTOCOL^PXRMUTIL("ENABLE")
 D SETPVER^PXRMUTIL("2.0P24")
 ;Install Exchange File entries.
 D SMEXINS^PXRMEXSI("EXARRAY","PXRMP24E")
 D RHTF^PXRMP24I
 D SENDIM^PXRMP24I
 Q
 ;
 ;==========================================
REEXCH ;Rename reminder definition VA-MHTC CANDIDATE and remove the old
 ;Exchange entry
 N IEN,LIST,LUVALUE,PXRMINST
 S PXRMINST=1
 D RENAME^PXRMUTIL(811.9,"VA-MHTC CANDIDATE","VA-MHTC NEEDS ASSIGNMENT")
 S LUVALUE(1)="VA-MHTC CANDIDATE"
 S LUVALUE(2)="05/23/2012@09:42:32"
 S IEN=$$FIND1^DIC(811.8,"","KU",.LUVALUE)
 S LIST(IEN)=""
 D DELETE^PXRMEXU1(.LIST)
 Q
 ;
 ;==========================================
RHTF ;Rename and inactivate health factors and terms that are no longer used.
 N IEN,NAME
 D BMES^XPDUTL("Renaming/inactivating health factors and terms that are no longer used.")
 D RENAME^PXRMUTIL(9999999.64,"MH NOSHOW INITIATE WELLNESS CHECK","ZZMH NOSHOW INITIATE WELLNESS CHECK")
 D RENAME^PXRMUTIL(9999999.64,"MH NOSHOW SUPPORT CONTACT","ZZMH NOSHOW SUPPORT CONTACT")
 D RENAME^PXRMUTIL(9999999.64,"MH NOSHOW UNABLE TO REACH PT","ZZMH NOSHOW UNABLE TO REACH PT")
 F NAME="ZZMH NOSHOW INITIATE WELLNESS CHECK","ZZMH NOSHOW SUPPORT CONTACT","ZZMH NOSHOW UNABLE TO REACH PT" D
 . S IEN=+$$FIND1^DIC(9999999.64,"","BX",NAME)
 . I IEN>0 S $P(^AUTTHF(IEN,0),U,11)=1
 D RENAME^PXRMUTIL(811.5,"VA-MH NOSHOW INITIATE WELLNESS CHECK","ZZVA-MH NOSHOW INITIATE WELLNESS CHECK")
 D RENAME^PXRMUTIL(811.5,"VA-MH NOSHOW SUPPORT CONTACT","ZZVA-MH NOSHOW SUPPORT CONTACT")
 D RENAME^PXRMUTIL(811.5,"VA-MH NOSHOW UNABLE TO REACH PT","ZZVA-MH NOSHOW UNABLE TO REACH PT")
 Q
 ;
 ;==========================================
SENDIM ;Send install message.
 N FROM,NODE,PARAM,SYSTEM,SUBJECT,TO,VALUE
 S NODE="PXRM*2.0*24"
 K ^TMP(NODE,$J)
 ;DBIA #1131 for ^XMB("NETNAME")
 S FROM="PXRM*2.0*24 Install@"_^XMB("NETNAME")
 ;DBIA #2541
 S SYSTEM=$$KSP^XUPARAM("WHERE")
 I $$PROD^XUPROD(1) S TO("G.CLINICAL REMINDERS SUPPORT@DOMAIN.EXT")=""
 E  D
 . N MGIEN,MGROUP
 . S MGIEN=$G(^PXRM(800,1,"MGFE"))
 . S MGROUP=$S(MGIEN'="":"G."_$$GET1^DIQ(3.8,MGIEN,.01),1:DUZ)
 . S TO(MGROUP)=""
 S SUBJECT="Install of PXRM*2.0*24"
 S ^TMP(NODE,$J,1,0)="PXRM*2.0*24 has been installed."
 S ^TMP(NODE,$J,2,0)="System is "_SYSTEM
 D SEND^PXRMMSG(NODE,SUBJECT,.TO,FROM)
 Q
 ;
