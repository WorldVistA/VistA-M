EC725U60 ;ALB/BP - EC National Procedure Update ; 1/19/10 2:49pm
 ;;2.0; EVENT CAPTURE ;**111**;8 May 96;Build 12
 ;
 ;this routine is used as a post-init in a KIDS build 
 ;to modify the EC National Procedure file #725
 ;
 Q
ADDPROC ;* add national procedures
 ;
 ;  ECXX is in format:
 ;   NAME^NATIONAL NUMBER^CPT CODE^FIRST NATIONAL NUMBER SEQUENCE
 ;   LAST NATIONAL NUMBER SEQUENCE
 ;
 N ECX,ECXX,ECDINUM,NAME,CODE,CPT,COUNT,X,Y,DIC,DIE,DA,DR,DLAYGO,DINUM
 N ECADD,ECBEG,ECEND,CODX,NAMX,ECSEQ,LIEN,STR,CPTN,STR
 D MES^XPDUTL(" ")
 D BMES^XPDUTL("Adding new procedures to EC NATIONAL PROCEDURE File (#725)...")
 D MES^XPDUTL(" ")
 S ECDINUM=$O(^EC(725,9999),-1),COUNT=$P(^EC(725,0),U,4)
 F ECX=1:1 S ECXX=$P($T(NEW+ECX),";;",2) Q:ECXX="QUIT"  D
 .S NAME=$P(ECXX,U,1),CODE=$P(ECXX,U,2),CPTN=$P(ECXX,U,3),CODX=CODE
 .S CPT=""
 .I CPTN'="" S CPT=$$FIND1^DIC(81,"","X",CPTN) I +CPT<1 D  Q
 ..S STR="   CPT code "_CPTN_" not a valid code in CPT File."
 ..D MES^XPDUTL(" ")
 ..D BMES^XPDUTL("   ["_CODE_"] "_STR)
 .S ECBEG=$P(ECXX,U,4),ECEND=$P(ECXX,U,5),NAMX=NAME
 .I ECBEG="" S X=NAME D FILPROC Q
 .F ECSEQ=ECBEG:1:ECEND D
 ..S ECADD="000"_ECSEQ,ECADD=$E(ECADD,$L(ECADD)-2,$L(ECADD))
 ..;S NAME=NAMX_ECADD,X=NAME,CODE=CODX_ECADD
 ..I $E(CODX,1,3)'="RCM" S NAME=NAMX_ECSEQ,X=NAME,CODE=CODX_ECADD
 ..E  S NAME=NAMX_$E(ECADD,2,99),X=NAME,CODE=CODX_$E(ECADD,2,99)
 ..D FILPROC
 S $P(^EC(725,0),U,4)=COUNT,X=$O(^EC(725,999999),-1),$P(^EC(725,0),U,3)=X
 Q
 ;
FILPROC ;File national procedures
 I '$D(^EC(725,"D",CODE)) D
 .S ECDINUM=ECDINUM+1,DINUM=ECDINUM,DIC(0)="L",DLAYGO=725,DIC="^EC(725,"
 .S DIC("DR")="1///^S X=CODE;4///^S X=CPT"
 .D FILE^DICN
 .I +Y>0 D
 ..S COUNT=COUNT+1
 ..D MES^XPDUTL(" ")
 ..S STR="   Entry #"_+Y_" for "_$P(Y,U,2)
 ..S STR=STR_$S(CPT'="":" [CPT: "_CPT_"]",1:"")_" ("_CODE_")"
 ..D BMES^XPDUTL(STR_"  ...successfully added.")
 .I Y=-1 D
 ..D MES^XPDUTL(" ")
 ..D BMES^XPDUTL("ERROR when attempting to add "_NAME_" ("_CODE_")")
 I $D(^EC(725,"DL",CODE)) D
 .S LIEN=$O(^EC(725,"DL",CODE,""))
 .D MES^XPDUTL(" ")
 .D BMES^XPDUTL("   Your site has a local procedure (entry #"_LIEN_") in File #725")
 .D BMES^XPDUTL("   which uses "_CODE_" as its National Number.")
 .D BMES^XPDUTL("   Please inactivate this local procedure.")
 .K Y
 Q
NEW ;national procedures to add;;descript^nation #^CPT code^beg seq^end seq
 ;;HH DRIVE TIME (15MIN) PSO^HH144
 ;;HH DRIVE TIME (15MIN) KT^HH145
 ;;HH DRIVE TIME (15MIN) SPCH PTH^HH146
 ;;INPT NUTR ED,IND,Q15M^NU187
 ;;STATUS - UNCLASSIFIED^NU188
 ;;DRIVE TIME-CBOC Q5M^NU189
 ;;CCHT INIT. SCREENING^NU190^99499
 ;;CCHT TECH ED/INSTALL^NU191^97762
 ;;CCHT ASSESS TX PLAN^NU192^99211
 ;;CCHT PHONE TX PLAN^NU193^98968
 ;;MOVE QUEST.W/PT1ST30^NU194^99499
 ;;MOV QUEST.W/PT QADL5^NU195
 ;;MV HOTLINE RVW&FU,Q5^NU196
 ;;MV QUEST.W/O PT Q10^NU197
 ;;MV QUEST.W/IP Q10M^NU198
 ;;COOK PREP/CLN2-5PT Q15^NU199
 ;;COOK PREP/CLN6-10PT Q15^NU200
 ;;COOK PREP/CLN11-20PT Q15^NU201
 ;;COOK PREP/CLN>20PT Q15^NU202
 ;;SECURE MSG CORRESPOND^NU203^98969
 ;;CCHT PHONE INIT SCREEN^NU204^98968
 ;;SENSORY INTEGRATION IND^RC099^97533
 ;;WHEELCHAIR ASSESSMENT^RC100^97542
 ;;QUIT
NAMECHG ;* change national procedure names
 ;
 ;  ECXX is in format:
 ;   NATIONAL NUMBER^NEW NAME
 ;
 N ECX,ECXX,ECDA,DA,DR,DIC,DIE,X,Y,STR
 D MES^XPDUTL(" ")
 D BMES^XPDUTL("Changing names in EC NATIONAL PROCEDURE File (#725)...")
 D MES^XPDUTL(" ")
 F ECX=1:1 S ECXX=$P($T(CHNG+ECX),";;",2) Q:ECXX="QUIT"  D
 .I $D(^EC(725,"D",$P(ECXX,U,1))) D
 ..S ECDA=+$O(^EC(725,"D",$P(ECXX,U,1),0))
 ..I $D(^EC(725,ECDA,0)) D
 ...S DA=ECDA,DR=".01////^S X=$P(ECXX,U,2)",DIE="^EC(725," D ^DIE
 ...D MES^XPDUTL(" ")
 ...D MES^XPDUTL("   Entry #"_ECDA_" for "_$P(ECXX,U,1))
 ...D BMES^XPDUTL("      ... field (#.01) updated to  "_$P(ECXX,U,2)_".")
 .I '$D(^EC(725,"D",$P(ECXX,U,1))) D
 ..D MES^XPDUTL(" ")
 ..S STR="Can't find entry for "_$P(ECXX,U,1)
 ..D BMES^XPDUTL(STR_" ...field (#.01) not updated.")
 Q
 ;
CHNG ;name changes -national code #^new procedure name
 ;;NU007^IN/CLC NUT CARE-BASIC,10M
 ;;NU008^IN/CLC NUT CARE-INTRM,10M
 ;;NU009^IN/CLC NUT CARE-CMPLX,10M
 ;;NU060^DSMT ACCR GP,2-5PT,30M
 ;;NU061^DSMT ACCR GP,6-10PT,30M
 ;;NU062^DSMT ACCR GP,11-20PT,30M
 ;;NU063^DSMT ACCR GP,>20PT,30M
 ;;NU077^NUTR GP, 2-5 PT 1ST 30M
 ;;NU078^NUTR GP, 6-10 PT 1ST 30M
 ;;NU079^NUTR GP, 11-20 PT 1ST 30M
 ;;NU080^NUTR GP, >20 PT 1ST 30M
 ;;NU156^HBPC NUTR DRIVE TIME,EA 15M
 ;;NU162^MNT F/U EA 15M
 ;;NU163^MNT SUBSEQ EA 15M
 ;;NU164^NUT CNSG IND,1ST15M
 ;;NU165^CASE MGT,W/PT EA15M
 ;;NU166^NUT SCREENING 10M
 ;;NU167^OTHER OPT VISIT
 ;;NU168^PT EDUC 1ST 15M
 ;;NU169^INSLN PMP ED 1ST15M
 ;;NU170^GLUC FINGER STICK
 ;;NU171^PHONE 5-10 MIN
 ;;NU172^PHONE 11-20 MIN
 ;;NU173^PHONE 21-30 MIN
 ;;NU174^DSMT ACCRED IND Q30M
 ;;NU175^DIABMGT NONACRD1ST15
 ;;NU176^DIAB MGT F/U 1ST15M
 ;;NU177^CGM-CONT.GLUC MNTR
 ;;NU178^SELF-MGT ED IND,EA 30M
 ;;NU179^SELF MGT GP2-4,EA 30M
 ;;NU180^SELF MGT GP5-8,EA 30M
 ;;NU181^CCHT MONTHLY MONITOR
 ;;NU182^MNT INIT EA 15M
 ;;NU185^HBPC-NUTR INPT 1ST 10M
 ;;NU186^HBPC-NUTR IP QADL10M
 ;;RC001^REFER/CONS/SCREEN
 ;;RC002^RECORD REVIEW
 ;;RC003^ASMNT INIT 30M
 ;;RC004^ASMNT UPREVDISC 30M
 ;;RC005^ASMNT PROG NOTE
 ;;RC006^ASMNT PROG NOTE 15M
 ;;RC007^DISCH/COMM REF 15M
 ;;RC008^DISCH/COMM REF 30M
 ;;RC010^TEAMEETCAREPLAN 30M
 ;;RC014^REC CREATARTGRP 2-4
 ;;RC015^REC CREATARTGRP 5-20
 ;;RC016^REC CREATARTGRP >20
 ;;RC053^MUSTHER SOC IND 15M
 ;;RC054^MUSTHER COG IND 15M
 ;;QUIT
