IB20P708 ;ALB/CXW - IB*2.0*708 COST BASED & INTER AGENCY RATE UPDATE ; 07/14/2021@13:38
 ;;2.0;INTEGRATED BILLING;**708**;21-MAR-94;Build 23
 ;;Per VA Directive 6402, this routine should not be modified.
 ; 
 ;
 ; Add FY22 Cost Based/Interagency Charges to Charge Master 363.2
 Q
POST ;
 N IBEFFDT,IBA,U S U="^"
 D MSG("    IB*2.0*708 Post-Install .....")
 S IBEFFDT=3211001 ; effective date of 10/01/2021
 D ADDCI(IBEFFDT)  ; add Charge Items (363.2) with new rates
 D MSG("    IB*2.0*708 Post-Install Complete")
 Q
 ;
ADDCI(IBEFFDT) ; pass in the effective date to add charge items with new charges
 N IBCNT,IBCNT1,IBDFLTDT,IBI,IBLN
 D MSG("")
 S (IBCNT,IBCNT1)=0,IBDFLTDT=+$G(IBEFFDT)
 I 'IBDFLTDT D MSG("** Error: No Effective Date, No Charges Added") G CIQ
 ;
 F IBI=1:1 S IBLN=$P($T(CIF+IBI),";;",2) Q:+IBLN!(IBLN="")  I $E(IBLN)?1A D SETCI
 ;
 I +IBCNT1 D MSG("    >> "_IBCNT1_" Duplicate Charge Items already exist, not re-added")
 ;
CIQ D MSG("    >> "_IBCNT_" Cost Based/Interagency Charge Items added to CHARGE ITEM file (#363.2)")
 D MSG("")
 Q
 ;
SETCI ; set Charge Item (duplicates based on item, CS, eff dt, rev cd)
 ;
 N IBCHG,IBCI,IBCS,IBDT,IBFN,IBPE,IBRVCD,IBX,IBXRF,IBZ,DA,DD,DO,DLAYGO,DIC,DIE,DR,X,Y
 S IBCS=$P(IBLN,U,2),IBCS=+$O(^IBE(363.1,"B",IBCS,0)) I 'IBCS D MSG("** Error: Charge Set "_$P(IBLN,U,2)_" undefined") Q
 S IBCI=+$$MCCRUTL($P(IBLN,U,1),5) I 'IBCI D MSG("** Error: Bed Section "_$P(IBLN,U,1)_" undefined") Q
 S IBDT=IBDFLTDT I +$P(IBLN,U,3) S IBDT=+$P(IBLN,U,3)
 S IBRVCD=$$RVCD($P(IBLN,U,4))
 S IBCHG=+$P(IBLN,U,5)
 S IBXRF="AIVDTS"_IBCS
 ;
 S IBX=0 F  S IBX=$O(^IBA(363.2,IBXRF,IBCI,-IBDT,IBX)) Q:'IBX  S IBZ=$G(^IBA(363.2,IBX,0)) I $P(IBZ,U,6)=IBRVCD D
 . S IBCI=0,IBCNT1=IBCNT1+1 I +$P(IBZ,U,5)'=IBCHG D MSG("** Error: Item exists, wrong charge: "_IBLN)
 Q:'IBCI
 ;
 S DLAYGO=363.2,DIC="^IBA(363.2,",DIC(0)="L",X=IBCI_";DGCR(399.1," D FILE^DICN
 I Y<1 D MSG("** Error: when adding the charge item "_$P(IBLN,U,2)_" with rate "_IBCHG_" to the file, Log a ticket!") Q
 S IBFN=+Y,IBCNT=IBCNT+1
 ;
 S DR=".02///"_IBCS_";.03///"_IBDT_";.05///"_IBCHG I +IBRVCD S DR=DR_";.06///"_IBRVCD
 S DIE="^IBA(363.2,",DA=+IBFN D ^DIE
 Q
 ;
 ;
MCCRUTL(IBC,IBPE) ; returns IEN in 399.1 if Name is found and piece P is true
 N IBX,IBY S IBY=""
 I $G(IBC)'="" S IBX=0 F  S IBX=$O(^DGCR(399.1,"B",IBC,IBX)) Q:'IBX  I $P($G(^DGCR(399.1,IBX,0)),U,+$G(IBPE)) S IBY=IBX
 Q IBY
 ;
RVCD(RVCD) ; returns IFN if revenue code is valid and active
 N IBX,IBY S IBY=""
 I +$G(RVCD) S IBX=$G(^DGCR(399.2,+RVCD,0)) I +$P(IBX,U,3) S IBY=+RVCD
 Q IBY
 ;
MSG(IBA) ;
 D MES^XPDUTL(IBA)
 Q
 ;
CIF ; 68 Charge Items (363.2): Bedsection ^ Charge Set ^Effective Date ^ Revenue Code ^ Charge
 ;;     
TORT ;; Cost Based (Tortiously Liable) - All Inclusive
 ;;     
 ;;ALCOHOL AND DRUG TREATMENT^TL-INPT (INCLUSIVE)^^^2744
 ;;BLIND REHABILITATION^TL-INPT (INCLUSIVE)^^^2933
 ;;GENERAL MEDICAL CARE^TL-INPT (INCLUSIVE)^^^5466
 ;;INTERMEDIATE CARE^TL-INPT (INCLUSIVE)^^^3269
 ;;NEUROLOGY^TL-INPT (INCLUSIVE)^^^5127
 ;;NURSING HOME CARE^TL-INPT (INCLUSIVE)^^^1750
 ;;POLYTRAUMA INPATIENT^TL-INPT (INCLUSIVE)^^^3936
 ;;PRRTP^TL-INPT (INCLUSIVE)^^^358
 ;;PSYCHIATRIC CARE^TL-INPT (INCLUSIVE)^^^2875
 ;;REHABILITATION MEDICINE^TL-INPT (INCLUSIVE)^^^3963
 ;;SPINAL CORD INJURY CARE^TL-INPT (INCLUSIVE)^^^3548
 ;;SURGICAL CARE^TL-INPT (INCLUSIVE)^^^9756
 ;;     
 ;; Non-Professional: Nursing/Room/Board 101 & Ancillary 240
 ;;ALCOHOL AND DRUG TREATMENT^TL-INPT (NPF)^^101^1847
 ;;ALCOHOL AND DRUG TREATMENT^TL-INPT (NPF)^^240^635
 ;;BLIND REHABILITATION^TL-INPT (NPF)^^101^1240
 ;;BLIND REHABILITATION^TL-INPT (NPF)^^240^1457
 ;;GENERAL MEDICAL CARE^TL-INPT (NPF)^^101^3388
 ;;GENERAL MEDICAL CARE^TL-INPT (NPF)^^240^1424
 ;;INTERMEDIATE CARE^TL-INPT (NPF)^^101^2628
 ;;INTERMEDIATE CARE^TL-INPT (NPF)^^240^480
 ;;NEUROLOGY^TL-INPT (NPF)^^101^3022
 ;;NEUROLOGY^TL-INPT (NPF)^^240^1354
 ;;NURSING HOME CARE^TL-INPT (NPF)^^101^1459
 ;;NURSING HOME CARE^TL-INPT (NPF)^^240^237
 ;;POLYTRAUMA INPATIENT^TL-INPT (NPF)^^101^2287
 ;;POLYTRAUMA INPATIENT^TL-INPT (NPF)^^240^1202
 ;;PRRTP^TL-INPT (NPF)^^101^297
 ;;PRRTP^TL-INPT (NPF)^^240^38
 ;;PSYCHIATRIC CARE^TL-INPT (NPF)^^101^2151
 ;;PSYCHIATRIC CARE^TL-INPT (NPF)^^240^453
 ;;REHABILITATION MEDICINE^TL-INPT (NPF)^^101^2302
 ;;REHABILITATION MEDICINE^TL-INPT (NPF)^^240^1211
 ;;SPINAL CORD INJURY CARE^TL-INPT (NPF)^^101^2215
 ;;SPINAL CORD INJURY CARE^TL-INPT (NPF)^^240^893
 ;;SURGICAL CARE^TL-INPT (NPF)^^101^5722
 ;;SURGICAL CARE^TL-INPT (NPF)^^240^2959
 ;;     
 ;; Professional Physician
 ;;ALCOHOL AND DRUG TREATMENT^TL-INPT (PF)^^^262
 ;;BLIND REHABILITATION^TL-INPT (PF)^^^236
 ;;GENERAL MEDICAL CARE^TL-INPT (PF)^^^654
 ;;INTERMEDIATE CARE^TL-INPT (PF)^^^161
 ;;NEUROLOGY^TL-INPT (PF)^^^751
 ;;NURSING HOME CARE^TL-INPT (PF)^^^54
 ;;POLYTRAUMA INPATIENT^TL-INPT (PF)^^^447
 ;;PRRTP^TL-INPT (PF)^^^23
 ;;PSYCHIATRIC CARE^TL-INPT (PF)^^^271
 ;;REHABILITATION MEDICINE^TL-INPT (PF)^^^450
 ;;SPINAL CORD INJURY CARE^TL-INPT (PF)^^^440
 ;;SURGICAL CARE^TL-INPT (PF)^^^1075
 ;;                    
 ;; Outpatient Care Other
 ;;OUTPATIENT DENTAL^TL-OPT DENTAL^^^506
 ;;OUTPATIENT VISIT^TL-OPT VST^^^506
 ;;POLYTRAUMA OUTPATIENT VISIT^TL-OPT VST POLYTRAUMA^^^678
 ;;PM&RS OUTPATIENT VISIT^TL-OPT VST PM&RS^^^347
 ;;          
IA ;; Interagency
 ;;            
 ;;ALCOHOL AND DRUG TREATMENT^IA-INPT^^^2663
 ;;BLIND REHABILITATION^IA-INPT^^^2843
 ;;GENERAL MEDICAL CARE^IA-INPT^^^5318
 ;;INTERMEDIATE CARE^IA-INPT^^^3179
 ;;NEUROLOGY^IA-INPT^^^4981
 ;;NURSING HOME CARE^IA-INPT^^^1698
 ;;POLYTRAUMA INPATIENT^IA-INPT^^^3805
 ;;PRRTP^IA-INPT^^^348
 ;;PSYCHIATRIC CARE^IA-INPT^^^2788
 ;;REHABILITATION MEDICINE^IA-INPT^^^3846
 ;;SPINAL CORD INJURY CARE^IA-INPT^^^3443
 ;;SURGICAL CARE^IA-INPT^^^9490
 ;;     
 ;; Outpatient Care Other
 ;;OUTPATIENT DENTAL^IA-OPT DENTAL^^^493
 ;;OUTPATIENT VISIT^IA-OPT VST^^^493
 ;;POLYTRAUMA OUTPATIENT VISIT^IA-OPT VST POLYTRAUMA^^^660
 ;;PM&RS OUTPATIENT VISIT^IA-OPT VST PM&RS^^^336
 ;;
 Q
