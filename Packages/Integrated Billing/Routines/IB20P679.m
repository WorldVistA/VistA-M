IB20P679 ;ALB/JTW - IB*2.0*679 COST BASED & INTER AGENCY RATE UPDATE; 06/25/20
 ;;2.0;INTEGRATED BILLING;**679**;21-MAR-94;Build 2
 ;Per VA Directive 6402, this routine should not be modified.
 ; 
 ;
 ; Add FY20 Cost Based/Interagency Charges to Charge Master 363.2
 Q
POST ;
 N IBEFFDT,IBA,U S U="^"
 D MSG("    IB*2.0*679 Post-Install .....")
 S IBEFFDT=3201001 ; effective date of 10/01/2020
 D ADDCI(IBEFFDT)  ; add Charge Items (363.2) with new rates
 D MSG("    IB*2.0*679 Post-Install Complete")
 Q
 ;
ADDCI(IBEFFDT) ; pass in the effective date to add charge items with new charges
 N IBCNT,IBCNT1,IBDFLTDT,IBI,IBLN
 D MSG("")
 S (IBCNT,IBCNT1)=0,IBDFLTDT=+$G(IBEFFDT)
 I 'IBDFLTDT D MSG("** Error: No Effective Date, No Charges Added") G CIQ
 ;
 F IBI=1:1 S IBLN=$P($T(CIF+IBI),";;",2) Q:+IBLN!(IBLN="")  I $E(IBLN)?1A D SETCI
 ;
 I +IBCNT1 D MSG("    >> "_IBCNT1_" Duplicate Charge Items already exist, not re-added")
 ;
CIQ D MSG("    >> "_IBCNT_" Cost Based/Interagency Charge Items added to CHARGE ITEM file (#363.2)")
 D MSG("")
 Q
 ;
SETCI ; set Charge Item (duplicates based on item, CS, eff dt, rev cd)
 ;
 N IBCHG,IBCI,IBCS,IBDT,IBFN,IBPE,IBRVCD,IBX,IBXRF,IBZ,DA,DD,DO,DLAYGO,DIC,DIE,DR,X,Y
 S IBCS=$P(IBLN,U,2),IBCS=+$O(^IBE(363.1,"B",IBCS,0)) I 'IBCS D MSG("** Error: Charge Set "_$P(IBLN,U,2)_" undefined") Q
 S IBCI=+$$MCCRUTL($P(IBLN,U,1),5) I 'IBCI D MSG("** Error: Bed Section "_$P(IBLN,U,1)_" undefined") Q
 S IBDT=IBDFLTDT I +$P(IBLN,U,3) S IBDT=+$P(IBLN,U,3)
 S IBRVCD=$$RVCD($P(IBLN,U,4))
 S IBCHG=+$P(IBLN,U,5)
 S IBXRF="AIVDTS"_IBCS
 ;
 S IBX=0 F  S IBX=$O(^IBA(363.2,IBXRF,IBCI,-IBDT,IBX)) Q:'IBX  S IBZ=$G(^IBA(363.2,IBX,0)) I $P(IBZ,U,6)=IBRVCD D
 . S IBCI=0,IBCNT1=IBCNT1+1 I +$P(IBZ,U,5)'=IBCHG D MSG("** Error: Item exists, wrong charge: "_IBLN)
 Q:'IBCI
 ;
 S DLAYGO=363.2,DIC="^IBA(363.2,",DIC(0)="L",X=IBCI_";DGCR(399.1," D FILE^DICN
 I Y<1 D MSG("** Error: when adding the charge item "_$P(IBLN,U,2)_" with rate "_IBCHG_" to the file, Log a ticket!") Q
 S IBFN=+Y,IBCNT=IBCNT+1
 ;
 S DR=".02///"_IBCS_";.03///"_IBDT_";.05///"_IBCHG I +IBRVCD S DR=DR_";.06///"_IBRVCD
 S DIE="^IBA(363.2,",DA=+IBFN D ^DIE
 Q
 ;
 ;
MCCRUTL(IBC,IBPE) ; returns IEN in 399.1 if Name is found and piece P is true
 N IBX,IBY S IBY=""
 I $G(IBC)'="" S IBX=0 F  S IBX=$O(^DGCR(399.1,"B",IBC,IBX)) Q:'IBX  I $P($G(^DGCR(399.1,IBX,0)),U,+$G(IBPE)) S IBY=IBX
 Q IBY
 ;
RVCD(RVCD) ; returns IFN if revenue code is valid and active
 N IBX,IBY S IBY=""
 I +$G(RVCD) S IBX=$G(^DGCR(399.2,+RVCD,0)) I +$P(IBX,U,3) S IBY=+RVCD
 Q IBY
 ;
MSG(IBA) ;
 D MES^XPDUTL(IBA)
 Q
 ;
CIF ; 68 Charge Items (363.2): Bedsection ^ Charge Set ^Effective Date ^ Revenue Code ^ Charge
 ;;     
TORT ;; Cost Based (Tortiously Liable) - All Inclusive
 ;;        
 ;;ALCOHOL AND DRUG TREATMENT^TL-INPT (INCLUSIVE)^^^2327
 ;;BLIND REHABILITATION^TL-INPT (INCLUSIVE)^^^2073
 ;;GENERAL MEDICAL CARE^TL-INPT (INCLUSIVE)^^^4626
 ;;INTERMEDIATE CARE^TL-INPT (INCLUSIVE)^^^3029
 ;;NEUROLOGY^TL-INPT (INCLUSIVE)^^^4433
 ;;NURSING HOME CARE^TL-INPT (INCLUSIVE)^^^1504
 ;;POLYTRAUMA INPATIENT^TL-INPT (INCLUSIVE)^^^3303
 ;;PRRTP^TL-INPT (INCLUSIVE)^^^306
 ;;PSYCHIATRIC CARE^TL-INPT (INCLUSIVE)^^^2403
 ;;REHABILITATION MEDICINE^TL-INPT (INCLUSIVE)^^^3090
 ;;SPINAL CORD INJURY CARE^TL-INPT (INCLUSIVE)^^^3032
 ;;SURGICAL CARE^TL-INPT (INCLUSIVE)^^^8205
 ;;             
 ;; Non-Professional: Nursing/Room/Board 101 & Ancillary 240    
 ;;ALCOHOL AND DRUG TREATMENT^TL-INPT (NPF)^^101^1567
 ;;ALCOHOL AND DRUG TREATMENT^TL-INPT (NPF)^^240^538
 ;;BLIND REHABILITATION^TL-INPT (NPF)^^101^876
 ;;BLIND REHABILITATION^TL-INPT (NPF)^^240^1030
 ;;GENERAL MEDICAL CARE^TL-INPT (NPF)^^101^2866
 ;;GENERAL MEDICAL CARE^TL-INPT (NPF)^^240^1206
 ;;INTERMEDIATE CARE^TL-INPT (NPF)^^101^2436
 ;;INTERMEDIATE CARE^TL-INPT (NPF)^^240^444
 ;;NEUROLOGY^TL-INPT (NPF)^^101^2614
 ;;NEUROLOGY^TL-INPT (NPF)^^240^1170
 ;;NURSING HOME CARE^TL-INPT (NPF)^^101^1254
 ;;NURSING HOME CARE^TL-INPT (NPF)^^240^203
 ;;POLYTRAUMA INPATIENT^TL-INPT (NPF)^^101^1919
 ;;POLYTRAUMA INPATIENT^TL-INPT (NPF)^^240^1009
 ;;PRRTP^TL-INPT (NPF)^^101^255
 ;;PRRTP^TL-INPT (NPF)^^240^32
 ;;PSYCHIATRIC CARE^TL-INPT (NPF)^^101^1798
 ;;PSYCHIATRIC CARE^TL-INPT (NPF)^^240^378
 ;;REHABILITATION MEDICINE^TL-INPT (NPF)^^101^1795
 ;;REHABILITATION MEDICINE^TL-INPT (NPF)^^240^944
 ;;SPINAL CORD INJURY CARE^TL-INPT (NPF)^^101^1893
 ;;SPINAL CORD INJURY CARE^TL-INPT (NPF)^^240^763
 ;;SURGICAL CARE^TL-INPT (NPF)^^101^4812
 ;;SURGICAL CARE^TL-INPT (NPF)^^240^2489
 ;;              
 ;; Professional Physician
 ;;ALCOHOL AND DRUG TREATMENT^TL-INPT (PF)^^^222
 ;;BLIND REHABILITATION^TL-INPT (PF)^^^167
 ;;GENERAL MEDICAL CARE^TL-INPT (PF)^^^554
 ;;INTERMEDIATE CARE^TL-INPT (PF)^^^149
 ;;NEUROLOGY^TL-INPT (PF)^^^649
 ;;NURSING HOME CARE^TL-INPT (PF)^^^47
 ;;POLYTRAUMA INPATIENT^TL-INPT (PF)^^^375
 ;;PRRTP^TL-INPT (PF)^^^19
 ;;PSYCHIATRIC CARE^TL-INPT (PF)^^^227
 ;;REHABILITATION MEDICINE^TL-INPT (PF)^^^351
 ;;SPINAL CORD INJURY CARE^TL-INPT (PF)^^^376
 ;;SURGICAL CARE^TL-INPT (PF)^^^904
 ;;                    
 ;; Outpatient Care Other
 ;;OUTPATIENT DENTAL^TL-OPT DENTAL^^^409
 ;;OUTPATIENT VISIT^TL-OPT VST^^^409
 ;;POLYTRAUMA OUTPATIENT VISIT^TL-OPT VST POLYTRAUMA^^^643
 ;;PM&RS OUTPATIENT VISIT^TL-OPT VST PM&RS^^^241
 ;;          
IA ;; Interagency
 ;;            
 ;;ALCOHOL AND DRUG TREATMENT^IA-INPT^^^2240
 ;;BLIND REHABILITATION^IA-INPT^^^1998
 ;;GENERAL MEDICAL CARE^IA-INPT^^^4473
 ;;INTERMEDIATE CARE^IA-INPT^^^2928
 ;;NEUROLOGY^IA-INPT^^^4280
 ;;NURSING HOME CARE^IA-INPT^^^1450
 ;;POLYTRAUMA INPATIENT^IA-INPT^^^3165
 ;;PRRTP^IA-INPT^^^297
 ;;PSYCHIATRIC CARE^IA-INPT^^^2314
 ;;REHABILITATION MEDICINE^IA-INPT^^^2979
 ;;SPINAL CORD INJURY CARE^IA-INPT^^^2924
 ;;SURGICAL CARE^IA-INPT^^^7935
 ;;OUTPATIENT DENTAL^IA-OPT DENTAL^^^396
 ;;OUTPATIENT VISIT^IA-OPT VST^^^396
 ;;POLYTRAUMA OUTPATIENT VISIT^IA-OPT VST POLYTRAUMA^^^622
 ;;PM&RS OUTPATIENT VISIT^IA-OPT VST PM&RS^^^231
 ;;
 Q
