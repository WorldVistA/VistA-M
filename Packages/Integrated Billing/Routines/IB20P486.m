IB20P486 ;ALB/RRA POST INIT ACTION ;24-JUN-2003
 ;;2.0;INTEGRATED BILLING;**486**;21-MAR-94;Build 3
 ;;Per VHA Directive 2004-038, this routine should not be modified.
 ;
 ;
 ;  Post Init Description: Add CAMP LEJEUNE TREATMENT as a new RNB
 ;
 Q
 ;
 ;
EN ;
 D BMES^XPDUTL(">>> UPDATING CLAIMS TRACKING NON-BILLABLE REASONS file #356.8")
 D MES^XPDUTL("  ")
 D BMES^XPDUTL(">>> Adding CAMP LEJEUNE TREATMENT .......")
 D MES^XPDUTL("  ")
 D NEWNBR
 ;
 Q
 ;
NEWNBR ; Add CAMP LEJEUNE TREATMENT IB non-billable reasons #356.8
 N IBMES,IBNAME,IBJ,IBNX,IBEFLAG,IBEPFLAG,IBCODE,DINUM
 S IBNAME="CAMP LEJEUNE TREATMENT"
 I $D(^IBE(356.8,"B",IBNAME)) D BMES^XPDUTL("CAMP LEJEUNE TREATMENT already on file....nothing updated") Q
 I '$D(^IBE(356.8,"B",IBNAME)) D
 . F IBJ=100:1 S IBNX=$G(^IBE(356.8,IBJ,0)) I IBNX="" S DINUM=IBJ Q
 . S IBEFLAG=1,IBEPFLAG=0,IBCODE="VA11"
 . S DLAYGO=356.8,DIC="^IBE(356.8,",DIC(0)="L",X=IBNAME
 . S DIC("DR")=".01///^S X=IBNAME;.02///^S X=IBEFLAG;.03///^S X=IBEPFLAG;.04///^S X=IBCODE"
 . D FILE^DICN
 .I +Y>0 D
 ..D MES^XPDUTL(" ")
 ..D BMES^XPDUTL("CAMP LEJEUNE TREATMENT.....successfully added.")
 .I Y=-1 D 
 ..D MES^XPDUTL(" ")
 ..D BMES^XPDUTL("****ERROR when attempting to add CAMP LEJEUNE TREATMENT")
 .K Y
 K DIC("DR"),DLAYGO,DIC,DIC(0),X
 Q
