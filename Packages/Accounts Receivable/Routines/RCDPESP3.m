RCDPESP3 ;BIRM/EWL - ePayment Lockbox Exclusion Audit Report ;Jun 11, 2014@13:00:05
 ;;4.5;Accounts Receivable;**298**;Nov 11, 2013;Build 121
 ;Per VA Directive 6402, this routine should not be modified.
RPT ; RUN THE RCDPE EXCLUSION AUDIT REPORT
 ;
 ; DESCRIPTION: This report is a simple listing of the RCDPE PARAMETER AUDIT file
 ;              including data concerning changes to the RCDPE AUTO PAY EXCLUSION file.
 ;
 ; GLOBALS:     ^RCY(344.7,              RCDPE PARAMETER AUDIT
 ;              ^RCY(344.6,              RCDPE AUTO PAY EXCLUSION
 ;              ^TMP("RCDPESP2",$J,      TMP FILE FOR LIST DIC OUTPUT
 ;
 ; INPUT PARAMETERS: NONE
 ;
 ; LOCAL VARIABLES: 
 ;    RCGET   - HOLDS POINTER TO TMP FILE RESULTS FROM LIST^DIC CALL
 ;    RCMSG   - HOLDS ERRORS FROM LIST^DIC
 ;    RCRUN   - DATE THE REPORT RAN
 ;    RCLINECT- LINE COUNTER
 ;    RCPAGE  - PAGE COUNTER
 ;    RCSTOP  - STOP DISPLAYING THE REPORT
 ;    RCIEN   - IEN OF CURRENT PAYER
 ;    RCPAYER - PAYER NAME
 ;    RCID    - PAYER ID
 ;    RCTIME  - TIMESTAMP PAYER WAS ADDED
 ;
 ; FOR REPORT FORMATTING
 ;    SPT     - TOTAL LINE SPACE - DATE & 2 SPACES 
 ;    SPI     - LINE SPACE AVAILABLE FOR PAYER ID
 ;    SPN     - LINE SPACE AVAILABLE FOR PAYER NAME
 ;    T1      - 1ST TAB STOP
 ;    T2      - 2ND TAB STOP
 N %ZIS,CT,RCGET,RCID,RCIEN,RCLINECT,RCPAGE,RCPAYER,RCRUNDT,RCSTOP,RCTIME,SPI,SPN,SPT,T1,T2
 ; FILEMAN VARIABLES
 N POP,X,X1,X2,Y,ZIS
 S (RCPAGE,RCSTOP,RCIEN,RCLINECT)=0
 ;
 ;Select output device
 S %ZIS="M" D ^%ZIS Q:POP  U IO
 ; SET UP PAGE FORMATTING
 I IOM<100 D
 . S SPT=IOM-10 ; SPACE AVAILABLE FOR PAYER ID AND PAYER NAME
 . S SPI=(SPT\3)-1 ; SPACE FOR PAYER ID
 . S SPN=SPT-SPI ; SPACE FOR PAYER NAME
 I IOM'<100 D
 . S SPT=90 ; SPACE AVAILABLE FOR PAYER ID AND PAYER NAME
 . S SPI=30 ; SPACE FOR PAYER ID
 . S SPN=60 ; SPACE FOR PAYER NAME
 S T1=SPI+1,T2=SPT+2
 ; 
 ; ********************************
 ; PROCESS THE PAYERS
 ; ********************************
 D HDR S RCLINECT=6
 F  S RCIEN=$O(^RCY(344.6,RCIEN)) Q:('RCIEN)!RCSTOP  D
 . S RCPAYER=$$GET1^DIQ(344.6,RCIEN_",",.01)
 . S RCID=$$GET1^DIQ(344.6,RCIEN_",",.02)
 . S RCTIME=$$FMTE^XLFDT($$GET1^DIQ(344.6,RCIEN_",",.03,"I"),"2D")
 . I $L($P(RCTIME,"/",1))=1 S $P(RCTIME,"/",1)="0"_$P(RCTIME,"/",1)
 . I $L($P(RCTIME,"/",2))=1 S $P(RCTIME,"/",2)="0"_$P(RCTIME,"/",2)
 . I RCLINECT+1>IOSL D HDR S RCLINECT=6
 . S RCLINECT=RCLINECT+1
 . W !,$E(RCID,1,SPI),?T1,$E(RCPAYER,1,SPN),?T2,RCTIME
 I 'RCSTOP D ASK^RCDPEARL()
 Q
 ;
HDR ; Report header
 ; LOCAL VARIABLES
 ; LN - SEPARATION LINE
 N LN
 I RCPAGE D ASK^RCDPEARL(.RCSTOP) Q:RCSTOP
 W @IOF
 S RCPAGE=RCPAGE+1 I RCPAGE=1 S RCRUNDT=$$FMTE^XLFDT($$NOW^XLFDT,2)
 W $$CNTR^RCDPESP2("PAYER EXCLUSION NAME / TIN REPORT"),?IOM-8,"Page: "_RCPAGE
 W !,$$CNTR^RCDPESP2("RUN DATE: "_RCRUNDT)
 W !!,"PAYER TIN",?T1,"PAYER NAME",?T2-2,"DATE ADDED"
 S $P(LN,"=",SPT+11)="" W !,LN
 Q
